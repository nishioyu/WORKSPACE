      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             ORCSONSHI002.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : オンライン資格確認
      *  コンポーネント名  : 限度額適用認定証・特定疾病療養受療証サブ
      *  返却エラーコード  : 
      *  管理者            : 
      *  作成日付    作業者        記述
      *  21/08/30    ORCAMO        新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      * 05.01.00     ORCAMO       22/12/XX  公費医療券画像確認対応など
      * 05.02.00     ORCAMO       24/02/XX  医療扶助対応など
      *****************************************************************
      *
       ENVIRONMENT             DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
      *
       DATA                    DIVISION.
      *FILE                        SECTION.
      *
      *
       WORKING-STORAGE             SECTION.
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-HKNNUM          PIC 9(01).
           03  FLG-ONSKAKU         PIC 9(01).
           03  FLG-HKNJAINF        PIC 9(01).
           03  FLG-ONSAIDLST       PIC 9(01).
      *
           03  FLG-OK              PIC 9(01).
      *2022/12
           03  FLG-GENNDO-ERR      PIC 9(01).
      *R05.2
           03  FLG-DOUI-NASI       PIC 9(01).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX                 PIC 9(04).
           03  IDY                 PIC 9(04).
           03  IDX3                PIC 9(04).
      *
           03  IDX-SK              PIC 9(04).
           03  IDX-SK2             PIC 9(04).
           03  IDX-K               PIC 9(04).
           03  IDX-GEN             PIC 9(04).
           03  IDX-MAX             PIC 9(04).
      *2024/01 医療扶助
           03  IDX-FUJ             PIC 9(04).
           03  IDX-AID             PIC 9(04).
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-SYMD.
               05  WRK-SYY             PIC 9(04).
               05  WRK-SMMDD.
                   07  WRK-SMM             PIC 9(02).
                   07  WRK-SDD             PIC 9(02).
      *
      *    限度額適用認定証提供
           03  WRK-GENDO               PIC X(200).
           03  WRK-GENDO-HEN           PIC X(200).
           03  WRK-SIKKAN-HEN          PIC X(200).
      *
           03  WRK-GENDO-SHO           PIC X(50).
           03  WRK-GENDO-TEK           PIC X(50).
           03  WRK-GENDO-STYMD         PIC X(09).
           03  WRK-GENDO-EDYMD         PIC X(09).
           03  WRK-GENDO-SKJGNSTYMD    PIC X(09).
      *
           03  WRK-SIKKAN              PIC X(50).
           03  WRK-SIKKAN-STYMD        PIC X(09).
           03  WRK-SIKKAN-EDYMD        PIC X(09).
           03  WRK-SIKKAN-FTNMONEY     PIC X(06).
           03  WRK-SIKKAN-FTNMONEY-9   PIC 9(06).
           03  WRK-ZZ9                 PIC ZZZZZ9.
      *
           03  WRK-SIKKAN-MSG          PIC X(200).
      *
           03  WRK-KOH-AREA.
               05  WRK-KOHNUM          PIC X(03).
               05  WRK-KOH-JKYSNUM     PIC X(20).
           03  WRK-TEISYO-AREA.
               05  WRK-TEISYO-KBN          PIC X(01).
               05  WRK-TEISYO-HKNTEKKBN    PIC X(01).
               05  WRK-TEISYO-KYOUKAI      PIC X(01).
               05  WRK-TEISYO-RRIFUK       PIC X(01).
      *
           03  WRK-KOH-TEKSTYMD        PIC X(08).
           03  WRK-KOH-TEKEDYMD        PIC X(08).
           03  WRK-HKN-TEKSTYMD        PIC X(08).
           03  WRK-HKN-TEKEDYMD        PIC X(08).
           03  WRK-MATU-DD             PIC X(02).
      *医療扶助
           03  WRK-FUJYO-HEN           PIC X(200).
      *
           03  WRK-MCP-PATHNAME        PIC X(64).
      *    医療券
           03  WRK-NYUGAIKBN           PIC X(04).
           03  WRK-SRYYM               PIC X(06).
      *
       01  TBL-ONS-AIDLST-AREA.
           03  TBL-ONS-AIDLST-OCC      OCCURS   10.
               07  TBL-TEISTYMD        PIC X(08).
               07  TBL-TEIEDYMD        PIC X(08).
               07  TBL-SRYYM           PIC X(06).
               07  TBL-KOUFUNUM        PIC X(15).
               07  TBL-NYUGAIKBN       PIC X(01).
      *
      *
       01  WRK-HENSYU-AREA.
           03  WRK-HENYMD.
               05  WRK-HGO         PIC X(01).
               05  WRK-HYY         PIC Z9.
               05  FILLER          PIC X(01)   VALUE  ".".
               05  WRK-HMM         PIC Z9.
               05  FILLER          PIC X(01)   VALUE  ".".
               05  WRK-HDD         PIC Z9.
      *
           03  WRK-HENTIME.
               05  WRK-THH         PIC 99.
               05  FILLER          PIC X(01)   VALUE  ":".
               05  WRK-TMM         PIC 99.
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *   日付変換サブ
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
      *
      *    数字変換領域
           COPY    "CPORCSNUM.INC".
      *
      *    全角チェックパラメタ
           COPY    "CPORCSKANACHK.INC".
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    共通領域
           COPY    "MCPAREA".
      *
      *    保険情報
      *01  HKNNUM-REC.
      *     COPY    "CPHKNNUM.INC".
      *
      *    オンライン資格確認照会結果テーブル
       01  ONSKAKU-REC.
           COPY    "CPONSHI-KAKU.INC".
      *
      *    保険者号マスタ
       01  HKNJAINF-REC.
           COPY    "CPHKNJAINF.INC".
      *    医療扶助　交付番号
       01  ONSHI-AIDLST-REC.
           COPY    "CPONSHI-AIDLST.INC".
      *
           COPY    "MCPDATA.INC".
      *
      *****************************************************************
      *    連絡　領域
      *****************************************************************
       LINKAGE                     SECTION.
      *
           COPY    "COMMON-SPA".
      *    限度額適用認定証など編集パラメタ
           COPY    "CPORCSONSHI002.INC".
      *    オンライン資格確認照会結果テーブル
       01  LNK-ONSKAKU-REC.
           COPY    "CPONSHI-KAKU.INC"
                                   REPLACING
                                   //ONSKAKU-// BY //LNK-ONSKAKU-//.
      *
       PROCEDURE                    DIVISION    USING
           SPA-AREA
           ORCSONSHI002AREA
           LNK-ONSKAKU-REC
           .
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                 SECTION.
      *
           INITIALIZE                  WRK-AREA
           INITIALIZE                  FLG-AREA
           INITIALIZE                  IDX-AREA
           INITIALIZE                  LNK-ONSHI002-OUT-AREA
           INITIALIZE                  LNK-ONSHI002-OUT02-AREA
           INITIALIZE                  LNK-ONSHI002-RC 
      *
      *    初期処理
           PERFORM 100-INIT-SEC
      *
           IF     (LNK-ONSHI002-RC     =   ZERO)
             AND  (LNK-ONSHI002-SYOKBN =   "01"
                                       OR  "03")
      *    オンライン資格確認名称編集
               PERFORM 210-ONSHIKAKU-HEN-SEC
           END-IF
      *
           IF     (LNK-ONSHI002-SYOKBN =   "02"
                                       OR  "03")
              AND (LNK-ONSHI002-RC     =   ZERO)
      *        オン資格公費内容変換
               PERFORM 220-ONSHIKAKU-KOHSYORI-SEC
           END-IF
      *2024/01
           IF     (LNK-ONSHI002-SYOKBN =   "02"
                                       OR  "03")
              AND (LNK-ONSHI002-RC     =   ZERO)
      *        オン資格医療扶助検索表示
               PERFORM 240-ONSHIKAKU-IRYOFUJYO-SEC
           END-IF
      *
           IF     (LNK-ONSHI002-SYOKBN =   "02"
                                       OR  "03")
              AND (LNK-ONSHI002-RC     =   ZERO)
      *        オン資格地方公費検索表示
               PERFORM 230-ONSHIKAKU-CHIHOU-SEC
           END-IF
      *    
      *
           .
       000-PROC-EXT.
           EXIT.
      *
           EXIT PROGRAM
           .
      *
      *****************************************************************
      *    初期処理
      *****************************************************************
       100-INIT-SEC                    SECTION.
      *
      *    処理区分
           IF       LNK-ONSHI002-SYOKBN    =   "01"
                                           OR  "02"
                                           OR  "03"
               CONTINUE
           ELSE
               MOVE    1                   TO  LNK-ONSHI002-RC
           END-IF
      *
           IF     (LNK-ONSKAKU-REC     =   SPACE )
               MOVE    1                   TO  LNK-ONSHI002-RC
           END-IF
           MOVE    LNK-ONSKAKU-REC     TO  ONSKAKU-REC
      *R05.2
      *    保険証確認の同意設定
           MOVE    ZERO                TO  FLG-DOUI-NASI
           IF      (ONSKAKU-SHOKAI-KBN         =   "2" )
               AND (ONSKAKU-ORCA-SHOKAI-KBN    =   "2" )
      *        保険証OCRは顔認証と同様
               CONTINUE
           ELSE
      *R04.1
      *    保険証確認の時は、患者登録で同意があること
               IF      (ONSKAKU-SHOKAI-KBN     =   "2")
                  AND  (LNK-ONSHI002-DOUIFLG   NOT =   "1")
                   MOVE    1                   TO  FLG-DOUI-NASI
               END-IF
           END-IF
           
           .
       100-INT-EXT.
           EXIT.
      *
      *****************************************************************
      *    オンライン資格確認編集処理
      *****************************************************************
       210-ONSHIKAKU-HEN-SEC           SECTION.
      *
           MOVE    SPACE               TO  WRK-GENDO
           MOVE    SPACE               TO  WRK-GENDO-HEN
           MOVE    SPACE               TO  WRK-SIKKAN
           MOVE    SPACE               TO  WRK-SIKKAN-HEN      
      *2024/01
      *    医療扶助
           IF     (ONSKAKU-CARD-CLASS  =   "A1"  )
               PERFORM 21013-FUJYO-HEN-SEC
               GO  TO  210-ONSHIKAKU-HEN-EXT
           END-IF
      *R04.1
      *    保険証確認の時は、患者登録で同意があること
      ***  IF      (ONSKAKU-SHOKAI-KBN     =   "2")
      ***     AND  (LNK-ONSHI002-DOUIFLG   NOT =   "1")
      *R05.2   同意なし
           IF      FLG-DOUI-NASI       =   1
               GO  TO  210-ONSHIKAKU-HEN-EXT
           END-IF
      *
      *    限度額適用認定証
           IF     (ONSKAKU-GENDO-DOUIFLG   =   "1" )
               PERFORM 21011-GENDO-HEN-SEC
           END-IF
      *
      *    特定疾病療養受療証提供
           IF     (ONSKAKU-SIKKAN-DOUIFLG  =   "1" )
               PERFORM 21012-SIKKAN-HEN-SEC
           END-IF
      *
           .
       210-ONSHIKAKU-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    限度額適用認定証編集処理
      *****************************************************************
       21011-GENDO-HEN-SEC              SECTION.
      *
           MOVE    SPACE               TO  WRK-GENDO-SHO
           MOVE    SPACE               TO  WRK-GENDO-TEK
           EVALUATE    ONSKAKU-GENDO-SHOKBN
               WHEN    "01"
                   MOVE    "限度額適用区分認定証"
                                           TO  WRK-GENDO-SHO
               WHEN    "02"
                   MOVE    "限度額適用・減額認定証"
                                           TO  WRK-GENDO-SHO
               WHEN    "03"
                   MOVE    "標準負担額減額認定証"
                                           TO  WRK-GENDO-SHO
               WHEN    OTHER
                   GO  TO  21011-GENDO-HEN-EXT
           END-EVALUATE
      *    限度額適用認定証適用区分
           EVALUATE    ONSKAKU-GENDO-TEKKBN
               WHEN    "A01"
                   MOVE    "ア"            TO  WRK-GENDO-TEK
               WHEN    "A02"
                   MOVE    "イ"            TO  WRK-GENDO-TEK
               WHEN    "A03"
                   MOVE    "ウ"            TO  WRK-GENDO-TEK
               WHEN    "A04"
                   MOVE    "エ"            TO  WRK-GENDO-TEK
               WHEN    "A05"
                   MOVE    "オ"            TO  WRK-GENDO-TEK
               WHEN    "A06"
                   MOVE    "オ（境）"      TO  WRK-GENDO-TEK
               WHEN    "B01"
                   MOVE    "現役並み３"    TO  WRK-GENDO-TEK
               WHEN    "B02"
                   MOVE    "現役並み２"    TO  WRK-GENDO-TEK
               WHEN    "B03"
                   MOVE    "現役並み１"    TO  WRK-GENDO-TEK
               WHEN    "B04"
                   MOVE    "一般"          TO  WRK-GENDO-TEK
               WHEN    "B05"
                   MOVE    "低所得２"      TO  WRK-GENDO-TEK
               WHEN    "B06"
                   MOVE    "低所得１"      TO  WRK-GENDO-TEK
               WHEN    "B07"
                   MOVE    "低所得１（老福）"  TO  WRK-GENDO-TEK
               WHEN    "B08"
                   MOVE    "低所得１（境）"    TO  WRK-GENDO-TEK
      *2022/9
               WHEN    "B09"
                   MOVE    "一般２"        TO  WRK-GENDO-TEK
               WHEN    "B10"
                   MOVE    "一般１"        TO  WRK-GENDO-TEK
           END-EVALUATE
           MOVE    ONSKAKU-GENDO-TEKSTYMD  TO  WRK-SYMD
           PERFORM 8001-SEIWA-YMDEDIT-SEC
           MOVE    WRK-HENYMD          TO  WRK-GENDO-STYMD
      *
           IF      ONSKAKU-GENDO-TEKEDYMD  =   SPACE
                                           OR  ZERO
               MOVE    ALL "9"             TO  WRK-SYMD
           ELSE
               MOVE    ONSKAKU-GENDO-TEKEDYMD  TO  WRK-SYMD
           END-IF
      *
           PERFORM 8001-SEIWA-YMDEDIT-SEC
           MOVE    WRK-HENYMD          TO  WRK-GENDO-EDYMD
      *
      *    限度額適用認定証長期入院該当年月日
           MOVE    ONSKAKU-GENDO-SKJGNSTYMD    TO  WRK-SYMD
           PERFORM 8001-SEIWA-YMDEDIT-SEC
           MOVE    WRK-HENYMD          TO  WRK-GENDO-SKJGNSTYMD
      *
           MOVE    SPACE               TO  WRK-GENDO-HEN
           STRING  ONSKAKU-GENDO-SHOKBN    DELIMITED  BY  SPACE
                   "："                    DELIMITED  BY  SPACE
                   WRK-GENDO-SHO           DELIMITED  BY  SPACE
                   "　"                    DELIMITED  BY  SPACE
                   ONSKAKU-GENDO-TEKKBN    DELIMITED  BY  SPACE
                   "："                    DELIMITED  BY  SPACE
                   WRK-GENDO-TEK           DELIMITED  BY  SPACE
                   "　"
                   WRK-GENDO-STYMD         DELIMITED  BY  SIZE
                   "〜"
                   WRK-GENDO-EDYMD         DELIMITED  BY  SIZE
                   "　長期入院該当年月日："
                   WRK-GENDO-SKJGNSTYMD    DELIMITED  BY  SIZE
                                           INTO  WRK-GENDO-HEN
           END-STRING
      *
      *    桁数取得
           INITIALIZE                  ORCSKANACHKAREA
           MOVE    "1"                 TO  KANACHK-SYORI
           MOVE    100                 TO  KANACHK-MAX-CNT
           MOVE    WRK-GENDO-HEN       TO  KANACHK-MAE-INPUT
           CALL    "ORCSKANACHK"       USING
                                           ORCSKANACHKAREA
           MOVE    KANACHK-MAX         TO  LNK-ONSHI002-GENDO-MAX
           MOVE    WRK-GENDO-HEN       TO  LNK-ONSHI002-GENDO-MSG
      *
           .
       21011-GENDO-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    特定疾病療養受療証提供編集処理
      *****************************************************************
       21012-SIKKAN-HEN-SEC              SECTION.
      *
           MOVE    SPACE                  TO  WRK-SIKKAN-MSG
           MOVE    ZERO                   TO  IDX-SK2
           PERFORM VARYING    IDX-SK      FROM    1   BY  1
                   UNTIL      IDX-SK      >   3
              IF      ONSKAKU-SIKKAN-SIPKBN(IDX-SK)   NOT =   SPACE
                   PERFORM 210121-SIKKAN-HENSYU-SEC
              END-IF
           END-PERFORM
           .
       21012-SIKKAN-HEN-EXT.
           EXIT.
      *
      *
      *****************************************************************
      *    特定疾病療養受療証提供編集処理
      *****************************************************************
       210121-SIKKAN-HENSYU-SEC          SECTION.
      *
           ADD     1               TO  IDX-SK2
           MOVE    SPACE           TO  WRK-SIKKAN
           MOVE    SPACE           TO  WRK-SIKKAN-FTNMONEY
           EVALUATE    ONSKAKU-SIKKAN-SIPKBN(IDX-SK)
               WHEN    "1"
                   MOVE    "人工透析　　　　"     TO  WRK-SIKKAN
               WHEN    "2"
                   MOVE    "血液凝固因子障害"     TO  WRK-SIKKAN
               WHEN    "3"
                   MOVE    "ＨＩＶ感染症　　"     TO  WRK-SIKKAN
           END-EVALUATE
      *
           MOVE    ONSKAKU-SIKKAN-TEKSTYMD (IDX-SK)
                                       TO  WRK-SYMD
           PERFORM 8001-SEIWA-YMDEDIT-SEC
           MOVE    WRK-HENYMD          TO  WRK-SIKKAN-STYMD
           IF      ONSKAKU-SIKKAN-TEKEDYMD (IDX-SK)    =   SPACE
                                                       OR  ZERO
               MOVE    ALL "9"             TO  WRK-SYMD
           ELSE
               MOVE    ONSKAKU-SIKKAN-TEKEDYMD (IDX-SK)
                                           TO  WRK-SYMD
           END-IF
      *
           PERFORM 8001-SEIWA-YMDEDIT-SEC
           MOVE    WRK-HENYMD          TO  WRK-SIKKAN-EDYMD
      *
           IF      ONSKAKU-SIKKAN-FTNMONEY(IDX-SK) NOT =   SPACE
               INITIALIZE                      ORCSNUMAREA
               MOVE    ONSKAKU-SIKKAN-FTNMONEY(IDX-SK) TO  SNUM-INX
               CALL    "ORCSNUM"           USING
                                           ORCSNUMAREA
               IF     (SNUM-RC         NOT =   ZERO  )   OR
                      (SNUM-MINKBN     NOT =   SPACE )   OR
                      (SNUM-SYOKBN     NOT =   SPACE )
                   MOVE    ONSKAKU-SIKKAN-FTNMONEY (IDX-SK)
                                           TO   WRK-SIKKAN-FTNMONEY
               ELSE
                   MOVE    SNUM-OUTNUM     TO  WRK-ZZ9
                   MOVE    WRK-ZZ9         TO  WRK-SIKKAN-FTNMONEY
               END-IF
           END-IF
      *
           STRING  ONSKAKU-SIKKAN-SIPKBN(IDX-SK)
                                           DELIMITED  BY  SPACE
                   "："                    DELIMITED  BY  SPACE
                   WRK-SIKKAN              DELIMITED  BY  SPACE
                   "　"                    DELIMITED  BY  SPACE
                   "　"
                   WRK-SIKKAN-STYMD         DELIMITED  BY  SIZE
                   "〜"
                   WRK-SIKKAN-EDYMD         DELIMITED  BY  SIZE
                   "　自己負担限度額："
                   WRK-SIKKAN-FTNMONEY      DELIMITED  BY  SIZE
                                            INTO  WRK-SIKKAN-MSG
           END-STRING
           INITIALIZE                  ORCSKANACHKAREA
           MOVE    "1"                 TO  KANACHK-SYORI
           MOVE    200                 TO  KANACHK-MAX-CNT
           MOVE    WRK-SIKKAN-MSG      TO  KANACHK-MAE-INPUT
           CALL    "ORCSKANACHK"       USING
                                           ORCSKANACHKAREA
           MOVE    KANACHK-MAX         TO  LNK-ONSHI002-SIKKAN-MAX
                                                   (IDX-SK2)
           MOVE    WRK-SIKKAN-MSG      TO  LNK-ONSHI002-SIKKAN-MSG
                                                   (IDX-SK2)
           .
       210121-SIKKAN-HENSYU-EXT.
           EXIT.
      *
      *2024/01 
      *****************************************************************
      *    オンライン資格確認　医療扶助内容編集処理
      *****************************************************************
       21013-FUJYO-HEN-SEC      SECTION.
      *
           MOVE    1                   TO  IDX-FUJ
           PERFORM 210131-FUJYO-MSGHEN-SEC
      *
      *    医療扶助で検索
           INITIALIZE                      ONSKAKU-REC
           MOVE    LNK-ONSKAKU-HOSPNUM     TO  ONSKAKU-HOSPNUM
           MOVE    LNK-ONSKAKU-FUJYO-UUID
                                       TO  ONSKAKU-FUJYO-UUID
           MOVE    LNK-ONSKAKU-KAKU-KAKUNINYMD
                                       TO  ONSKAKU-KAKU-KAKUNINYMD
           MOVE    ONSKAKU-REC         TO  MCPDATA-REC
           MOVE    "tbl_onshi_kaku"    TO  MCP-TABLE
           MOVE    "key27"             TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "key27"             TO  MCP-PATHNAME
               PERFORM 900-ONSKAKU-READ-SEC
           ELSE
               MOVE    1               TO  FLG-ONSKAKU
           END-IF
           PERFORM UNTIL       (FLG-ONSKAKU   =   1  )
                         OR    (IDX-FUJ       >=  2  )
      *        生年月日が同じこと
               IF     (LNK-ONSKAKU-RES-BIRTHDAY
                                           =   ONSKAKU-RES-BIRTHDAY)
      *          処理結果区分
                 AND  (ONSKAKU-RESULT-KBN  =   "1"  )
      *          医療扶助のみ
                 AND  (ONSKAKU-CARD-CLASS  =   "A1" )
                 AND  (ONSKAKU-TBL-UUID
                                       NOT =   LNK-ONSKAKU-TBL-UUID)
                   ADD     1                   TO  IDX-FUJ
                   PERFORM 210131-FUJYO-MSGHEN-SEC
               END-IF
               MOVE    "key27"             TO  MCP-PATHNAME
               PERFORM 900-ONSKAKU-READ-SEC
           END-PERFORM
      * 
           MOVE    "tbl_onshi_kaku"    TO  MCP-TABLE
           MOVE    "key27"             TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
           .
       21013-FUJYO-HEN-EXT.
           EXIT.
      *****************************************************************
      *    オンライン資格確認　医療扶助内容編集処理
      *****************************************************************
       210131-FUJYO-MSGHEN-SEC      SECTION.
      *
      *    加入者資格取得日
           MOVE    ONSKAKU-KOH-TEKSTYMD (1)
                                       TO  WRK-SYMD
           PERFORM 8001-SEIWA-YMDEDIT-SEC
           MOVE    WRK-HENYMD          TO  WRK-SIKKAN-STYMD
      *    加入者資格喪失日
           IF      ONSKAKU-KOH-TEKEDYMD (1)    =   SPACE
                                               OR  ZERO
               MOVE    ALL "9"             TO  WRK-SYMD
           ELSE
               MOVE    ONSKAKU-KOH-TEKEDYMD (1)
                                           TO  WRK-SYMD
           END-IF
           PERFORM 8001-SEIWA-YMDEDIT-SEC
           MOVE    WRK-HENYMD          TO  WRK-SIKKAN-EDYMD
      *
      *
           MOVE    SPACE               TO  WRK-FUJYO-HEN
      *
           STRING  "負担者番号："
                   ONSKAKU-RES-HKNJANUM    DELIMITED BY  SPACE
                   "　受給者番号："
                   ONSKAKU-RES-NUM         DELIMITED BY  SPACE
                   "　期間："
                   WRK-SIKKAN-STYMD        DELIMITED  BY  SIZE
                   "〜"
                   WRK-SIKKAN-EDYMD        DELIMITED  BY  SIZE
                                           INTO  WRK-FUJYO-HEN
           END-STRING
      *
           INITIALIZE                  ORCSKANACHKAREA
           MOVE    "1"                 TO  KANACHK-SYORI
           MOVE    200                 TO  KANACHK-MAX-CNT
           MOVE    WRK-FUJYO-HEN       TO  KANACHK-MAE-INPUT
           CALL    "ORCSKANACHK"       USING
                                           ORCSKANACHKAREA
           MOVE    KANACHK-MAX         TO  LNK-ONSHI002-FUJYO-MAX
                                                   (IDX-FUJ)
           MOVE    WRK-FUJYO-HEN       TO  LNK-ONSHI002-FUJYO-MSG
                                                   (IDX-FUJ)
      *
      *
           INITIALIZE                  TBL-ONS-AIDLST-AREA
           MOVE    ZERO                TO  IDX-AID
      *    交付番号の転記をするのみ
           IF      LNK-ONSHI002-KFTNKKBN   =   "1"
               CONTINUE
           ELSE
               GO  TO  210131-FUJYO-MSGHEN-EXT
           END-IF
      *
      *    医療券情報検索
           INITIALIZE                  ONSHI-AIDLST-REC
           MOVE    ONSKAKU-HOSPNUM     TO  ONS-AIDLST-HOSPNUM
           MOVE    ONSKAKU-FUJYO-UUID
                                       TO  ONS-AIDLST-FUJYO-UUID
           MOVE    ONSKAKU-RES-HKNJANUM
                                       TO  ONS-AIDLST-FTNJANUM
           MOVE    ONSKAKU-RES-NUM
                                       TO  ONS-AIDLST-JKYSNUM
      *
           MOVE    ONSHI-AIDLST-REC    TO  MCPDATA-REC
           MOVE    "tbl_onshi_aidlst"  TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "key2"              TO  MCP-PATHNAME
               PERFORM 900-ONSAIDLST-READ-SEC
           ELSE
               MOVE    1               TO  FLG-ONSAIDLST
           END-IF
           PERFORM UNTIL       (FLG-ONSAIDLST   =   1  )
                         OR    (IDX-AID         >=  10 )
                         OR    (IDX-FUJ         >=  10 )
      *        患者番号が一致しないと対象外
      *???? 更新まで判定保留
      *         IF     (LNK-ONSKAKU-PTID    NOT =   ZERO   )
      *         AND    (LNK-ONSKAKU-PTID    NOT =   ONS-AIDLST-PTID)
      *             CONTINUE
      *         ELSE
                   ADD     1               TO  IDX-AID
                   MOVE    ONS-AIDLST-TEKSTYMD
                                           TO  TBL-TEISTYMD (IDX-AID)
                   MOVE    ONS-AIDLST-TEKEDYMD
                                           TO  TBL-TEIEDYMD (IDX-AID)
                   MOVE    ONS-AIDLST-NYUGAIKBN
                                           TO  TBL-NYUGAIKBN (IDX-AID)
                   MOVE    ONS-AIDLST-SRYYM
                                           TO  TBL-SRYYM  (IDX-AID)
                   MOVE    ONS-AIDLST-KOFUNUM
                                           TO  TBL-KOUFUNUM (IDX-AID)
                   PERFORM 210131-ONSAIDLST-MSGHEN-SEC
      *         END-IF
               MOVE    "key2"             TO  MCP-PATHNAME
               PERFORM 900-ONSAIDLST-READ-SEC
           END-PERFORM
      * 
           MOVE    "tbl_onshi_aidlst"  TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
           .
       210131-FUJYO-MSGHEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    オンライン資格確認　公費内容編集処理
      *****************************************************************
       210131-ONSAIDLST-MSGHEN-SEC      SECTION.
      *
      *    有効開始年月日
           MOVE    ONS-AIDLST-TEKSTYMD
                                       TO  WRK-SYMD
           PERFORM 8001-SEIWA-YMDEDIT-SEC
           MOVE    WRK-HENYMD          TO  WRK-SIKKAN-STYMD
      *    有効終了日
           IF      ONS-AIDLST-TEKEDYMD =   SPACE
                                       OR  ZERO
               MOVE    ALL "9"             TO  WRK-SYMD
           ELSE
               MOVE    ONS-AIDLST-TEKEDYMD
                                           TO  WRK-SYMD
           END-IF
           PERFORM 8001-SEIWA-YMDEDIT-SEC
           MOVE    WRK-HENYMD          TO  WRK-SIKKAN-EDYMD
      *    診療年月
           MOVE    ONS-AIDLST-SRYYM
                                       TO  WRK-SYMD(1:6)
           MOVE    "01"                TO  WRK-SYMD(7:2)
           PERFORM 8001-SEIWA-YMDEDIT-SEC
           MOVE    WRK-HENYMD(1:6)     TO  WRK-SRYYM
      *
           EVALUATE    ONS-AIDLST-NYUGAIKBN
           WHEN    "1"
               MOVE    "入院"          TO  WRK-NYUGAIKBN
           WHEN    "2"
               MOVE    "外来"          TO  WRK-NYUGAIKBN
           END-EVALUATE
      *
      *
           MOVE    SPACE               TO  WRK-FUJYO-HEN
      *
           STRING  "　〔医療券"
                   "　交付番号："
                   ONS-AIDLST-KOFUNUM      DELIMITED  BY  SIZE
                   "　診療年月："
                   WRK-SRYYM               DELIMITED  BY  SIZE
                   "　"
                   WRK-NYUGAIKBN           DELIMITED BY  SPACE
                   "　期間："
                   WRK-SIKKAN-STYMD        DELIMITED  BY  SIZE
                   "〜"
                   WRK-SIKKAN-EDYMD        DELIMITED  BY  SIZE
                   "〕"                    DELIMITED  BY  SIZE
                                           INTO  WRK-FUJYO-HEN
           END-STRING
      *
           ADD     1                   TO  IDX-FUJ
           IF      IDX-FUJ             >   10
               GO  TO  210131-ONSAIDLST-MSGHEN-EXT
           END-IF
      *
           INITIALIZE                  ORCSKANACHKAREA
           MOVE    "1"                 TO  KANACHK-SYORI
           MOVE    200                 TO  KANACHK-MAX-CNT
           MOVE    WRK-FUJYO-HEN       TO  KANACHK-MAE-INPUT
           CALL    "ORCSKANACHK"       USING
                                           ORCSKANACHKAREA
           MOVE    KANACHK-MAX         TO  LNK-ONSHI002-FUJYO-MAX
                                                   (IDX-FUJ)
           MOVE    WRK-FUJYO-HEN       TO  LNK-ONSHI002-FUJYO-MSG
                                                   (IDX-FUJ)
      *
           .
       210131-ONSAIDLST-MSGHEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    オンライン資格確認　公費内容編集処理
      *****************************************************************
       220-ONSHIKAKU-KOHSYORI-SEC      SECTION.
      *
           MOVE    ZERO                TO  IDX-K
      *R04.1
      *    保険証確認の時は、患者登録で同意があること
      ***  IF      (ONSKAKU-SHOKAI-KBN     =   "2" )
      ***    AND   (LNK-ONSHI002-DOUIFLG   NOT =   "1" )
      *R05.2   同意なし
           IF      FLG-DOUI-NASI       =   1
               GO  TO  220-ONSHIKAKU-KOHSYORI-EXT
           END-IF
      *
      *    限度額適用認定証
           IF     (ONSKAKU-GENDO-DOUIFLG   =   "1" )
               PERFORM  2201-GENDO-KOHHEN-SEC
           END-IF
      *
      *    特定疾病療養受療証提供
           IF     (ONSKAKU-SIKKAN-DOUIFLG  =   "1" )
               PERFORM 2202-SIKKAN-KOHHEN-SEC
           END-IF
      *
           MOVE    IDX-K               TO   LNK-ONSHI002-KOHMAX
           .
       220-ONSHIKAKU-KOHSYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    限度額適用認定証　編集処理
      *****************************************************************
       2201-GENDO-KOHHEN-SEC           SECTION.
      *
           MOVE    SPACE               TO  WRK-TEISYO-AREA
           MOVE    SPACE               TO  WRK-KOH-AREA
      *
           MOVE    ZERO                TO  FLG-GENNDO-ERR
           EVALUATE    ONSKAKU-GENDO-SHOKBN
               WHEN    "01"
      *        限度額適用区分認定証
                    PERFORM 22011-GENDO01-KOHHEN-SEC
               WHEN    "02"
      *        限度額適用・減額認定証
                   PERFORM 22012-GENDO02-KOHHEN-SEC
               WHEN    "03"
      *        標準負担額減額認定証
                    PERFORM 22013-GENDO03-KOHHEN-SEC
           END-EVALUATE
      *
      *2022/12
           IF      FLG-GENNDO-ERR      =   1
               STRING    "〔限度額認定証の制度内容が確認できません。"
                         "保険者に照会して下さい〕"
                                    DELIMITED BY SIZE
                                    INTO  LNK-ONSHI002-GENDO-KEIMSG
               END-STRING
           END-IF
      *
      *    保険の資格取得日との判定
           IF      (ONSKAKU-ELDER-TEKSTYMD NOT =   SPACE)
               MOVE    ONSKAKU-ELDER-TEKSTYMD  TO  WRK-HKN-TEKSTYMD
               MOVE    ONSKAKU-ELDER-TEKEDYMD  TO  WRK-HKN-TEKEDYMD
           ELSE
               MOVE    ONSKAKU-TEKSTYMD        TO  WRK-HKN-TEKSTYMD
               MOVE    ONSKAKU-TEKEDYMD        TO  WRK-HKN-TEKEDYMD
           END-IF
           MOVE    ONSKAKU-GENDO-TEKSTYMD  TO  WRK-KOH-TEKSTYMD
           IF      (WRK-HKN-TEKSTYMD       =   ONSKAKU-GENDO-TEKSTYMD)
              OR   (ONSKAKU-GENDO-TEKSTYMD(7:2)    =   "01")
      *        開始日が同じか、１日始まり
               CONTINUE
           ELSE
      *        開始日が当月の時、１日
               IF      ONSKAKU-GENDO-TEKSTYMD(1:6) =   SPA-SYSYMD(1:6)
                   MOVE    "01"         TO  WRK-KOH-TEKSTYMD(7:2)
               END-IF
           END-IF
      *    終了日
           MOVE    ONSKAKU-GENDO-TEKEDYMD   TO  WRK-KOH-TEKEDYMD
      *    月末日
           MOVE    SPACE               TO  WRK-MATU-DD
           IF      ONSKAKU-GENDO-TEKEDYMD   =   SPACE
                                            OR  ZERO
                                            OR  ALL "9"
               MOVE    ALL "9"              TO  WRK-KOH-TEKEDYMD
           ELSE
      *       末日を求める
               INITIALIZE                      STS-AREA-DAY
               INITIALIZE                      LNK-DAY7-AREA
               MOVE    "71"                TO  LNK-DAY7-IRAI
               MOVE    WRK-KOH-TEKEDYMD(1:6)   TO  LNK-DAY7-YM
               CALL    "ORCSDAY"           USING   STS-AREA-DAY
                                                   LNK-DAY7-AREA
               IF      STS-DAY-RC1         =   ZERO
                   MOVE    LNK-DAY7-KOYOMI(7:2)    TO  WRK-MATU-DD
               END-IF
           END-IF
      *    保険の終了日と同じか、月末日
           IF     (ONSKAKU-GENDO-TEKEDYMD  =   WRK-HKN-TEKEDYMD)
              OR  (ONSKAKU-GENDO-TEKEDYMD(7:2) =   WRK-MATU-DD )
              OR  (WRK-KOH-TEKEDYMD        =   ALL "9"     )
              OR  (WRK-MATU-DD             =   SPACE   )
               CONTINUE
           ELSE
               MOVE    WRK-MATU-DD         TO  WRK-KOH-TEKEDYMD(7:2)
           END-IF
      *
      *    限度額適用認定証の日付
           MOVE    ONSKAKU-GENDO-TEKSTYMD  TO  LNK-ONSHI002-GENDO-STYMD
           MOVE    ONSKAKU-GENDO-TEKEDYMD  TO  LNK-ONSHI002-GENDO-EDYMD
      *    公費あり
           IF      WRK-KOHNUM      NOT =   SPACE
               ADD     1               TO  IDX-K
               MOVE    WRK-KOHNUM      TO  LNK-ONSHI002-KOHNUM(IDX-K)
               MOVE    WRK-KOH-JKYSNUM TO  LNK-ONSHI002-JKYSNUM
                                                              (IDX-K)
               MOVE    WRK-KOH-TEKSTYMD
                                       TO   LNK-ONSHI002-TEKSTYMD
                                                              (IDX-K)
               MOVE    WRK-KOH-TEKEDYMD
                                       TO   LNK-ONSHI002-TEKEDYMD
                                                              (IDX-K)
      *        公費あり
               MOVE    "1"             TO  LNK-ONSHI002-KOH-ARI
           END-IF
      *    低所得１
           IF      WRK-TEISYO-KBN      =   "1"
               MOVE    WRK-KOH-TEKSTYMD
                                       TO   LNK-ONSHI002-STK1-TEKSTYMD
               MOVE    WRK-KOH-TEKEDYMD
                                       TO   LNK-ONSHI002-STK1-TEKEDYMD
      *        認定範囲区分
               MOVE    WRK-TEISYO-HKNTEKKBN
                                       TO  LNK-ONSHI002-STK1-HKNTEKKBN
      *        老齢福祉年金受給者証
               MOVE    WRK-TEISYO-RRIFUK
                                       TO  LNK-ONSHI002-STK1-RRIFUK
      *        境界層該当区分
               MOVE    WRK-TEISYO-KYOUKAI
                                       TO  LNK-ONSHI002-STK1-KYOUKAI
      *        低所得あり
               IF      LNK-ONSHI002-KOH-ARI    =   SPACE
                   MOVE    "2"             TO  LNK-ONSHI002-KOH-ARI
               END-IF
           END-IF
      *    低所得２
           IF      WRK-TEISYO-KBN      =   "2"
               MOVE    WRK-KOH-TEKSTYMD
                                       TO   LNK-ONSHI002-STK2-TEKSTYMD
               MOVE    WRK-KOH-TEKEDYMD
                                       TO   LNK-ONSHI002-STK2-TEKEDYMD
      *        認定範囲区分
               MOVE    WRK-TEISYO-HKNTEKKBN
                                       TO  LNK-ONSHI002-STK2-HKNTEKKBN
      *        長期入院該当年月日
               MOVE    ONSKAKU-GENDO-SKJGNSTYMD
                                       TO  LNK-ONSHI002-STK2-SKJGNSTYMD
      *        境界層該当区分
               MOVE    WRK-TEISYO-KYOUKAI
                                       TO  LNK-ONSHI002-STK2-KYOUKAI
      *        低所得あり
               IF      LNK-ONSHI002-KOH-ARI    =   SPACE
                   MOVE    "2"             TO  LNK-ONSHI002-KOH-ARI
               END-IF
           END-IF
           .
        2201-GENDO-KOHHEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    限度額適用区分認定証　編集処理
      *****************************************************************
       22011-GENDO01-KOHHEN-SEC           SECTION.
      *
      *    限度額適用認定証適用区分
           EVALUATE    ONSKAKU-GENDO-TEKKBN
               WHEN    "A01"
                   MOVE    "966"           TO  WRK-KOHNUM
                   MOVE    "ア"            TO  WRK-KOH-JKYSNUM
               WHEN    "A02"
                   MOVE    "966"           TO  WRK-KOHNUM
                   MOVE    "イ"            TO  WRK-KOH-JKYSNUM
               WHEN    "A03"
                   MOVE    "967"           TO  WRK-KOHNUM
                   MOVE    "ウ"            TO  WRK-KOH-JKYSNUM
               WHEN    "A04"
                   MOVE    "967"           TO  WRK-KOHNUM
                   MOVE    "エ"            TO  WRK-KOH-JKYSNUM
               WHEN    "B01"
      *            現役並み３
                   CONTINUE
               WHEN    "B02"
      *            現役並み３
                   MOVE    "946"           TO  WRK-KOHNUM
                   MOVE    "２"            TO  WRK-KOH-JKYSNUM
               WHEN    "B03"
      *            現役並み３
                   MOVE    "946"           TO  WRK-KOHNUM
                   MOVE    "１"            TO  WRK-KOH-JKYSNUM
               WHEN    "B04"
      *            一般
                   CONTINUE
               WHEN    "B09"
      *            一般２
                   CONTINUE
               WHEN    "B10"
      *            一般１
                   CONTINUE
      *2022/12
               WHEN    OTHER
                   IF      ONSKAKU-GENDO-TEKKBN    NOT =   SPACE
                        MOVE    1              TO  FLG-GENNDO-ERR
                   END-IF
           END-EVALUATE
           .
       22011-GENDO01-KOHHEN-EXT.
           EXIT.
      *****************************************************************
      *    限度額適用・減額認定証　編集処理
      *****************************************************************
       22012-GENDO02-KOHHEN-SEC              SECTION.
      *
      *    限度額適用認定証適用区分
           EVALUATE    ONSKAKU-GENDO-TEKKBN
                WHEN    "A05"
      *         オ 
                   MOVE    "967"           TO  WRK-KOHNUM
                   MOVE    "オ"            TO  WRK-KOH-JKYSNUM
      *            低所得２ 
                   MOVE    "2"             TO  WRK-TEISYO-KBN
                   MOVE    "0"             TO  WRK-TEISYO-KYOUKAI
                   MOVE    "0"             TO  WRK-TEISYO-HKNTEKKBN
                WHEN    "A06"
      *         オ（境）
                   MOVE    "967"           TO  WRK-KOHNUM
                   MOVE    "オ"            TO  WRK-KOH-JKYSNUM
      *            低所得２ 
                   MOVE    "2"             TO  WRK-TEISYO-KBN
      *            境界層
                   MOVE    "1"             TO  WRK-TEISYO-KYOUKAI
      *            認定範囲
                   MOVE    "0"             TO  WRK-TEISYO-HKNTEKKBN
               WHEN    "B05"
      *            低所得２
                   MOVE    "2"             TO  WRK-TEISYO-KBN
      *            境界層
                   MOVE    "0"             TO  WRK-TEISYO-KYOUKAI
      *            認定範囲
                   MOVE    "0"             TO  WRK-TEISYO-HKNTEKKBN
              WHEN    "B06"
      *            低所得１
                   MOVE    "1"             TO  WRK-TEISYO-KBN
      *            認定範囲
                   MOVE    "0"             TO  WRK-TEISYO-HKNTEKKBN
      *            境界層
                   MOVE    "0"             TO  WRK-TEISYO-KYOUKAI
      *            老齢福祉年金受給者証
                   MOVE    "0"             TO  WRK-TEISYO-RRIFUK 
               WHEN    "B07"
      *            低所得１（老福）
                   MOVE    "1"             TO  WRK-TEISYO-KBN
      *            認定範囲
                   MOVE    "0"             TO  WRK-TEISYO-HKNTEKKBN
      *            境界層
                   MOVE    "0"             TO  WRK-TEISYO-KYOUKAI
      *            老齢福祉年金受給者証
                   MOVE    "1"             TO  WRK-TEISYO-RRIFUK
               WHEN    "B08"
      *            低所得１（境）
                   MOVE    "1"             TO  WRK-TEISYO-KBN
      *            認定範囲
                   MOVE    "0"             TO  WRK-TEISYO-HKNTEKKBN
      *            境界層
                   MOVE    "1"             TO  WRK-TEISYO-KYOUKAI
      *            老齢福祉年金受給者証
                   MOVE    "0"             TO  WRK-TEISYO-RRIFUK
      *2022/12
               WHEN    OTHER
                   IF      ONSKAKU-GENDO-TEKKBN    NOT =   SPACE
                        MOVE    1              TO  FLG-GENNDO-ERR
                   END-IF
           END-EVALUATE
           .
       22012-GENDO02-KOHHEN-EXT.
           EXIT.
      *****************************************************************
      *    標準負担額減額認定証　編集処理
      *****************************************************************
       22013-GENDO03-KOHHEN-SEC              SECTION.
      *
      *    限度額適用認定証適用区分
           EVALUATE    ONSKAKU-GENDO-TEKKBN
                WHEN    "A05"
      *         オ 
      *            低所得２ 
                   MOVE    "2"             TO  WRK-TEISYO-KBN
      *            認定範囲
                   MOVE    "3"             TO  WRK-TEISYO-HKNTEKKBN
                   MOVE    "0"             TO  WRK-TEISYO-KYOUKAI
               WHEN    "A06"
      *         オ（境）
      *            低所得２ 
                   MOVE    "2"             TO  WRK-TEISYO-KBN
      *            境界層
                   MOVE    "1"             TO  WRK-TEISYO-KYOUKAI
      *            認定範囲
                   MOVE    "3"             TO  WRK-TEISYO-HKNTEKKBN
               WHEN    "B05"
      *            低所得２
                   MOVE    "2"             TO  WRK-TEISYO-KBN
      *            境界層
                   MOVE    "0"             TO  WRK-TEISYO-KYOUKAI
      *            認定範囲
                   MOVE    "3"             TO  WRK-TEISYO-HKNTEKKBN
              WHEN    "B06"
      *            低所得１
                   MOVE    "1"             TO  WRK-TEISYO-KBN
      *            認定範囲
                   MOVE    "3"             TO  WRK-TEISYO-HKNTEKKBN
      *            境界層
                   MOVE    "0"             TO  WRK-TEISYO-KYOUKAI
      *            老齢福祉年金受給者証
                   MOVE    "0"             TO  WRK-TEISYO-RRIFUK 
               WHEN    "B07"
      *            低所得１（老福）
                   MOVE    "1"             TO  WRK-TEISYO-KBN
      *            認定範囲
                   MOVE    "3"             TO  WRK-TEISYO-HKNTEKKBN
      *            境界層
                   MOVE    "0"             TO  WRK-TEISYO-KYOUKAI
      *            老齢福祉年金受給者証
                   MOVE    "1"             TO  WRK-TEISYO-RRIFUK
               WHEN    "B08"
      *            低所得１（境）
                   MOVE    "1"             TO  WRK-TEISYO-KBN
      *            認定範囲
                   MOVE    "3"             TO  WRK-TEISYO-HKNTEKKBN
      *            境界層
                   MOVE    "1"             TO  WRK-TEISYO-KYOUKAI
      *            老齢福祉年金受給者証
                   MOVE    "0"             TO  WRK-TEISYO-RRIFUK
      *2022/12
               WHEN    OTHER
                   IF      ONSKAKU-GENDO-TEKKBN    NOT =   SPACE
                        MOVE    1              TO  FLG-GENNDO-ERR
                   END-IF
           END-EVALUATE
           .
       22013-GENDO03-KOHHEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    特定疾病療養受療証提供 公費編集処理
      *****************************************************************
       2202-SIKKAN-KOHHEN-SEC        SECTION.
      * 
           MOVE    ZERO                   TO  IDX-SK2
           PERFORM VARYING    IDX-SK      FROM    1   BY  1
                   UNTIL      IDX-SK      >   3
              IF     (ONSKAKU-SIKKAN-SIPKBN(IDX-SK)   NOT =   SPACE )
               AND   (ONSKAKU-SIKKAN-FTNMONEY(IDX-SK) NOT =   SPACE )
                   MOVE    SPACE               TO  WRK-KOH-AREA
      *            数値変換
                   INITIALIZE                      ORCSNUMAREA
                   MOVE    ONSKAKU-SIKKAN-FTNMONEY(IDX-SK)
                                               TO  SNUM-INX
                   CALL    "ORCSNUM"           USING
                                               ORCSNUMAREA
                   IF     (SNUM-RC         NOT =   ZERO  )   OR
                          (SNUM-MINKBN     NOT =   SPACE )   OR
                          (SNUM-SYOKBN     NOT =   SPACE )
                       CONTINUE
                   ELSE
                       IF      SNUM-OUTNUM      =   10000
                           MOVE    "972"        TO  WRK-KOHNUM
                       END-IF
                       IF      SNUM-OUTNUM      =   20000
                           MOVE    "974"        TO  WRK-KOHNUM
                       END-IF
                   END-IF
                   IF      WRK-KOHNUM      NOT =   SPACE
                       ADD     1               TO  IDX-K
                       MOVE    WRK-KOHNUM      TO  LNK-ONSHI002-KOHNUM
                                                             (IDX-K)
                       MOVE    ONSKAKU-SIKKAN-TEKSTYMD (IDX-SK)
                                           TO  LNK-ONSHI002-TEKSTYMD
                                                              (IDX-K)
                       IF      ONSKAKU-SIKKAN-TEKEDYMD (IDX-SK)
                                               =   SPACE
                           MOVE    ALL "9"
                                           TO  LNK-ONSHI002-TEKEDYMD
                                                              (IDX-K)
                       ELSE
                           MOVE    ONSKAKU-SIKKAN-TEKEDYMD (IDX-SK)
                                           TO  LNK-ONSHI002-TEKEDYMD
                                                              (IDX-K)
                       END-IF
                       MOVE    "1"         TO  LNK-ONSHI002-SIPKBN
                                                              (IDX-K)
      *                公費は１件とする
                       MOVE    3           TO  IDX-SK
                   END-IF
               END-IF
           END-PERFORM
      *
           .
       2202-SIKKAN-KOHHEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    特定疾病療養受療証提供 公費編集処理
      *****************************************************************
       230-ONSHIKAKU-CHIHOU-SEC        SECTION.
      *
      *    地方公費の検索
      *    オンライン資格確認UID検索処理
           INITIALIZE                      ONSKAKU-REC
           MOVE    LNK-ONSKAKU-HOSPNUM     TO  ONSKAKU-HOSPNUM
           MOVE    LNK-ONSKAKU-TBL-UUID
                                       TO  ONSKAKU-KOUHI-UUID
      *2024/01
      *    医療扶助
           IF     (LNK-ONSKAKU-CARD-CLASS  =   "A1"  )
              MOVE    LNK-ONSKAKU-FUJYO-UUID
                                       TO  ONSKAKU-KOUHI-UUID
           END-IF
      *
      *    照会区分＝９　が地方公費
      *     MOVE    "9"                 TO  ONSKAKU-SHOKAI-KBN
      **    kouhi-uuidでのみ検索へ
           MOVE    ONSKAKU-REC         TO  MCPDATA-REC
           MOVE    "tbl_onshi_kaku"    TO  MCP-TABLE
      ***  MOVE    "key12"             TO  MCP-PATHNAME
           MOVE    "key13"             TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "key13"             TO  MCP-PATHNAME
               PERFORM 900-ONSKAKU-READ-SEC
           ELSE
               MOVE    1               TO  FLG-ONSKAKU
           END-IF
           PERFORM UNTIL       (FLG-ONSKAKU   =   1  )
                         OR    (IDX-K         >=  10  )
      *        保険の生年月日と同じこと
               IF     (LNK-ONSKAKU-RES-BIRTHDAY
                                           =   ONSKAKU-RES-BIRTHDAY)
      *          処理結果区分
                 AND  (ONSKAKU-RESULT-KBN  =   "1"  )
      *          公費照会のみ
                 AND  (ONSKAKU-SHOKAI-KBN  =   "8"
                                           OR  "9"  )
      *2022/12     公費画像画面から対象外設定
                 IF     ONSKAKU-RES-HONKZKKBN  =   "9"
                   CONTINUE
                 ELSE 
      ****
                   ADD     1               TO  IDX-K
                   MOVE    IDX-K           TO  LNK-ONSHI002-KOHMAX
      *            負担者番号
                   MOVE     ONSKAKU-RES-HKNJANUM
                                           TO   LNK-ONSHI002-FTNJANUM
                                                               (IDX-K)
      *            負担者番号から公費の種類決定
                   PERFORM 2301-FTNJANUM-KOHNUM-SEC
      *            保険者番号あり
                   IF      FLG-HKNJAINF        =   ZERO
                       MOVE    HKNJA-HKNNUM    TO  LNK-ONSHI002-KOHNUM
                                                         (IDX-K)
                   END-IF
      *            受給者番号
                   MOVE     ONSKAKU-RES-NUM
                                           TO  LNK-ONSHI002-JKYSNUM
                                                             (IDX-K)
      *            資格取得日
                   MOVE    ONSKAKU-KOH-TEKSTYMD (1)
                                           TO  LNK-ONSHI002-TEKSTYMD
                                                              (IDX-K)

                   IF     (ONSKAKU-KOH-TEKSTYMD(2)  NOT =   SPACE )
                      AND (ONSKAKU-KOH-TEKSTYMD(1)
                                           >   ONSKAKU-KOH-TEKSTYMD(2))
      *               小さい方を開始日
                       MOVE    ONSKAKU-KOH-TEKSTYMD (2)
                                           TO  LNK-ONSHI002-TEKSTYMD
                                                              (IDX-K)
                   END-IF
      *            終了日
                   MOVE    ONSKAKU-KOH-TEKEDYMD(1)
                                           TO  LNK-ONSHI002-TEKEDYMD
                                                             (IDX-K)
                   IF     (ONSKAKU-KOH-TEKEDYMD(2)  NOT =   SPACE )
                     AND  (ONSKAKU-KOH-TEKEDYMD(1)  NOT =   SPACE )
                     AND  (ONSKAKU-KOH-TEKEDYMD(1)
                                           <   ONSKAKU-KOH-TEKEDYMD(2))
                       MOVE    ONSKAKU-KOH-TEKEDYMD(2)
                                           TO  LNK-ONSHI002-TEKEDYMD
                                                             (IDX-K)
                   END-IF
                   IF      LNK-ONSHI002-TEKEDYMD(IDX-K)
                                           =   SPACE
                       MOVE    ALL "9"
                                           TO  LNK-ONSHI002-TEKEDYMD
                                                               (IDX-K)
                   END-IF
      *            地方公費区分
                   MOVE    "1"             TO  LNK-ONSHI002-CHIKOU
                                                              (IDX-K)
      *            UUID
                   MOVE    ONSKAKU-TBL-UUID
                                           TO  LNK-ONSHI002-KOHUUID
                                                             (IDX-K)
      *            地方公費あり
                   MOVE    "3"             TO  LNK-ONSHI002-KOH-ARI
      ****
                END-IF
      ****
               END-IF
               MOVE    "key13"             TO  MCP-PATHNAME
               PERFORM 900-ONSKAKU-READ-SEC
           END-PERFORM
      * 
           MOVE    "tbl_onshi_kaku"    TO  MCP-TABLE
           MOVE    "key13"             TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           .
       230-ONSHIKAKU-CHIHOU-EXT.
           EXIT.
      *
      *****************************************************************
      *    負担者番号から公費の種類決定
      *****************************************************************
       2301-FTNJANUM-KOHNUM-SEC        SECTION.
      *
      *    保険者番号　検索チェック
           INITIALIZE                    HKNJAINF-REC
           MOVE    SPA-HOSPNUM           TO  HKNJA-HOSPNUM
           MOVE    ONSKAKU-RES-HKNJANUM  TO  HKNJA-HKNJANUM
      *
           MOVE    HKNJAINF-REC          TO  MCPDATA-REC
           MOVE    "tbl_hknjainf"        TO  MCP-TABLE
           MOVE    "key"                 TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_hknjainf"        TO  MCP-TABLE
               MOVE    "key"                 TO  MCP-PATHNAME
               PERFORM 900-HKNJAINF-READ-SEC
           ELSE
               INITIALIZE                  HKNJAINF-REC
               MOVE    1               TO  FLG-HKNJAINF
           END-IF
           MOVE    "tbl_hknjainf"      TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
      *    保険者番号あり
      *     IF      FLG-HKNJAINF        =   ZERO
      *         MOVE    HKNJA-HKNNUM        TO  LNK-ONSHI002-KOHNUM
      *                                                   (IDX-K)
      *     END-IF
           .
      *
       2301-FTNJANUM-KOHNUM-EXT.
           EXIT.
      *
      *2024/01
      *****************************************************************
      *    オン資格医療扶助検索表示 公費編集処理
      *****************************************************************
       240-ONSHIKAKU-IRYOFUJYO-SEC        SECTION.
      *
      *    MSGを再編集
           MOVE    ZERO                TO  IDX-FUJ
           INITIALIZE                  LNK-ONSHI002-FUJYO-G
      *    選択が医療扶助の時
           IF     (LNK-ONSKAKU-CARD-CLASS  =   "A1"  )
               MOVE    LNK-ONSKAKU-REC     TO  ONSKAKU-REC
               PERFORM 2401-KOHFUJYO-HEN-SEC
           END-IF
      *    医療扶助検索
           INITIALIZE                      ONSKAKU-REC
           MOVE    LNK-ONSKAKU-HOSPNUM     TO  ONSKAKU-HOSPNUM
      *?????
           IF     (LNK-ONSKAKU-CARD-CLASS  =   "A1"  )
      *        FUJYO-UUID で検索
               MOVE    LNK-ONSKAKU-FUJYO-UUID
                                           TO  ONSKAKU-FUJYO-UUID
               MOVE    LNK-ONSKAKU-KAKU-KAKUNINYMD
                                           TO  ONSKAKU-KAKU-KAKUNINYMD
               MOVE    "key27"             TO  WRK-MCP-PATHNAME
           ELSE
      *        KOUHI_UUID で医療扶助を検索
               MOVE    LNK-ONSKAKU-TBL-UUID
                                           TO  ONSKAKU-KOUHI-UUID
               MOVE    "key13"             TO  WRK-MCP-PATHNAME
           END-IF
           MOVE    ONSKAKU-REC         TO  MCPDATA-REC
           MOVE    "tbl_onshi_kaku"    TO  MCP-TABLE
           MOVE    WRK-MCP-PATHNAME    TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    WRK-MCP-PATHNAME    TO  MCP-PATHNAME
               PERFORM 900-ONSKAKU-READ-SEC
           ELSE
               MOVE    1               TO  FLG-ONSKAKU
           END-IF
           PERFORM UNTIL       (FLG-ONSKAKU   =   1  )
                         OR    (IDX-K         >=  10  )
      *        生年月日と同じこと
               IF     (LNK-ONSKAKU-RES-BIRTHDAY
                                           =   ONSKAKU-RES-BIRTHDAY)
      *          処理結果区分
                 AND  (ONSKAKU-RESULT-KBN  =   "1"  )
      *          医療扶助のみ
                 AND  (ONSKAKU-CARD-CLASS  =   "A1" )
                 AND  (ONSKAKU-TBL-UUID
                                       NOT =   LNK-ONSKAKU-TBL-UUID)
      *          医療扶助対応後
                 AND  (ONSKAKU-FUJYO-UUID
                                       NOT =   SPACE )
                   PERFORM 2401-KOHFUJYO-HEN-SEC
               END-IF
               MOVE    WRK-MCP-PATHNAME    TO  MCP-PATHNAME
               PERFORM 900-ONSKAKU-READ-SEC
           END-PERFORM
      * 
           MOVE    "tbl_onshi_kaku"    TO  MCP-TABLE
           MOVE    WRK-MCP-PATHNAME    TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           .
       240-ONSHIKAKU-IRYOFUJYO-EXT.
           EXIT.
      *****************************************************************
      *    医療扶助公費編集処理
      *****************************************************************
       2401-KOHFUJYO-HEN-SEC          SECTION.
      *
           ADD     1                   TO  IDX-K
           MOVE    IDX-K               TO  LNK-ONSHI002-KOHMAX
      *    負担者番号
           MOVE     ONSKAKU-RES-HKNJANUM
                                       TO   LNK-ONSHI002-FTNJANUM
                                                               (IDX-K)
      *    負担者番号から公費の種類決定
           PERFORM 2301-FTNJANUM-KOHNUM-SEC
      *    保険者番号あり
           IF      FLG-HKNJAINF        =   ZERO
               MOVE    HKNJA-HKNNUM    TO  LNK-ONSHI002-KOHNUM
                                                         (IDX-K)
           ELSE
      *        生活保護とする
               IF      ONSKAKU-RES-HKNJANUM(1:2)   =   "12"
                   MOVE    "012"           TO  LNK-ONSHI002-KOHNUM
                                                         (IDX-K)
               END-IF
           END-IF
      *    受給者番号
           MOVE     ONSKAKU-RES-NUM    TO  LNK-ONSHI002-JKYSNUM
                                                             (IDX-K)
      *    資格取得日
           MOVE    ONSKAKU-KOH-TEKSTYMD (1)
                                       TO  LNK-ONSHI002-TEKSTYMD
                                                              (IDX-K)
      *    終了日
           MOVE    ONSKAKU-KOH-TEKEDYMD(1)
                                       TO  LNK-ONSHI002-TEKEDYMD
                                                             (IDX-K)
           IF      LNK-ONSHI002-TEKEDYMD(IDX-K)
                                       =   SPACE
               MOVE    ALL "9"             TO  LNK-ONSHI002-TEKEDYMD
                                                               (IDX-K)
           END-IF
      *    医療扶助区分
           MOVE    "4"                 TO  LNK-ONSHI002-CHIKOU
                                                              (IDX-K)
      *    UUID
           MOVE    ONSKAKU-TBL-UUID
                                       TO  LNK-ONSHI002-KOHUUID
                                                             (IDX-K)
      *    医療扶助あり
           MOVE    "4"             TO  LNK-ONSHI002-KOH-ARI
      *    医療扶助あり
           MOVE    "1"             TO  LNK-ONSHI002-FUJ-ARI
      *
      *
           ADD     1               TO  IDX-FUJ
           PERFORM 210131-FUJYO-MSGHEN-SEC
           PERFORM VARYING     IDX3    FROM    1   BY  1
                   UNTIL       IDX3    >   IDX-AID 
               MOVE    TBL-KOUFUNUM (IDX3)
                                       TO  LNK-ONSHI002-KOFUNUM
                                                          (IDX-K IDX3)
               MOVE    TBL-NYUGAIKBN (IDX3)
                                       TO  LNK-ONSHI002-NYUGAIKBN
                                                          (IDX-K IDX3)
               MOVE    TBL-SRYYM (IDX3)
                                       TO  LNK-ONSHI002-SRYYM
                                                          (IDX-K IDX3)
           END-PERFORM
           .
       240-ONSHIKAKU-IRYOFUJYO-EXT.
           EXIT.
      *****************************************************************
      *    西暦→和暦FORMAT日付編集
      *****************************************************************
       8001-SEIWA-YMDEDIT-SEC          SECTION.
      *
           IF      WRK-SYMD            =   SPACE
                                       OR  ZERO
                                       OR  ALL "9"
               MOVE    WRK-SYMD            TO  WRK-HENYMD
           ELSE
               MOVE    SPACE               TO  STS-AREA-DAY
               INITIALIZE                      STS-AREA-DAY
               MOVE    SPACE               TO  LNK-DAY2-AREA
               MOVE    "21"                TO  LNK-DAY2-IRAI
               MOVE    WRK-SYMD            TO  LNK-DAY2-YMD
               CALL    "ORCSDAY"       USING   STS-AREA-DAY
                                               LNK-DAY2-AREA
               IF      STS-DAY-RC1             =   ZERO
                   MOVE    LNK-DAY2-EDTYMD1    TO  WRK-HENYMD
               ELSE
                   MOVE    SPACE               TO  WRK-HENYMD
               END-IF
           END-IF
           .
       8001-SEIWA-YMDEDIT-EXT.
           EXIT.
      *
      *****************************************************************
      *    オンライン資格確認照会結果読込
      *****************************************************************
       900-ONSKAKU-READ-SEC         SECTION.
      *
           MOVE    "tbl_onshi_kaku"    TO  MCP-TABLE
           PERFORM 920-DBFETCH-SEC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC     TO  ONSKAKU-REC
               MOVE    ZERO            TO  FLG-ONSKAKU
           ELSE
               INITIALIZE                  ONSKAKU-REC
               MOVE    1               TO  FLG-ONSKAKU
           END-IF
           .
       900-ONSKAKU-READ-EXT.
           EXIT.
      *****************************************************************
      *    保険者番号マスタ次検索処理
      *****************************************************************
       900-HKNJAINF-READ-SEC              SECTION.
      *
           MOVE    "tbl_hknjainf"      TO  MCP-TABLE
      *
           PERFORM 920-DBFETCH-SEC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  HKNJAINF-REC
               MOVE    ZERO                TO  FLG-HKNJAINF
           ELSE
               INITIALIZE                  HKNJAINF-REC
               MOVE    1                   TO  FLG-HKNJAINF
           END-IF
           .
       900-HKNJAINF-READ-EXT.
           EXIT.
      *****************************************************************
      *    保険者番号マスタ次検索処理
      *****************************************************************
       900-ONSAIDLST-READ-SEC              SECTION.
      *
           MOVE    "tbl_onshi_aidlst"  TO  MCP-TABLE
           PERFORM 920-DBFETCH-SEC
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  ONSHI-AIDLST-REC
               MOVE    ZERO                TO  FLG-ONSAIDLST
           ELSE
               INITIALIZE                  ONSHI-AIDLST-REC
               MOVE    1                   TO  FLG-ONSAIDLST
           END-IF
           .
       900-ONSAIDLST-READ-EXT.
           EXIT.
      *****************************************************************
      *    テーブル検索処理
      *****************************************************************
       910-DBSELECT-SEC                SECTION.
      *
           MOVE    "DBSELECT"          TO  MCP-FUNC
           CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
       910-DBSELECT-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢ読込み処理
      *****************************************************************
       920-DBFETCH-SEC         SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
       920-DBFETCH-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢクロース処理
      *****************************************************************
       990-DBCLOSE-SEC         SECTION.
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
       990-DBCLOSE-EXT.
           EXIT.
      *
