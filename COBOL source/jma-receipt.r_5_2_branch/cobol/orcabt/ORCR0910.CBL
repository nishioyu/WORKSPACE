      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             ORCR0910.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : レセプト入院（労災・自賠責）
      *  コンポーネント名  : 月次業務　労災自賠責明細書ファイル作成
      *                      点数摘要欄編集（労災・自賠責）
      *  管理者            : 
      *  作成日付    作業者        記述
      *  03/6/01     NACL-藤原     新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      *  01.00.01    NACL-門脇    04.01.28  診療区分９２のコメント
      *                                     のみの剤もレセ記載する
      *  01.00.02    NACL-藤原    04.02.05  薬剤・器材の編集順を逆にする
      *  ２００４年４月改正対応
      *  01.01.01    NACL-門脇    04.04.01  初診（乳幼児）（夜間・休日・
      *                                     深夜）加算の摘要欄記載
      *  01.01.02    NACL-門脇    04.04.26  ユーザーコメント追加対応
      *  01.01.03    NACL-藤原    04.07.16  検査のまとめを回数テーブルの
      *                                     合計で行うようにする 
      *  01.01.04    NACL-門脇    04.09.22  薬剤・器材の記載順修正
      *  中止処理追加
      *  01.02.01    NACL-藤原    04/10/13  中止処理の追加
      *  01.02.02    NACL-門脇    04/12/17  胸部固定帯加算、創外固定器
      *                                     使用加算を薬剤等へ
      *  01.02.03    NACL-門脇    05/01/27  腰・胸部固定帯加算、創外固定器
      *                                     使用加算の技術・薬剤等の選択対応
      *  01.02.04    NACL-門脇    05/02/28  （サ）記載対応
      *  01.02.05    NACL-門脇    05/03/02  薬剤で単位のないデータの数量編集
      *                                     修正
      *  01.02.06    NACL-藤原    05/03/07  包括対象検査のまとめの修正
      *  01.02.07    NACL-藤原    05/04/18  包括対象検査がまとまらない
      *                                     場合の編集の修正
      *  01.02.08    NACL-門脇    05/05/20  入院点滴種別追加に伴う修正
      *  01.02.09    NACL-門脇    05/05/26  手術・麻酔の記載順変更対応
      *  01.02.10    NACL-門脇    05/06/06  ワーク項目桁数修正
      *  01.02.11    NACL-藤原    05/08/17  包括対象検査フラグの新設に対応
      *  01.02.12    NACL-門脇    05/08/18  自賠責従来様式対応
      *  01.02.13    NACL-伊藤    05/09/01  検体検査コメントのついた包括
      *                                     対象検査の剤まとめに対応
      *  01.02.14    NACL-門脇    05/11/01  （検選）等コメント対応
      *
      *  ２００６年４月改正対応
      *  02.09.01    NACL-門脇    06/04/13  ２００６年４月改正対応
      *  02.09.02    NACL-藤原    06/04/14  包括対象検査「１１」の追加
      *  02.09.03    NACL-門脇    06/05/22  電子化加算編集修正
      *  03.01.01    NACL-門脇    06/06/28  実日数減算コード対応
      *  03.01.02    NACL-門脇    06/07/10  在支援コメント自動記載対応
      *  03.01.03    NACL-門脇    06/07/14  診療区分置き換えフラグの編集
      *  03.03.01    NACL-門脇    06/10/19  療養病棟レセコメント記載対応
      *  03.04.01    NACL-門脇    06/12/01  自賠責健保準拠対応
      *  03.04.02    NACL-門脇    07/03/08  点滴（種別３４０）の記載編集修正
      *  03.04.03    NACL-門脇    07/03/23  月途中医療機関コード変更時対応
      *  03.04.04    NACL-門脇    07/05/15  点滴の記載編集修正
      *
      *  03.05.01    NACL-門脇    07/06/01  グループ診療対応
      *  03.05.02    NACL-藤原    07/06/01  realtime preview 処理追加
      *  03.05.03    NACL-門脇    07/09/13  皮下筋肉、静脈注射の記載変更対応
      *  03.05.04    NACL-門脇    07/09/13  点滴の記載順変更（日付順）
      *
      *  04.01.01    NACL-門脇    07/10/05  入院点滴コメント対応
      *  04.01.02    NACL-門脇    07/11/01  公務災害対応
      *  04.01.03    NACL-門脇    07/11/26  特定器材商品名記載対応
      *  04.02.01    NACL-門脇    08/03/24  平成２０年４月改正対応
      *                                     （初診（夜間・早朝等）加算追加）
      *                                     （点滴編集修正）
      *                                     （診療区分６４：病理診断対応）
      *                                     （再就労、職業復帰：記載順対応）
      *  04.02.02    NACL-門脇    08/05/08  点滴３３２の手技料編集
      *  04.02.03    NACL-門脇    08/06/25  ８０その他記載順対応
      *
      *  04.03.01    NACL-門脇    08/07/09  投薬の記載順変更（日付順）
      *  04.03.02    NACL-門脇    08/07/14  投薬剤点０（院外等）記載対応
      *  04.03.03    NACL-門脇    08/12/01  在宅（院外分）編集修正
      *
      *  04.04.01    NACL-門脇    09/02/04  検査のコメント自動記載対応
      *  04.04.02    NACL-門脇    09/09/30  検査の数量編集修正
      *  04.04.03    NACL-門脇    09/11/19  自賠責健保準拠療養担当手当対応
      *  04.04.04    NACL-門脇    10/03/03  療養レセコメント編集修正
      *  04.04.05    NACL-門脇    10/03/29  改正対応
      *                                     （画像診断２回目以降減算編集）
      *                                     （コード追加）
      *  04.04.06    NACL-門脇    10/03/31  改正対応
      *                                     （頸部固定帯加算の対応）
      *                                     （８０その他の記載順）
      *  04.04.07    NACL-門脇    10/09/03  在宅薬剤院外処方点検用レセ
      *                                     記載修正
      *
      *  04.05.01    NACL-門脇    09/06/10  摘要欄下部へのコメント記載対応
      *  04.05.02    NACL-門脇    09/06/17  処置の記載順変更（日付順）
      *  04.05.03    NACL-藤原    09/07/14  小数点以下５桁対応
      *  04.05.04    NACL-門脇    10/01/25  治験対応
      *  04.05.05    NACL-門脇    11/11/30  画像診断フイルム分画数対応
      *  04.05.06    NACL-門脇    13/01/22  ワーク領域拡張
      *
      *  04.06.01    NACL-門脇    12/10/15  診療行為入力したコメントのみの剤の
      *                                     レセプト記載対応
      *  04.06.02    NACL-門脇    13/02/18  労災レセ電対応
      *                                     （新診療行為コード対応）
      *  04.06.03    NACL-門脇    13/02/27  平成２５年４月改正対応
      *  04.06.04    NACL-門脇    13/11/07  公務災害（労災準拠）の地公災特別
      *                                     加算対応（京都府）
      *
      *  04.07.01    NACL-門脇    12/08/28  薬剤料減点対応
      *  04.07.02    NACL-門脇    13/01/21  労災レセ電対応
      *  04.07.03    NACL-門脇    13/10/02  自賠責（第三者行為）対応
      *  04.07.04    NACL-門脇    13/12/03  自賠責（労災準拠）のレセプト様式
      *                                     追加対応
      *  04.07.05    NACL-門脇    14/11/12  画像診断フイルム分画数対応修正
      *  04.07.06    NACL-門脇    14/12/15  平成２７年１月改正対応
      *                                     （初再診料・低妥結率対応）
      *  04.07.07    NACL-門脇    14/12/24  平成２７年１月改正対応
      *                                     （初再診料・低妥結率対応）
      *  04.07.08    NACL-門脇    15/06/02  記載不要なシステム予約コードの対応
      *
      *  04.08.01    NACL-門脇    13/01/25  包括診療分の点検用レセプト記載対応
      *  04.08.02    NACL-藤原    14/07/07  一時ディレクトリ対応
      *  04.08.03    NACL-門脇    15/12/02  ０点地域公費記載対応
      *  04.08.04    NACL-門脇    15/12/08  労災コードの表用番号・公表順序番号
      *                                     編集修正
      *  04.08.05    NACL-門脇    16/01/12  点検用レセプト院外処方一般名記載対応
      *  04.08.06    NACL-門脇    16/03/28  平成２８年４月改正対応
      *  04.08.07    NACL-門脇    16/08/23  持参薬対応
      *  04.08.08    NACL-門脇    16/09/20  １５円以下薬剤の提出用レセプト記載
      *                                     （記録）対応
      *  04.08.09    NACL-門脇    16/11/07  １５円以下薬剤の提出用レセプト記載
      *                                     （記録）対応修正
      *  04.08.10    NACL-門脇    17/10/18  入院料と異なる保険組み合わせ
      *                                     での食事算定対応
      *  04.08.11    NACL-門脇    17/12/13  包括点数の公害補償法減点分のマイナス
      *                                     表示対応
      *  04.08.12    NACL-藤原    18/03/28  平成３０年４月改正対応
      *  04.08.13    NACL-門脇    18/04/02  平成３０年４月改正対応
      *
      *  05.00.01    NACL-門脇    18/11/16  宮城県地方公費対応
      *  05.00.02    NACL-門脇    19/03/11  公務災害（労災準拠）の地公災特別
      *                                     加算対応（京都府）修正
      *  05.00.03    NACL-門脇    19/03/11  予約コード読み飛ばし修正
      *  05.00.04    NACL-門脇    20/03/30  令和２年４月改正対応
      *  05.00.05    NACL-藤原    20/04/01  令和２年４月改正対応
      *                                     包括対象検査の追加
      *                                     名称の桁数変更による記載対応
      *  05.00.06    NACL-門脇    20/12/16  コロナ新設加算対応
      *  05.00.07    NACL-門脇    21/03/11  感染症対策実施加算対応
      *  05.00.08    ORCAMO       21/08/18  二類感染症患者入院診療加算対応
      *
      *  05.01.01    ORCAMO       22/03/16  令和４年４月改正対応
      *  05.01.02    ORCAMO       22/06/08  紙レセプトの記載順対応
      *                                     （診療区分１３，１４）
      *  05.01.03    ORCAMO       22/07/01  レセ電請求時の受付エラー分の
      *                                     種別不明レセ対応
      *                                     病名なし
      *                                     コメントの文字データ入力なし
      *                                     （８２を除く）
      *                                     特定器材の単位コードなし
      *                                      （金額種別５を除く）
      *  05.01.04    ORCAMO       22/09/27  令和４年１０月改正対応
      *                                     （減算コード対応）
      *  05.02.01    ORCAMO       23/05/01  令和５年５月改正対応
      *  05.02.02    ORCAMO       24/04/04  紙レセプトの記載順修正
      *                                     （診療区分１３，１４）
      *  05.02.03    ORCAMO       24/04/26  令和６年度改正対応
      *                                     CDKBN-KOUBANを２桁から３桁に変更
      *  05.02.04    ORCAMO       24/06/13  令和６年６月改正対応
      *****************************************************************
      *
       ENVIRONMENT             DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
           SELECT  RECE82-FILE     ASSIGN  RECE01PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  DYNAMIC
                                   RECORD  KEY     IS  RECE82-KEY
                                   FILE    STATUS  IS  STS-RECE82.
           SELECT  RECE84-FILE     ASSIGN  RECE84PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  SEQUENTIAL
                                   RECORD  KEY     IS  RECE84-KEY
                                   FILE    STATUS  IS  STS-RECE84.
           SELECT  RECE85-FILE     ASSIGN  RECE85PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  SEQUENTIAL
                                   RECORD  KEY     IS  RECE85-KEY
                                   FILE    STATUS  IS  STS-RECE85.
           SELECT  RECE86-FILE     ASSIGN  RECE86PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  DYNAMIC
                                   RECORD  KEY     IS  RECE86-KEY
                                   FILE    STATUS  IS  STS-RECE86.
           SELECT  RECE86X-FILE     ASSIGN  RECE86PARA
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  SEQUENTIAL
                                   RECORD  KEY     IS  RECE86X-KEY
                                   FILE    STATUS  IS  STS-RECE86.
      *    エラーファイル
           SELECT  RECEERR-FILE    ASSIGN  RECEERR
                                   FILE    STATUS  IS  STS-RECEERR.
      *
       DATA                    DIVISION.
       FILE                        SECTION.
      *
       FD  RECE82-FILE.
       01  RECE82-REC.
           COPY    "CPRCF082.INC".
      *
       FD  RECE84-FILE.
       01  RECE84-REC.
           COPY    "CPRCF084.INC".
      *
       FD  RECE85-FILE.
       01  RECE85-REC.
           COPY    "CPRCF085.INC".
      *
       FD  RECE86-FILE.
       01  RECE86-REC. 
           COPY    "CPRCF086.INC".
      *
       FD  RECE86X-FILE.
       01  RECE86X-REC. 
           COPY    "CPRCF086.INC"  REPLACING  //RECE86//
                                   BY         //RECE86X//.
      *    エラーファイル
       FD  RECEERR-FILE.
       01  RECEERR-REC             PIC X(200). 
      *
       WORKING-STORAGE             SECTION.
      *
           COPY    "CPRECEDAT1.INC"
                                   REPLACING  //RECEDAT1//
                                   BY         //RECE01//.
           COPY    "CPRECEDAT1.INC"
                                   REPLACING  //RECEDAT1//
                                   BY         //RECE84//.
           COPY    "CPRECEDAT1.INC"
                                   REPLACING  //RECEDAT1//
                                   BY         //RECE85//.
           COPY    "CPRECEDAT1.INC"
                                   REPLACING  //RECEDAT1//
                                   BY         //RECE86//.
      *    エラーファイル 名称領域 
           COPY    "CPERRFL.INC"   REPLACING  //ERRFLPARA//
                                   BY         //RECEERR//.
      *
      *    フラグ領域
       01  STS-AREA.
           03  SPA-STS             PIC 9(04).
           03  STS-RECE82          PIC X(02).
           03  STS-RECE84          PIC X(02).
           03  STS-RECE85          PIC X(02).
           03  STS-RECE86          PIC X(02).
           03  STS-RECEERR         PIC X(02).
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-END             PIC 9(01).
           03  FLG-RECE84          PIC 9(01).
           03  FLG-RECE85          PIC 9(01).
           03  FLG-SP              PIC 9(01).
           03  FLG-NOPRT           PIC 9(01).
           03  FLG-TEIGEN          PIC 9(01).
           03  FLG-COMMENT         PIC 9(01).
           03  FLG-COMMENT2        PIC 9(01).
           03  FLG-COMMENT3        PIC 9(01).
           03  FLG-COMMENT4        PIC 9(01).
           03  FLG-HIKAKIN         PIC 9(01).
           03  FLG-SRYCD1          PIC 9(01).
           03  FLG-OK              PIC 9(01).
           03  FLG-GAIJINKEN       PIC 9(01).
           03  FLG-ARI             PIC 9(01).
           03  FLG-COM-CHK         PIC 9(01).
           03  FLG-SYSKANRI        PIC 9(01).
           03  FLG-GENTENCD-ARI    PIC 9(01).
           03  FLG-SYOSAI-CHK      PIC 9(01).
           03  FLG-TANZAING        PIC 9(01).
      *
      *    カウント領域
       01  CNT-AREA.
           03  CNT-RECE82          PIC 9(06).
           03  CNT-RECE84          PIC 9(06).
           03  CNT-RECE85          PIC 9(06).
           03  CNT-RECE86          PIC 9(06).
           03  CNT-IN-CANCEL       PIC 9(06).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDV                 PIC 9(04).
           03  IDW                 PIC 9(04).
           03  IDX                 PIC 9(04).
           03  IDY                 PIC 9(04).
           03  IDZ                 PIC 9(04).
           03  IDXX                PIC 9(04).
      *    点数回数編集用     
           03  IDA                 PIC 9(04).
           03  IDB                 PIC 9(04).
      *    剤診療行為コード別編集用     
           03  IDX1                PIC 9(04).
           03  IDX2                PIC 9(04).
           03  IDX3                PIC 9(04).
           03  IDX4                PIC 9(04).
           03  IDX6                PIC 9(04).
           03  IDX8                PIC 9(03).
           03  IDX9                PIC 9(03).
      *
           03  IDW3                PIC 9(04).
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-PARA.
               05  WRK-PARA-SRYYM      PIC  X(06).
               05  WRK-PARA-TERMID     PIC  X(16).
               05  WRK-PARA-SYSYMD     PIC  X(08).
               05  WRK-PARA-RECEKBN    PIC  X(01).
               05  WRK-PARA-JOBID      PIC  9(07).
               05  WRK-PARA-SHELLID    PIC  X(08).
           03  WRK-PARA-JIBAIPRTKBN    PIC  X(01).
           03  WRK-PARA-PRTKBN         PIC  X(01).
           03  WRK-PARA-HOSPNUM        PIC  9(02).
      *
           03  WRK-LINENUM         PIC 9(05).
      *
           03  WRK-TENSUGOKEI      PIC 9(07).
      *
      *    剤印字行数
           03  WRK-TBLMAX          PIC 9(03).
      *    剤明細総数（診療行為コード別）
           03  WRK-ZAIWMAX-G.
               05  WRK-ZAIWMAX     PIC 9(03)   OCCURS  6.
           03  WRK-ZAIWMAX-SAV-G.
               05  WRK-ZAIWMAX-SAV PIC 9(03)   OCCURS  6.
      * 
           03  WRK-YUKOKETA        PIC  9(03).
           03  WRK-NAME            PIC  X(300).
      *
           03  WRK-RECE85          PIC 9(01).
      *
      *    数字半角全角変換用     
           03  WRK-NUM                         PIC 9(01).
           03  WRK-CHAR-IDX                    PIC 9(02).
           03  WRK-CHAR-HENKAN-AREA.
               05  WRK-CHAR-X                  PIC X(10).
               05  WRK-CHAR-N                  PIC X(20).
               05  WRK-CHAR-CNT                PIC 9(02).
               05  WRK-CHAR-FLG                PIC 9(01). 
               05  WRK-CHAR-N-CNT              PIC 9(02).
      *
           03  WRK-EDIT-AREA.
               05  WRK-WCLM                    PIC 9(03).
      *
           03  WRK-KOUI                        PIC X(300).
      *
           03  WRK-ZAI.
               05  WRK-ZAI-MK                  PIC X(02).
               05  WRK-ZAI-NAME.
                   07  FILLER                  PIC X(28).
                   07  WRK-ZAI-TNSKAI          PIC X(22).
      *
           03  WRK-TNSKAI.
               05  WRK-ZAITEN                  PIC X(14).
               05  WRK-X                       PIC X(02).
               05  WRK-ZAIKAISU                PIC X(06).
      *
           03  WRK-ZAITENX                     PIC ZZZZZZ9.
           03  WRK-ZAIKAISUX                   PIC ZZ9.
           03  WRK-TNSKAI-ST                   PIC 9(02).
           03  WRK-TNSKAI-KETA                 PIC 9(02).
      *
           03  WRK-SRYCD                       PIC X(01).
      *
      *    検査の回数
           03  WRK-KENSA-KAISU                 PIC 9(05). 
      *     
           03  WRK-SRYSURYOW                   PIC X(24).
           03  WRK-NAMEKETA                    PIC 9(03).
           03  WRK-YOHAKU                      PIC 9(03).
           03  WRK-WKETA                       PIC 9(03).
      *    分画数編集用     
           03  WRK-BUNKAKUKETA                 PIC 9(02).
           03  WRK-BUNKAKU-N                   PIC X(10).
           03  WRK-SRYKAISU-X                  PIC ZZ9.
      *    数量編集用     
           03  WRK-SURYOKETA                   PIC 9(02).
           03  WRK-SURYOKETA1                  PIC 9(02).
           03  WRK-SURYOKETA2                  PIC 9(02).
           03  WRK-SURYOST                     PIC 9(02).
           03  WRK-SURYO                       PIC 9(05)V9(05).
           03  WRK-SURYO-R  REDEFINES  WRK-SURYO.
               05  WRK-SURYO-S1                PIC 9(05).
               05  WRK-SURYO-S2                PIC 9(05).
           03  WRK-SURYO-X.
      *********05  WRK-SURYO-Z1                PIC ZZZZZ.
               05  WRK-SURYO-Z1                PIC ZZZZ9.
               05  WRK-SURYO-Z2                PIC X(01).
               05  WRK-SURYO-Z3                PIC ZZZZZ.
           03  WRK-SURYO-N.
               05  WRK-SURYO-N1                PIC X(10).
               05  WRK-SURYO-N2                PIC X(02).
               05  WRK-SURYO-N3                PIC X(10).
      *
      *    特定保険医療材料編集用
           03  WRK-RECEHENNAME                 PIC X(300).
           03  WRK-RECEHENTANINAME             PIC X(50).
           03  WRK-RECEHENTANI                 PIC 9(02).
      *
      *    剤診療行為コード別編集用     
           03  WRK-WIDX1                       PIC 9(03).
           03  WRK-WIDX2                       PIC 9(03).
      *     
           03  WRK-Z9                          PIC ZZZ9.
      *
      *    剤の診療種別区分
           03  WRK-SRYSYUKBN                   PIC X(03).
      *
           03  WRK-KENTAICOMMENT-YUKOKETA      PIC 9(03).
           03  WRK-KENTAICOMMENT-NAME          PIC X(300).
           03  WRK-KENTAICOMMENT-NAME-TEMP     PIC X(300).
           03  WRK-KENTAICOMMENT-SRYCD         PIC 9(09).
      *
           03  WRK-SET-LINENUM                 PIC 9(05).
      *
           03  WRK-MOJISU          PIC 9(03).    
      *
       01  WRK-ERR-AREA.
           03  WRK-RECEERR         PIC X(200).
           03  WRK-ERRMSG          PIC X(300).
           03  WRK-MCP-RC          PIC 9(9).
           03  WRK-ERR-FILE-STS    PIC X(02).
           03  WRK-ERR-FILE-MSG    PIC X(100).
           03  WRK-ERR-FILE-MSG1   PIC X(17).
           03  WRK-ERR-FLG         PIC 9(01).
      *    エラーファイル名称領域 
           COPY    "CPTEMPFL.INC"  REPLACING  //TEMPFLPARA//
                                   BY         //WRK-ERR-FILE-NM//.
      *    FULLNAME内のBASENAME開始位置
       01  WRK-SGETTEMP-ST         PIC 9(05).
      *
      *    剤・診療行為テーブル
       01  TBL-ZAI-AREA.
      ***  03  TBL-ZAI-OCC         OCCURS   100.
           03  TBL-ZAI-OCC         OCCURS   300.
               05  TBL-ZAI         PIC X(52).
               05  TBL-ZAI-IDX     PIC 9(01).
               05  TBL-ZAI-SRYCD   PIC X(09).
      *
      *    検査をまとめるテーブル
       01  WRK-KENSA-MATOME-MAX            PIC 9(03).
       01  WRK-KENSA-MATOME-TABLE.
           03  WRK-KENSA-MATOME            OCCURS  101.
               05  WRK-HOUKNSKBN           PIC 9(02).
               05  WRK-GAIJINKEN           PIC 9(02).
               05  WRK-SRYSYUKBN-K         PIC X(03).
               05  WRK-KAISU-G.
                   07  WRK-KAKSU-OCC       OCCURS  4.
                       09  WRK-KAISU       PIC 9(03).  
                   07  WRK-KAISU1          PIC 9(02).
                   07  WRK-KAISU2          PIC 9(03).
               05  WRK-KENSA-SU            PIC 9(03).
               05  WRK-RECE84-IDX-G.
                   07  WRK-RECE84-IDX      PIC 9(03)
                                                  OCCURS  30.
               05  WRK-KENSA-SRYCD-G.
                   07  WRK-KENSA-SRYCD-OCC OCCURS  30.
                       09  WRK-KENSA-KENTAICOMMENT-SRYCD
                                           PIC 9(09).
                       09  WRK-KENSA-SRYCD PIC 9(09).
                       09  WRK-KENSA-SRYSURYO PIC 9(05)V9(05).
      *
       01  WRK-RECE84-ZAITEN               PIC 9(08).
      *
       01  WRK-RECE84-MAX                  PIC 9(03).
       01  WRK-SUM-KAISU-TABLE.
           03  WRK-SUM-KAISU               PIC 9(04)
                                                  OCCURS  31.
       01  WRK-MIN-KAISU-TABLE.
           03  WRK-MIN-KAISU-G             OCCURS  31.
               05  WRK-MIN-KAISU           PIC 9(03).
               05  WRK-MIN-HOUKNSKBN       PIC 9(02).
               05  WRK-MIN-GAIJINKEN       PIC 9(02).
               05  WRK-MIN-SRYSYUKBN       PIC X(03).
       01  WRK-RECE84-FLG-TABLE.
           03  WRK-RECE84-FLG              PIC 9(01)
                                                  OCCURS  100.
       01  WRK-RECE84-MATOME-TABLE.
           03  WRK-RECE84-MATOME           PIC 9(01)
                                                  OCCURS  100.
       01  WRK-RECE84-TABLE.
           03  WRK-RECE84-TBL              OCCURS  100.
               05  WRK-RECE84-HOUKNSKBN           PIC 9(02).
               05  WRK-RECE84-GAIJINKEN           PIC 9(02).
               05  WRK-RECE84-SRYSYUKBN           PIC X(03).
               05  WRK-RECE84-REC                 PIC X(1000).
               05  WRK-RECE84-SRYCD-G.
                   07  WRK-RECE84-KENTAICOMMENT-SRYCD
                                                  PIC 9(09).
                   07  WRK-RECE84-SRYCD           PIC 9(09).
                   07  WRK-RECE84-SRYSURYO        PIC 9(05)V9(05).
               05  WRK-RECE84-DAY-AREA.
                   07  WRK-RECE84-DAY-TBL         OCCURS  4.
                       09  WRK-RECE84-KAISUINF    OCCURS  31.
                           11  WRK-RECE84-KAISU   PIC  9(03).
      *
      *    検査のとき適用の編集に使用
       01  WRK-KENSA-MAX                   PIC 9(04).
       01  TBL-NAME-AREA.
           03  TBL-NAME-REC      OCCURS 100.
               05  TBL-SRYCD               PIC  X(09).
               05  TBL-SRYSURYO            PIC  9(05)V9(05).
      *        単位コード/単位名称有効桁数/単位名称
               05  TBL-TANICD              PIC  9(03).
               05  TBL-TANIMOJISU          PIC  9(01).
               05  TBL-TANINAME            PIC  X(24).
               05  TBL-NAMEINF.
      *            漢字有効桁数/漢字名称
                   07  TBL-YUKOKETA        PIC  9(03).
                   07  TBL-NAME            PIC  X(300).
      *        データ区分情報
               05  TBL-DATAKBN             PIC  9(01).
      *        包括対象検査情報
               05  TBL-HOUKNSKBN           PIC  9(02).
      *
      *     検査の項目数用
        01  WRK-KOUMOKUSU               PIC 9(02). 
        01  WRK-TNS-HOUKNSKBN           PIC 9(02).
        01  WRK-CONS-KENSACOMMENT-AREA.
            03  WRK-CONS-KENSACOMMENT-G.
                05  FILLER                 PIC X(50)   VALUE
                "血液化学検査".                        
                05  FILLER                 PIC X(50)   VALUE
                "内分泌学的検査".                        
                05  FILLER                 PIC X(50)   VALUE
                "肝炎ウイルス関連検査".                        
                05  FILLER                 PIC X(50)   VALUE
                "腫瘍マーカーのうちα−フェトプロテイン等".                        
                05  FILLER                 PIC X(50)   VALUE
                "腫瘍マーカー".                        
                05  FILLER                 PIC X(50)   VALUE
                "出血・凝固検査".                        
                05  FILLER                 PIC X(50)   VALUE
                "自己抗体検査".                        
                05  FILLER                 PIC X(50)   VALUE
                "内分泌負荷試験".                        
                05  FILLER                 PIC X(50)   VALUE
                "ウイルス抗体価".                        
                05  FILLER                 PIC X(50)   VALUE
                "ウイルス抗体価精密測定".                        
                05  FILLER                 PIC X(50)   VALUE
                "血漿蛋白免疫学的検査".                        
                05  FILLER                 PIC X(50)   VALUE
                "悪性腫瘍遺伝子検査".
                05  FILLER                 PIC X(50)   VALUE
                "悪性腫瘍遺伝子検査（処理が容易なもの）".
                05  FILLER                 PIC X(50)   VALUE
                "悪性腫瘍遺伝子検査（処理が複雑なもの）".
                05  FILLER                 PIC X(50)   VALUE
                "悪性腫瘍遺伝子検査（血液・血漿）１".
                05  FILLER                 PIC X(50)   VALUE
                "悪性腫瘍遺伝子検査（血液・血漿）２".
            03  WRK-CONS-KENSACOMMENT-R    REDEFINES
                                           WRK-CONS-KENSACOMMENT-G.
                05  WRK-CONS-KENSACOMMENT  PIC X(50)   OCCURS  16.
      *外来迅速検体検査加算算定日テーブル
       01  WRK-KENTAIKASAN-TABLE.
           03  WRK-KENTAIKASAN             PIC 9(01)
                                                  OCCURS  31.
       01  WRK-GAIJINKEN-ZAINUM            PIC 9(03).
      *外来迅速検体検査加算算定日の剤情報退避
       01  WRK-RECE84GJK-MAX               PIC 9(02).
       01  WRK-RECE84GJK-TABLE.
           03  WRK-RECE84GJK-TBL           OCCURS 31.
               05  WRK-RECE84GJK-REC       PIC X(1000).
       01  RECE84GJK-REC.
           COPY    "CPRCF084.INC"  REPLACING  //RECE84//
                                   BY         //RECE84GJK//.
       01  RECE84SAV-REC.
           COPY    "CPRCF084.INC"  REPLACING  //RECE84//
                                   BY         //RECE84SAV//.
      *
       01  TBL-ZAIW-AREA.
      *    剤の先頭の診療コード
           03  WRK-ZAIW-SRYCD1             PIC X(09).
      *    剤の先頭の診療コードの各種番号等情報
           03  WRK-ZAIW-CDKBN-AREA.
               05  WRK-ZAIW-CDKBN-KBN          PIC X(01).
               05  WRK-ZAIW-CDKBN-SYO          PIC 9(01).
               05  WRK-ZAIW-CDKBN-BU           PIC 9(02).
               05  WRK-ZAIW-CDKBN-KBNNUM       PIC 9(03).
               05  WRK-ZAIW-CDKBN-KBNNUM-EDA   PIC 9(02).
               05  WRK-ZAIW-CDKBN-KOUBAN       PIC 9(03).
               05  WRK-ZAIW-KOUHYOJYUNNUM      PIC 9(09).
      *    剤の先頭の診療コードの検査等実施判断情報
           03  WRK-ZAIW-KNSJISKBN          PIC 9(01).
      *    データ区分
           03  WRK-ZAIW-DATAKBN            PIC 9(01).
      *    告示等識別区分１
           03  WRK-ZAIW-KOKUJISKBKBN1      PIC 9(01).
      *    告示等識別区分２
           03  WRK-ZAIW-KOKUJISKBKBN2      PIC 9(01).
      *    優先区分
           03  WRK-ZAIW-PRIORITYKBN        PIC 9(01).
      *
      *    診療行為別テーブル
      *    １：ユーザーコメント、２：コメント、
      *    ３：診療行為、４：薬剤、５：特定器材、６：外迅検退避用
           03  TBL-ZAIW-OCC                OCCURS 6.
      ***      05  TBL-ZAIW-REC            OCCURS 100.
               05  TBL-ZAIW-REC            OCCURS 300.
                   07  TBL-ZAIW-SRYACTINF.
      *                診療コード/数量/回数
                       09  TBL-ZAIW-SRYCD           PIC  X(09).
                       09  TBL-ZAIW-SRYSYUKBN-X     PIC  X(03).
                       09  TBL-ZAIW-SRYSURYO        PIC  9(05)V9(05).
                       09  TBL-ZAIW-SRYKAISU        PIC  9(03).
      *                単位コード/単位名称有効桁数/単位名称
                       09  TBL-ZAIW-TANICD          PIC  9(03).
                       09  TBL-ZAIW-TANIMOJISU      PIC  9(01).
                       09  TBL-ZAIW-TANINAME        PIC  X(24).
      *                点数識別区分/点数/明細請求フラグ
                       09  TBL-ZAIW-TENSIKIBETU     PIC  9(01).
                       09  TBL-ZAIW-TEN             PIC  9(09)V9(02).
                       09  TBL-ZAIW-MEISKYFLG       PIC  X(01).
      *                自動算定区分/名称入力番号/点数欄集計先識別
                       09  TBL-ZAIW-AUTOKBN         PIC  X(01).
                       09  TBL-ZAIW-INPUTNUM        PIC  9(03).
                       09  TBL-ZAIW-TENTTLKBN       PIC  9(03).
      *
                       09  TBL-ZAIW-DATAKBN         PIC  9(01).
                       09  TBL-ZAIW-HOUKNSKBN       PIC  9(02).
                       09  TBL-ZAIW-KNSJISKBN       PIC  9(01).
                       09  TBL-ZAIW-KNSJISGRPKBN    PIC  9(02).
                       09  TBL-ZAIW-CDKBNINF.
                         11 TBL-ZAIW-CDKBN-KBN        PIC  X(01).
                         11 TBL-ZAIW-CDKBN-SYO        PIC  9(01).
                         11 TBL-ZAIW-CDKBN-BU         PIC  9(02).
                         11 TBL-ZAIW-CDKBN-KBNNUM     PIC  9(03).
                         11 TBL-ZAIW-CDKBN-KBNNUM-EDA PIC  9(02).
                         11 TBL-ZAIW-CDKBN-KOUBAN     PIC  9(03).
                         11 TBL-ZAIW-KOUHYOJYUNNUM    PIC  9(09).
                       09  TBL-ZAIW-KOKUJISKBKBN1   PIC  9(01).
                       09  TBL-ZAIW-KOKUJISKBKBN2   PIC  9(01).
                       09  TBL-ZAIW-TUSOKUGAIKBN    PIC  9(01).
                       09  TBL-ZAIW-TIMEKSNKBN      PIC  9(01).
                       09  TBL-ZAIW-KZMCOMPSIKIBETU PIC  9(01).
                       09  TBL-ZAIW-CHUKSNINF.
                         11 TBL-ZAIW-CHUKSNCD       PIC  X(04).
                         11 TBL-ZAIW-CHUKSNTUBAN    PIC  X(01).
                       09  TBL-ZAIW-KENTAICOMMENT   PIC  9(01).
                       09  TBL-ZAIW-INPUTCOMENT     PIC  X(140).
                   07  TBL-ZAIW-NAMEINF.
      *                漢字有効桁数/漢字名称
                       09  TBL-ZAIW-YUKOKETA        PIC  9(03).
                       09  TBL-ZAIW-NAME            PIC  X(300).
                   07  TBL-ZAIW-SRYSYUKBN-INPUT     PIC  X(03).
                   07  TBL-ZAIW-KENTAICOMMENT-SRYCD PIC  9(09).
      *            レセ用特定器材商品名編集フラグ
                   07  TBL-ZAIW-RECEHENFLG          PIC  X(01).
      *
       01  TBL-ZAI-MAX         PIC  9(03)  VALUE   100.
       01  TBL-ZAI-MAX2        PIC  9(03)  VALUE   300.
      *
      *    初診・再診の剤まとめ情報
       01  TBL-SORT11-INF.
         02    TBL-SORT11-OCC                   OCCURS  34.
           03  TBL-SORT11-FLG                   PIC  9(01).
           03  TBL-SORT11-TEN                   PIC  9(07).
           03  TBL-SORT11-TENSIKIBETU           PIC  9(01).
           03  TBL-SORT11-YUKOKETA              PIC  9(03).
           03  TBL-SORT11-NAME                  PIC  X(300).
           03  TBL-SORT11-CDKBNINF.
               05  TBL-SORT11-CDKBN-KBN         PIC  X(01).
               05  TBL-SORT11-CDKBN-SYO         PIC  9(01).
               05  TBL-SORT11-CDKBN-BU          PIC  9(02).
               05  TBL-SORT11-CDKBN-KBNNUM      PIC  9(03).
               05  TBL-SORT11-CDKBN-KBNNUM-EDA  PIC  9(02).
               05  TBL-SORT11-CDKBN-KOUBAN      PIC  9(03).
               05  TBL-SORT11-KOUHYOJYUNNUM     PIC  9(09).
           03  TBL-SORT11-ZAIKAISU              PIC  9(04).
           03  TBL-SORT11-HKNCOMBI              PIC  9(04).
      *
      *   日本語数字
       01  TBL-SUJI-AREA.
           03  FILLER          PIC X(20)   VALUE
              "１２３４５６７８９０".
       01  TBL-SUJI-R          REDEFINES   TBL-SUJI-AREA.
           03  TBL-SUJI        PIC X(02)   OCCURS   10.
      *
      *   印字しない診療行為テーブル
       01  TBL-NOSRYCD-AREA.
      *    調基（入院）
           03  FILLER          PIC X(09)   VALUE   "120001710".
      *    調剤料（入院）
           03  FILLER          PIC X(09)   VALUE   "120001110".
      *    調剤料（麻・向・覚・毒）（入院）
           03  FILLER          PIC X(09)   VALUE   "120000410".
      *    初診料算定科：
           03  FILLER          PIC X(09)   VALUE   "830000020".
      *    実日数算定
           03  FILLER          PIC X(09)   VALUE   "099409901".
      *    初診料ダミー
           03  FILLER          PIC X(09)   VALUE   "099110001".
      *    実日数減算
           03  FILLER          PIC X(09)   VALUE   "099409903".
      ***
      *    初診料
           03  FILLER          PIC X(09)   VALUE   "111000110".
      *    乳幼児加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111000370".
      *    時間外加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111000570".
      *    休日加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111000670".
      *    深夜加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111000770".
      *    乳幼児時間外加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111011970".
      *    乳幼児休日加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111012070".
      *    乳幼児深夜加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111012170".
      *
       01  TBL-NOSRYCD-R       REDEFINES   TBL-NOSRYCD-AREA.
            03  TBL-NOSRYCD    PIC X(09)   OCCURS   15.
      *
       01  TBL-NOSRYCD-MAX     PIC 9(03)   VALUE    15.
      *
      *    初診集計用診療行為テーブル
      *    （同一日複数科受診時の２科目は不要）
       01  TBL-SYOSRYCD-AREA.
      *    初診料（文書による紹介がない患者）
           03  FILLER          PIC X(09)   VALUE   "111012510".
      *    特定妥結率初診料
           03  FILLER          PIC X(09)   VALUE   "111012710".
      *    初診料（新型コロナウイルス感染症・診療報酬上臨時的取扱）
           03  FILLER          PIC X(09)   VALUE   "111013850".
      *    初診料（文書による紹介がない患者の場合）（初減）（特例）
           03  FILLER          PIC X(09)   VALUE   "111016150".
      *
      *    初診料（情報通信機器）
           03  FILLER          PIC X(09)   VALUE   "111014210".
      *    初診料（文書による紹介がない患者）（情報通信機器）
           03  FILLER          PIC X(09)   VALUE   "111014310".
      *    特定妥結率初診料（情報通信機器）
           03  FILLER          PIC X(09)   VALUE   "111014410".
      *
      *    時間外特例医療機関加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111000870".
      *    乳幼児時間外特例医療機関加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111012270".
      *    乳幼児夜間加算（小児科初診）
           03  FILLER          PIC X(09)   VALUE   "111011570".
      *    乳幼児休日加算（小児科初診）
           03  FILLER          PIC X(09)   VALUE   "111011670".
      *    乳幼児深夜加算（小児科初診）
           03  FILLER          PIC X(09)   VALUE   "111011770".
      *    夜間・早朝等加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111012470".
      *    機能強化加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111013770".
      *    外来感染対策向上加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111014870".
      *    連携強化加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111014970".
      *    サーベイランス強化加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111015070".
      *    電子的保健医療情報活用加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111015170".
      *    電子的保健医療情報活用加算（初診）（診療情報等の取得が困難等）
           03  FILLER          PIC X(09)   VALUE   "111015270".
      *    乳幼児感染予防策加算（初診料・診療報酬上臨時的取扱）
           03  FILLER          PIC X(09)   VALUE   "111013970".
      *    二類感染症患者入院診療加算（電話等初診料・診療報酬上臨時的取扱）
           03  FILLER          PIC X(09)   VALUE   "111014170".
      *    （選）病床数が２００以上の病院初診（２００点減算）
           03  FILLER          PIC X(09)   VALUE   "111015370".
      *    （選）病床数が２００以上の病院初診（１８６点減算）
           03  FILLER          PIC X(09)   VALUE   "111015470".
      *    （選）病床数が２００以上の病院初診（１４４点減算）
           03  FILLER          PIC X(09)   VALUE   "111015570".
      *    （選）病床数が２００以上の病院初診（１２５点減算）
           03  FILLER          PIC X(09)   VALUE   "111015670".
      *    （選）病床数が２００以上の病院初診（１０７点減算）
           03  FILLER          PIC X(09)   VALUE   "111015770".
      *    （選）病床数が２００以上の病院初診（９３点減算）
           03  FILLER          PIC X(09)   VALUE   "111015870".
      *    医療情報・システム基盤整備体制充実加算１（初診）
           03  FILLER          PIC X(09)   VALUE   "111015970".
      *    医療情報・システム基盤整備体制充実加算２（初診）
           03  FILLER          PIC X(09)   VALUE   "111016070".
      *    発熱患者等対応加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111702970".
      *    抗菌薬適正使用加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111703070".
      *    医療情報取得加算１（初診）
           03  FILLER          PIC X(09)   VALUE   "111703170".
      *    医療情報取得加算２（初診）
           03  FILLER          PIC X(09)   VALUE   "111703270".
      *    医療ＤＸ推進体制整備加算（初診）
           03  FILLER          PIC X(09)   VALUE   "111703370".
      *
       01  TBL-SYOSRYCD-R      REDEFINES   TBL-SYOSRYCD-AREA.
            03  TBL-SYOSRYCD   PIC X(09)   OCCURS   34.
      *
       01  TBL-SYOSRYCD-MAX    PIC 9(03)   VALUE    34.
      *
      *    固定値
       01  CONST-AREA.
      *    画像診断の単位の変更（枚→回）
           03  WRK-CONS-TANICD PIC 9(03)   VALUE   006.
           03  WRK-CONS-TANINAME
                               PIC X(02)   VALUE   "回".
           03  WRK-CONS-BUNKAKU-MOJISU
                               PIC 9(02)   VALUE   4.                    
           03  WRK-CONS-BUNKAKU
                               PIC X(04)   VALUE   "分画".
      *
      *    尿一般
           03  WRK-CONS-NYOIPPAN-N     PIC 9(09)   VALUE 
                                                   160000310.
      *    検査の回数に集計しない診療行為
      *    採血（Ｂ−Ａ）
           03  WRK-CONS-SAIKETSU1      PIC X(09)   VALUE "160101210".
      *    採血（Ｂ−Ｖ）
           03 WRK-CONS-SAIKETSU2       PIC X(09)   VALUE "160095710".
      *    採血（Ｂ−Ｃ）
           03 WRK-CONS-SAIKETSU3       PIC X(09)   VALUE "160095810".
      *
      *    腰部固定帯加算
           03  WRK-CONS-YOUBUKASAN     PIC X(09)   VALUE "140037490".
      *    胸部固定帯加算
           03  WRK-CONS-KYOBUKASAN     PIC X(09)   VALUE "140040110".
      *    頸部固定帯加算
           03  WRK-CONS-KEIBUKASAN     PIC X(09)   VALUE "140052610".
      *    創外固定器使用加算
           03  WRK-CONS-SOGAIKASAN     PIC X(09)   VALUE "150266970".
      *    療養担当手当て
           03  WRK-CONS-RYOYOTEATE     PIC X(09)   VALUE "199000510".
      *
      *    フィルム料（乳幼児）加算
           03  WRK-CONS-FILM-YOUJI-KASAN
                                       PIC X(09)   VALUE 
                                                   "799990070".
      *    酸素補正率
           03  WRK-CONS-SANSO          PIC X(09)   VALUE 
                                                   "770020070".
      *    高気圧酸素加算
           03  WRK-CONS-SANSO2         PIC X(09)   VALUE 
                                                   "770030070".
      *
           COPY    "MCPAREA".
      *
           COPY    "COMMON-SPA".
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    ジョブ管理マスタ
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *
      *    システム管理マスタ
           COPY    "CPSYSKANRI.INC".
      *
           COPY    "CPSK200501.INC"
                                   REPLACING  //SYS-200501//
                                   BY         //SYS-25//.
      *
      *****************************************************************
      *    サブプロ用領域
      *****************************************************************
      *   ジョブ管理ＤＢ制御サブ
           COPY    "CPORCSJOBKANRI.INC".
      *
           COPY    "CPORCSGETTEMP.INC".
      *     
      *    数値変換サブ
           COPY    "CPORCSNUM.INC".
      *
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
      *
      ****************************************************************
       LINKAGE                 SECTION.
       01  COMMAND-PARAM.
           02  FILLER      PIC X(256).
      ****************************************************************
       PROCEDURE           DIVISION
               USING
           COMMAND-PARAM.
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           PERFORM 100-INIT-SEC
      *
           PERFORM 200-MAIN-SEC
                   UNTIL       SPA-STS     =   2
      *
           PERFORM 300-END-SEC
      *
           IF      WRK-PARA-SHELLID    NOT =   "RECEPTX"
               STOP    RUN
           ELSE
               EXIT    PROGRAM
           END-IF 
           .
      *****************************************************************
      *    初期　処理
      *****************************************************************
       100-INIT-SEC                SECTION.
      *
           INITIALIZE                  SPA-AREA
           INITIALIZE                  FLG-AREA
           INITIALIZE                  STS-AREA
           INITIALIZE                  WRK-AREA
           INITIALIZE                  CNT-AREA
           INITIALIZE                  TBL-ZAI-AREA
           INITIALIZE                  TBL-ZAIW-AREA
           INITIALIZE                  RECEERR
      *
      *    ファイルＯＰＥＮ処理
      *
           PERFORM 110-FILE-OPEN-SEC
      *
           IF      WRK-PARA-SHELLID    NOT =   "RECEPTX"
               PERFORM 120-DBOPEN-SEC
      *
      *        ステップ管理開始処理
               MOVE    "STS"               TO  SJOBKANRI-MODE
               INITIALIZE                      JOBKANRI-REC
               MOVE    "ORCR0910"      TO  JOB-PGID
               MOVE    "点数摘要欄編集（労災・自賠責）"
                                           TO  JOB-SHELLMSG
grpsys         PERFORM 900-CALL-ORCSJOB-SEC
      *
      *        処理中止設定処理
               PERFORM 500-CANCEL-HENSYU-SEC
           END-IF 
      *
           PERFORM 110-SYSKANRI-HENSYU-SEC
      *
           IF      SPA-STS         =   ZERO
               PERFORM 990-RECE82-READ-SEC
           END-IF 
           .
       100-INIT-EXT.
           EXIT.
      *
      *****************************************************************
      *    ファイルＯＰＥＮ処理
      *****************************************************************
       110-FILE-OPEN-SEC          SECTION.
      *
           UNSTRING    COMMAND-PARAM   DELIMITED  BY  ","
                                       INTO    WRK-PARA
                                               WRK-PARA-JIBAIPRTKBN
                                               WRK-PARA-PRTKBN
                                               WRK-PARA-HOSPNUM
                                               RECEERR
           END-UNSTRING
           MOVE    WRK-PARA-HOSPNUM    TO  SPA-HOSPNUM
      *
           MOVE    SPACE               TO  RECE01PARA
           INITIALIZE                      RECE01PARA
           MOVE    "RECE820"           TO  RECE01PARA-FILE-ID
           MOVE    WRK-PARA-TERMID     TO  RECE01PARA-TERMID
grpsys     MOVE    WRK-PARA-HOSPNUM    TO  RECE01PARA-HOSPNUM
      *
           MOVE    SPACE               TO  RECE84PARA
           INITIALIZE                      RECE84PARA
           MOVE    "RECE84A"           TO  RECE84PARA-FILE-ID
           MOVE    WRK-PARA-TERMID     TO  RECE84PARA-TERMID
grpsys     MOVE    WRK-PARA-HOSPNUM    TO  RECE84PARA-HOSPNUM
      *
           MOVE    SPACE               TO  RECE85PARA
           INITIALIZE                      RECE85PARA
           MOVE    "RECE85A"           TO  RECE85PARA-FILE-ID
           MOVE    WRK-PARA-TERMID     TO  RECE85PARA-TERMID
grpsys     MOVE    WRK-PARA-HOSPNUM    TO  RECE85PARA-HOSPNUM
      *
           MOVE    SPACE               TO  RECE86PARA
           INITIALIZE                      RECE86PARA
           MOVE    "RECE860"           TO  RECE86PARA-FILE-ID
           MOVE    WRK-PARA-TERMID     TO  RECE86PARA-TERMID
grpsys     MOVE    WRK-PARA-HOSPNUM    TO  RECE86PARA-HOSPNUM
      *
           INITIALIZE                      SGETTEMP-AREA
           MOVE    RECEERR             TO  SGETTEMP-BASENAMES  (1)
           MOVE    RECE01PARA-BASENAME TO  SGETTEMP-BASENAMES  (2)
           MOVE    RECE84PARA-BASENAME TO  SGETTEMP-BASENAMES  (3)
           MOVE    RECE85PARA-BASENAME TO  SGETTEMP-BASENAMES  (4)
           MOVE    RECE86PARA-BASENAME
                                       TO  SGETTEMP-BASENAMES  (5)
           CALL    "ORCSGETTEMP"       USING   SGETTEMP-AREA
           MOVE    SGETTEMP-FULLNAMES (1)
                                       TO  RECEERR
           MOVE    SGETTEMP-FULLNAMES (2)
                                       TO  RECE01PARA
           MOVE    SGETTEMP-FULLNAMES (3)
                                       TO  RECE84PARA
           MOVE    SGETTEMP-FULLNAMES (4)
                                       TO  RECE85PARA
           MOVE    SGETTEMP-FULLNAMES (5)
                                       TO  RECE86PARA
      *
           OPEN    I-O                 RECE82-FILE
      *
           OPEN    INPUT               RECE84-FILE
      *
           OPEN    INPUT               RECE85-FILE
      *
           OPEN    OUTPUT              RECE86X-FILE
           CLOSE                       RECE86X-FILE
      *
           OPEN    I-O                 RECE86-FILE
      *
           .
       110-FILE-OPEN-EXT.
           EXIT.
      * 
      *****************************************************************
      *    システム管理マスタ取得      
      *****************************************************************
       110-SYSKANRI-HENSYU-SEC      SECTION.
      *         
      *   レセプト編集情報
           MOVE    SPACE           TO  SYSKANRI-REC
           INITIALIZE                  SYSKANRI-REC
           MOVE    SPA-HOSPNUM     TO  SYS-HOSPNUM
           MOVE    "2005"          TO  SYS-KANRICD
           MOVE    "01"            TO  SYS-KBNCD
           MOVE    WRK-PARA-SRYYM  TO  SYS-STYUKYMD (1:6)
           MOVE    "01"            TO  SYS-STYUKYMD (7:2)
           MOVE    SYS-STYUKYMD    TO  SYS-EDYUKYMD
      *
           MOVE    SYSKANRI-REC    TO  MCPDATA-REC
           MOVE    "tbl_syskanri"  TO  MCP-TABLE
           MOVE    "key10"         TO  MCP-PATHNAME
           PERFORM 900-DBSELECT-SEC
           IF      MCP-RC          =   ZERO
               MOVE    "tbl_syskanri"  TO  MCP-TABLE
               MOVE    "key10"         TO  MCP-PATHNAME
               PERFORM 900-DBFETCH-SEC
               IF      MCP-RC          =   ZERO
                   MOVE    MCPDATA-REC      TO  SYS-25-REC
               ELSE    
                   INITIALIZE                   SYS-25-REC
               END-IF
           ELSE    
               INITIALIZE                   SYS-25-REC
           END-IF
           MOVE    "tbl_syskanri"  TO  MCP-TABLE
           MOVE    "key10"         TO  MCP-PATHNAME
           PERFORM 900-CLOSE-SEC
      * 
           IF      SYS-25-CHKKBN   =   SPACE
               MOVE    "0"             TO  SYS-25-CHKKBN
           END-IF
      *    
           .
       110-SYSKANRI-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       200-MAIN-SEC                SECTION.
      *
      *        行カウンタ初期化
           MOVE    ZERO                TO  WRK-LINENUM
      *    検査回数初期化
           MOVE    ZERO                TO  WRK-KENSA-KAISU
      *
           MOVE    ZERO                TO  WRK-GAIJINKEN-ZAINUM
           INITIALIZE                      WRK-RECE84-TABLE
                                           WRK-RECE84-MAX 
                                           WRK-SUM-KAISU-TABLE
                                           WRK-KENSA-MATOME-TABLE
                                           WRK-KENSA-MATOME-MAX
                                           WRK-MIN-KAISU-TABLE
                                           WRK-KENTAIKASAN-TABLE
      *
           INITIALIZE                      TBL-SORT11-INF
      *
      *    外来迅速検体検査加算算定日を確認
      ***  IF      RECE82-KENTAIKASANFLG     =   '1'
      ***      PERFORM 2009-KENTAIKASAN-KAKUNIN-SEC
      ***  END-IF
      *
      *    パラメータ０４ファイルより編集
           INITIALIZE                      RECE84-REC
           MOVE    RECE82-HOSPNUM      TO  RECE84-HOSPNUM
           MOVE    RECE82-SRYYM        TO  RECE84-SRYYM
           MOVE    RECE82-NYUGAIKBN    TO  RECE84-NYUGAIKBN
           MOVE    RECE82-PTID         TO  RECE84-PTID
           MOVE    RECE82-RECEKA       TO  RECE84-RECEKA
           MOVE    RECE82-HKNID        TO  RECE84-HKNID
           MOVE    RECE82-TEKSTYMD     TO  RECE84-TEKSTYMD
           MOVE    ZERO                TO  RECE84-ZAINUM
      *
           START   RECE84-FILE
               KEY IS    >=   RECE84-KEY
               INVALID
                   MOVE    1           TO  FLG-RECE84
               NOT INVALID
                   PERFORM 990-RECE84-NEXT-SEC
           END-START
           PERFORM
               UNTIL  ( SPA-STS              =   2                   )
               OR     ( FLG-RECE84           =   1                   )
               OR     ( RECE82-HOSPNUM   NOT =   RECE84-HOSPNUM      )
               OR     ( RECE82-SRYYM     NOT =   RECE84-SRYYM        )
               OR     ( RECE82-NYUGAIKBN NOT =   RECE84-NYUGAIKBN    )
               OR     ( RECE82-PTID      NOT =   RECE84-PTID         )
               OR     ( RECE82-RECEKA    NOT =   RECE84-RECEKA       )
               OR     ( RECE82-HKNID     NOT =   RECE84-HKNID        )
               OR     ( RECE82-TEKSTYMD  NOT =   RECE84-TEKSTYMD     )
      *
               MOVE    ZERO                TO  FLG-SRYCD1
      *
               INITIALIZE                      TBL-ZAIW-AREA
               MOVE    ZERO                TO  WRK-ZAIWMAX-G
               MOVE    ZERO                TO  WRK-TBLMAX
               MOVE    ZERO                TO  FLG-TEIGEN
                                               FLG-COMMENT
                                               FLG-COMMENT2
                                               FLG-COMMENT3
                                               FLG-COMMENT4
                                               FLG-HIKAKIN
                                               FLG-GAIJINKEN
                                               FLG-TANZAING
                                               FLG-SYOSAI-CHK
               MOVE    ZERO                TO  WRK-RECE85
                                               IDY
               MOVE    SPACE               TO  WRK-SRYSYUKBN
      *         
               PERFORM 2002-TBLZAIW-TAIHI-SEC
      *
               IF      SPA-STS          =   ZERO
      *            レセプト０４剤編集前処理
                   PERFORM 2001-RECE84-ZAIHEN-MAE-SEC
      * 
      *            レセプト０４ファイル編集処理
                   EVALUATE    RECE84-SRYKBN
      *                初診
                       WHEN    "11"
                           PERFORM 2003-SRYKBN11-HENSYU-SEC
      *                検査
                       WHEN    "60"
                       WHEN    "64"
                           PERFORM 2006-SRYKBN60-CHECK-SEC 
      *                それ以外
                       WHEN    OTHER
                           PERFORM 2005-SRYKBNELSE-HENSYU-SEC
                   END-EVALUATE
      *
                   ADD     1               TO  CNT-RECE84
      *
                   PERFORM 990-RECE84-NEXT-SEC
      *
      *            処理中止設定処理
                   IF      CNT-IN-CANCEL   =   50
                       MOVE    ZERO            TO  CNT-IN-CANCEL
                       PERFORM 500-CANCEL-HENSYU-SEC
                   END-IF
               END-IF     
           END-PERFORM
      *
           IF      SPA-STS             =   ZERO
      *        検査があるときまとめる
               IF      WRK-RECE84-MAX          >   ZERO
                   PERFORM 2007-SRYKBN60-HENSYU-SEC
               END-IF         
      *
      *        検査のとき回数を編集する
               IF      WRK-KENSA-KAISU        >   ZERO
                   EVALUATE  RECE82-HKNKBN
                     WHEN      "1"
                     WHEN      "2"
                       MOVE      WRK-KENSA-KAISU  TO  RECE82-KAISU  (17)
                     WHEN      "4"
                       EVALUATE  WRK-PARA-RECEKBN
                         WHEN      "4"
      *****************（新様式）
                           MOVE  WRK-KENSA-KAISU  TO  RECE82-KAISU  (18)
                         WHEN      "5"
      *****************（従来様式）
                           MOVE  WRK-KENSA-KAISU  TO  RECE82-KAISU  (21)
                       END-EVALUATE
                     WHEN      "5"
                       MOVE      WRK-KENSA-KAISU  TO  RECE82-KAISU  (32)
                     WHEN      "6"
                       MOVE      WRK-KENSA-KAISU  TO  RECE82-KAISU  (21)
                   END-EVALUATE
                   REWRITE RECE82-REC
               END-IF
      *
      *        初診・再診の剤まとめ編集
      *        （注）TABLEが50を超えたらZAINUMの割り当て変更が必要
               MOVE    ZERO      TO    FLG-TEIGEN
               PERFORM  VARYING  IDX6   FROM   1   BY  1
                         UNTIL   IDX6    >    34
                 IF      TBL-SORT11-FLG(IDX6)  =   1
                   PERFORM 2010-SYOSAIMATOME-HENSYU1-SEC
                 END-IF
               END-PERFORM
      *
               PERFORM 990-RECE82-READ-SEC
           END-IF
      *
           .
       200-MAIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    レセプト０４より剤編集前処理
      *****************************************************************
       2001-RECE84-ZAIHEN-MAE-SEC     SECTION.
      *
           MOVE    SPACE               TO  WRK-ZAI
           MOVE    "＊"                TO  WRK-ZAI-MK
      *    点数・回数セット
           MOVE    RECE84-ZAITEN       TO  WRK-ZAITENX
           MOVE    RECE84-ZAIKAISU     TO  WRK-ZAIKAISUX
      *    必要有効桁数・開始位置・回数編集
           PERFORM 810-TENSUKAI-HEN-SEC
      *
           .
       2001-RECE84-ZAIHEN-MAE-EXT.
           EXIT.
      *
      *****************************************************************
      *    剤　明細テーブル退避処理
      *****************************************************************
       2002-TBLZAIW-TAIHI-SEC    SECTION.
      *
      *    パラメータ０５ファイルより編集
           INITIALIZE                      RECE85-REC
           MOVE    RECE84-HOSPNUM      TO  RECE85-HOSPNUM
           MOVE    RECE84-SRYYM        TO  RECE85-SRYYM
           MOVE    RECE84-NYUGAIKBN    TO  RECE85-NYUGAIKBN
           MOVE    RECE84-PTID         TO  RECE85-PTID
           MOVE    RECE84-RECEKA       TO  RECE85-RECEKA
           MOVE    RECE84-HKNID        TO  RECE85-HKNID
           MOVE    RECE84-TEKSTYMD     TO  RECE85-TEKSTYMD
           MOVE    RECE84-ZAINUM       TO  RECE85-ZAINUM
           START   RECE85-FILE
               KEY IS    >=   RECE85-KEY
               INVALID
                   MOVE    1           TO  FLG-RECE85
               NOT INVALID
                   PERFORM 990-RECE85-NEXT-SEC
           END-START
           PERFORM
               UNTIL  ( SPA-STS              =   2                   )
               OR     ( FLG-RECE85           =   1                   )
               OR     ( RECE84-HOSPNUM   NOT =   RECE85-HOSPNUM      )
               OR     ( RECE84-SRYYM     NOT =   RECE85-SRYYM        )
               OR     ( RECE84-NYUGAIKBN NOT =   RECE85-NYUGAIKBN    )
               OR     ( RECE84-PTID      NOT =   RECE85-PTID         )
               OR     ( RECE84-RECEKA    NOT =   RECE85-RECEKA       )
               OR     ( RECE84-HKNID     NOT =   RECE85-HKNID        )
               OR     ( RECE84-TEKSTYMD  NOT =   RECE85-TEKSTYMD     )
               OR     ( RECE84-ZAINUM    NOT =   RECE85-ZAINUM       )
      *
               PERFORM   VARYING   IDX   FROM    1   BY  1
                         UNTIL   ( IDX               >   5     )
                         OR      ( RECE85-SRYCD(IDX) =   SPACE )
                         OR      ( SPA-STS           =   2     ) 
                 IF      RECE85-SIKFLG (IDX)         =   "1"        
                 AND     RECE85-RECEPTFLG(IDX)       =   SPACE
      *
                   IF      RECE85-SRYCD (IDX) (1:1)  =   "1"
                       IF      RECE85-DATAKBN(IDX)   =    2
                           MOVE    1                 TO  FLG-SRYCD1
                       END-IF
                   ELSE
                       MOVE    1                     TO  FLG-SRYCD1
                   END-IF
      *
                   IF      RECE84-SRYKBN     <   "31"
      **>          OR      RECE84-SRYKBN     =   "33"                   
                       MOVE    1             TO  IDY
                   ELSE                          
                    IF     RECE84-ZAIKBN     =    1
                       MOVE    2             TO  IDY
                    ELSE
      *              療養病棟レセコメントがある剤
                     IF    RECE84-RYOTENKISAISU  NOT = ZERO
                       IF    WRK-ZAIWMAX(3) >= RECE84-RYOTENKISAISU
                          MOVE    5          TO  IDY
                       ELSE
                          MOVE    3          TO  IDY
                       END-IF
                     ELSE
                       EVALUATE    RECE85-SRYCD (IDX) (1:1)
                           WHEN    "0"
                            IF   RECE82-HKNKBN    NOT  =  "4" AND "6"
                              IF     RECE85-SRYCD (IDX) (1:3) = "059"
                                 IF    ( RECE84-SRYKBN     =   "70" )
                                 AND   ( RECE85-DATAKBN (IDX)
                                                           =    3   )
                                     MOVE    3             TO  IDY
                                 ELSE
                                     MOVE    4             TO  IDY
                                 END-IF
                              ELSE
                                IF   RECE85-SRYCD (IDX) (1:3) = "058"
                                     MOVE    4             TO  IDY
                                ELSE
                                 IF  RECE85-SRYCD (IDX) (1:3) = "008"
                                    IF      IDY           =   ZERO
                                        MOVE    2          TO  IDY
                                    END-IF
                                 ELSE
                                    IF      IDY           =   ZERO
                                        MOVE    1          TO  IDY
                                    END-IF
                                 END-IF
                                END-IF
                              END-IF
                            ELSE
                              IF     RECE85-SRYCD (IDX) (1:3) = "059"
                                     MOVE    4             TO  IDY
                              ELSE
                                IF   RECE85-SRYCD (IDX) (1:3) = "058"
                                     MOVE    4             TO  IDY
                                ELSE
                                 IF  RECE85-SRYCD (IDX) (1:3) = "008"
                                    IF      IDY           =   ZERO
                                        MOVE    2          TO  IDY
                                    END-IF
                                 ELSE
                                    IF      IDY           =   ZERO
                                        MOVE    1          TO  IDY
                                    END-IF
                                 END-IF
                                END-IF
                              END-IF
                            END-IF
                           WHEN    "8"
                               IF   IDY = ZERO
                                   MOVE    2             TO  IDY
                               END-IF    
                           WHEN    "1"
                               IF   RECE82-HKNKBN    NOT  =  "4" AND "6"
                                   MOVE    3             TO  IDY
                               ELSE
                                   IF  RECE85-SRYCD (IDX)    =
                                           WRK-CONS-YOUBUKASAN
                                        OR WRK-CONS-KYOBUKASAN
                                        OR WRK-CONS-KEIBUKASAN
                                        OR WRK-CONS-SOGAIKASAN
                                     IF  RECE82-KOTEIKBN = SPACE OR "2"
                                       MOVE    4         TO  IDY
                                     ELSE
                                       MOVE    3         TO  IDY
                                     END-IF
                                   ELSE
                                     IF  RECE85-SRYCD (IDX)  =
                                           WRK-CONS-RYOYOTEATE
                                       IF ((RECE82-HKNKBN   = "4") AND
                                           (RECE82-JIBAIKBN = "2"))
                                       OR ( RECE82-HKNKBN   = "6" )
                                         MOVE  4         TO  IDY
                                       ELSE
                                         MOVE  3         TO  IDY
                                       END-IF
                                     ELSE
                                       MOVE    3         TO  IDY
                                     END-IF
                                   END-IF
                               END-IF
                           WHEN    "6"
                               MOVE    5             TO  IDY
                           WHEN    "7"
                            IF   RECE82-HKNKBN    NOT  =  "4" AND "6"
                               IF    ( RECE84-SRYKBN     =   "70" )
                               AND   ( RECE85-DATAKBN (IDX)
                                                         =    3   )
                                   MOVE    3                 TO  IDY
                               ELSE
                                   IF      RECE85-SRYCD (IDX)    =
                                             WRK-CONS-FILM-YOUJI-KASAN
                                       MOVE    3             TO  IDY
                                   ELSE
                                       MOVE    4             TO  IDY
                                   END-IF
                               END-IF
                            ELSE
                                       MOVE    4             TO  IDY
                            END-IF
                           WHEN    OTHER
                               DISPLAY "ORCR0910 SRYCD="
                                                 RECE85-SRYCD (IDX) 
                               MOVE    "診療行為コードが範囲外です"
                                                 TO  WRK-RECEERR
                               PERFORM 500-ERR-HENSYU-SEC
                               MOVE    2             TO  SPA-STS
                               GO  TO  2002-TBLZAIW-TAIHI-EXT
                       END-EVALUATE
                     END-IF
                    END-IF
                   END-IF            
      *
                   IF      RECE85-KENTAICOMMENT (IDX)   =   1
                       MOVE    RECE85-NAME (IDX)        TO
                                             WRK-KENTAICOMMENT-NAME
                       INSPECT WRK-KENTAICOMMENT-NAME
                               REPLACING  ALL  "〈"  BY  "（"
                                          ALL  "〉"  BY  "）"
                       MOVE    RECE85-YUKOKETA (IDX)    TO
                                             WRK-KENTAICOMMENT-YUKOKETA
                       MOVE    RECE85-SRYCD (IDX)       TO
                                             WRK-KENTAICOMMENT-SRYCD
                   ELSE
      *
                   ADD     1                 TO  WRK-ZAIWMAX (IDY)
                                                 WRK-ZAIWMAX (6)
                   IF  ((RECE84-RYOTENKISAISU      =   ZERO    ) AND
                        (WRK-ZAIWMAX (IDY)     >   TBL-ZAI-MAX ))
                   OR  ((RECE84-RYOTENKISAISU  NOT =   ZERO    ) AND
                        (WRK-ZAIWMAX (IDY)     >   TBL-ZAI-MAX2))
                       MOVE    "テーブルがオーバーしました"
                                                 TO  WRK-RECEERR
                       PERFORM 500-ERR-HENSYU-SEC
                       MOVE    2                 TO  SPA-STS
                       GO  TO  2002-TBLZAIW-TAIHI-EXT
                   ELSE
      *                先頭の診療行為コード編集              
                       IF      WRK-RECE85        =   ZERO
                         IF  ( RECE85-SRYCD(IDX)(1:1)  =  "8"       )
                         OR  ((RECE85-SRYCD(IDX)(1:3)  NOT =  "059") AND
                              (RECE85-SRYCD(IDX)(1:1)  =  "0"      ))
                         OR  ( RECE84-SAFLG       NOT  =  SPACE     )
                         OR  ( RECE84-ZAIKBN           =   1        )
                           CONTINUE
                         ELSE
                           MOVE    RECE85-SRYCD (IDX)
                                                 TO  WRK-ZAIW-SRYCD1 
                           MOVE    RECE85-CDKBN-KBN(IDX)
                                                 TO  WRK-ZAIW-CDKBN-KBN
                           MOVE    RECE85-CDKBN-SYO(IDX)
                                                 TO  WRK-ZAIW-CDKBN-SYO
                           MOVE    RECE85-CDKBN-BU(IDX)
                                                 TO  WRK-ZAIW-CDKBN-BU
                           MOVE    RECE85-CDKBN-KBNNUM(IDX)
                                         TO  WRK-ZAIW-CDKBN-KBNNUM
                           MOVE    RECE85-CDKBN-KBNNUM-EDA(IDX)
                                         TO  WRK-ZAIW-CDKBN-KBNNUM-EDA
                           MOVE    RECE85-CDKBN-KOUBAN(IDX)
                                         TO  WRK-ZAIW-CDKBN-KOUBAN
                           MOVE    RECE85-KOUHYOJYUNNUM(IDX)
                                         TO  WRK-ZAIW-KOUHYOJYUNNUM
                           MOVE    RECE85-KNSJISKBN(IDX)
                                         TO  WRK-ZAIW-KNSJISKBN
                           MOVE    RECE85-DATAKBN(IDX)
                                         TO  WRK-ZAIW-DATAKBN
                           MOVE    RECE85-KOKUJISKBKBN1(IDX)
                                         TO  WRK-ZAIW-KOKUJISKBKBN1
                           MOVE    RECE85-KOKUJISKBKBN2(IDX)
                                         TO  WRK-ZAIW-KOKUJISKBKBN2
                           MOVE    1             TO  WRK-RECE85
                         END-IF
                       END-IF
      *
                       MOVE    RECE85-SRYSYUKBN  TO  WRK-SRYSYUKBN
      *                                                         
                       MOVE    WRK-ZAIWMAX (IDY) TO  IDZ
                       MOVE    RECE85-SRYACTINF (IDX)
                                     TO  TBL-ZAIW-SRYACTINF (IDY IDZ)
                       MOVE    RECE85-NAMEINF   (IDX)
                                     TO  TBL-ZAIW-NAMEINF   (IDY IDZ)
                       IF  RECE85-INGAIFLG-RECE(IDX) = "1"
                         MOVE  RECE85-GENERICNAME(IDX)
                                     TO  TBL-ZAIW-NAME(IDY IDZ)
                         MOVE  RECE85-GENERICYUKOKETA(IDX)
                                     TO  TBL-ZAIW-YUKOKETA(IDY IDZ)
                       END-IF
                       MOVE    RECE85-RECEHENFLG(IDX)
                                     TO  TBL-ZAIW-RECEHENFLG(IDY IDZ)
                       MOVE    RECE85-SRYSYUKBN  TO
                                     TBL-ZAIW-SRYSYUKBN-INPUT(IDY IDZ)
                       IF      WRK-KENTAICOMMENT-SRYCD  NOT =  ZERO
                           STRING  TBL-ZAIW-NAME (IDY IDZ)
                                     DELIMITED   BY  SPACE
                                   WRK-KENTAICOMMENT-NAME
                                     DELIMITED   BY  SPACE
                                   INTO  WRK-KENTAICOMMENT-NAME-TEMP
                           END-STRING
                           MOVE    WRK-KENTAICOMMENT-NAME-TEMP
                                     TO  TBL-ZAIW-NAME (IDY IDZ)
                           COMPUTE TBL-ZAIW-YUKOKETA (IDY IDZ)  =
                                   TBL-ZAIW-YUKOKETA (IDY IDZ)  +
                                   WRK-KENTAICOMMENT-YUKOKETA
                           MOVE    WRK-KENTAICOMMENT-SRYCD      TO
                                  TBL-ZAIW-KENTAICOMMENT-SRYCD (IDY IDZ)
                           MOVE    ZERO          TO
                                     WRK-KENTAICOMMENT-YUKOKETA
                                     WRK-KENTAICOMMENT-SRYCD
                           MOVE    SPACE         TO
                                     WRK-KENTAICOMMENT-NAME
                                     WRK-KENTAICOMMENT-NAME-TEMP
                       END-IF
     *
                       IF      RECE85-SRYCD (IDX) (1:1)
                                                 =   "1"
                       AND     RECE84-SRYKBN     =   "70"
                       AND     RECE85-TANICD(IDX)
                                                 =   WRK-CONS-TANICD
                           MOVE    WRK-CONS-TANINAME TO 
                                         TBL-ZAIW-TANINAME  (IDY IDZ) 
                       END-IF                     
      *
                       IF      RECE85-SRYCD (IDX) (1:1)  =  "7"
                          IF    ( RECE84-SRYKBN     =   "70" )
                          AND   ( RECE85-DATAKBN (IDX)
                                                    =    3   )
                            IF  TBL-ZAIW-TANINAME  (IDY IDZ)  
                                                    =   SPACE
                                  MOVE    "＊"  TO 
                                         TBL-ZAIW-TANINAME  (IDY IDZ) 
                            END-IF
                          ELSE
                            IF      RECE85-SRYCD (IDX)    =
                                     WRK-CONS-FILM-YOUJI-KASAN  OR
                                     WRK-CONS-SANSO             OR
                                     WRK-CONS-SANSO2
                               CONTINUE
                            ELSE
                               IF  TBL-ZAIW-TANINAME  (IDY IDZ)  
                                                    =   SPACE
                                  MOVE    "＊"  TO 
                                         TBL-ZAIW-TANINAME  (IDY IDZ) 
                               END-IF
                            END-IF
                          END-IF
                       END-IF
      *
                       IF      RECE85-SRYCD (IDX) (1:1)  =  "6"
                          IF   RECE85-SRYCD (IDX) (1:2)  =  "63"
                               CONTINUE
                          ELSE
                            IF  TBL-ZAIW-TANINAME  (IDY IDZ)  
                                                    =   SPACE
                                  MOVE    "＊"  TO 
                                         TBL-ZAIW-TANINAME  (IDY IDZ) 
                            END-IF
                          END-IF
                       END-IF
      *                コメント
                       IF      RECE85-SRYCD(IDX) =
                               "810000001" OR "099999901"
                           MOVE    1             TO  FLG-COMMENT
                       END-IF 
                       IF      RECE85-SRYCD(IDX) =
                               "820000003" OR "820000004" OR
                               "820000005" OR "820000006"
                           MOVE    1             TO  FLG-COMMENT2
                       END-IF
                       IF     (RECE85-SRYCD(IDX)(1:1)   =   "8"  )
                       OR     (RECE85-SRYCD(IDX)(1:3)   =   "008")
                           MOVE    1             TO  FLG-COMMENT3
                       END-IF
      *                剤にコメント入力有
                       IF   (RECE82-SRYYM     >=  "202204"    )
                       AND  (RECE84-SRYKBN     =  "11"        )
                         IF    (FLG-COMMENT4           = ZERO)
                         AND   (RECE85-SRYCD(IDX)(1:1) = "1" )
                            MOVE    1     TO    FLG-COMMENT4
                         END-IF
                         IF   ( FLG-COMMENT4           =  1    )
                         AND  ((RECE85-SRYCD(IDX)(1:1) = "8"  ) OR
                               (RECE85-SRYCD(IDX)(1:3) = "008"))
                            MOVE    2     TO    FLG-COMMENT4
                         END-IF
                       END-IF
      *                逓減・減点
                       IF      RECE85-SRYCD(IDX) =
                               "630010001" OR "630010002" OR
                               "199000210" OR "199000410"
                           MOVE    1             TO  FLG-TEIGEN
                       END-IF
      *                レセ電請求時の受付エラー分チェック
      *                （公務災害以外）
                       IF    ( SYS-25-CHKKBN       =   "1" OR  "2" )
                       AND   ( RECE82-RECESYUBETU  =
                                               "7081"  OR  "7082"  OR
                                               "7101"  OR  "7102"     )
                       AND   ( RECE82-ERR-ERRKBN   =   ZERO        )
                           PERFORM 20021-DATA-CHK-SEC
                       END-IF
                   END-IF
      *
                   END-IF
                 END-IF
               END-PERFORM
               ADD     1                 TO  CNT-RECE85
      *
               PERFORM 990-RECE85-NEXT-SEC
      *
      *        処理中止設定処理
               IF      CNT-IN-CANCEL   =   50
                   MOVE    ZERO            TO  CNT-IN-CANCEL
                   PERFORM 500-CANCEL-HENSYU-SEC
               END-IF
           END-PERFORM
      *
           .
       2002-TBLZAIW-TAIHI-EXT.
           EXIT.
      *
      *****************************************************************
      *    レセ電請求時の受付エラー分チェック処理
      *****************************************************************
       20021-DATA-CHK-SEC    SECTION.
      *
      *    自費分はチェックしない
           IF      RECE84-SRYKBN       =   "95"    OR  "96"
               GO  TO  20021-DATA-CHK-EXT
           END-IF
      *
      *    コメントの文字データの入力がなかったとき
           IF    ( RECE82-ERR-ERRKBN   =   ZERO        )
           AND   ( RECE85-SRYCD (IDX)(1:1)
                                       =   "0" OR  "8" )
               IF      RECE85-SRYCD (IDX)(1:2)
                                           =   "82"
                   CONTINUE
               ELSE
                   IF      RECE85-SRYCD (IDX)  =   "810000001"
      *                空白または全角スペースのみのとき
                       IF      RECE85-NAME (IDX)   =   SPACE
                           MOVE    11  TO  RECE82-ERR-ERRKBN
                       ELSE
                           MOVE    ZERO    TO  FLG-COM-CHK
                           PERFORM VARYING IDXX    FROM    1   BY  2
                                   UNTIL   IDXX    >   300
                                   OR  RECE85-NAME(IDX)(IDXX:2)
                                                   =   SPACE
                                   OR  FLG-COM-CHK =   1
                               IF       RECE85-NAME(IDX) (IDXX:2)
                                                       NOT =   "　"
                                    MOVE    1          TO  FLG-COM-CHK
                               END-IF
                           END-PERFORM
                           IF      FLG-COM-CHK =   ZERO
                               MOVE    11          TO  RECE82-ERR-ERRKBN
                           END-IF
                       END-IF    
                   ELSE
                       IF    ( RECE85-SRYCD (IDX) (1:1)    =   "8"   )
                       OR    ( RECE85-SRYCD (IDX) (1:3)    =   "008" )
                           IF      RECE85-INPUTCOMENT (IDX)    =   SPACE
                               MOVE    11          TO  RECE82-ERR-ERRKBN
                           ELSE
                               MOVE    ZERO        TO  FLG-COM-CHK
                               PERFORM VARYING IDXX    FROM    1   BY  2
                                       UNTIL   IDXX    >       140
                                       OR      RECE85-INPUTCOMENT (IDX)
                                                              (IDXX:2)
                                                       =   SPACE
                                       OR  FLG-COM-CHK =   1
                                   IF      RECE85-INPUTCOMENT (IDX)
                                                               (IDXX:2)
                                                       NOT =   "　"
                                       MOVE    1       TO  FLG-COM-CHK
                                   END-IF
                               END-PERFORM
                               IF      FLG-COM-CHK =   ZERO
                                   MOVE    11      TO  RECE82-ERR-ERRKBN
                               END-IF
                           END-IF
      *
      *                    コメントパターン４０の文字チェック
                           IF    ( RECE82-ERR-ERRKBN   =   ZERO )
                           AND   ( RECE85-SRYCD (IDX)(1:2)
                                                       =   "84" )
                               COMPUTE WRK-MOJISU  =
                                         ( RECE85-SSTKIJUNCD (IDX 2) +
                                           RECE85-SSTKIJUNCD (IDX 4) +
                                           RECE85-SSTKIJUNCD (IDX 6) +
                                           RECE85-SSTKIJUNCD (IDX 8)   )
                                                   *   2   -   1
                               IF      RECE85-INPUTCOMENT (IDX)
                                                       (WRK-MOJISU:2)
                                                       =   "　"    OR
                                                           SPACE
                                   MOVE    11      TO  RECE82-ERR-ERRKBN
                               END-IF
                           END-IF 
                       ELSE
                           IF      RECE85-NAME (IDX)   =   SPACE
                               MOVE    11          TO  RECE82-ERR-ERRKBN
                           ELSE
                               MOVE    ZERO        TO  FLG-COM-CHK
                               PERFORM VARYING IDXX    FROM    1   BY  2
                                       UNTIL   IDXX    >       300
                                       OR  RECE85-NAME (IDX)  (IDXX:2)
                                                       =   SPACE
                                       OR  FLG-COM-CHK =   1
                                   IF      RECE85-NAME (IDX)   (IDXX:2)
                                                       NOT =   "　"
                                       MOVE    1       TO  FLG-COM-CHK
                                   END-IF
                               END-PERFORM
                               IF      FLG-COM-CHK  =   ZERO
                                   MOVE    11      TO  RECE82-ERR-ERRKBN
                               END-IF
                           END-IF
                       END-IF
                   END-IF    
               END-IF
           END-IF
      *
      *    特定器材の単位コードが未設定のとき（金額種別５以外）
           IF      RECE82-ERR-ERRKBN   =   ZERO 
               IF    ( RECE85-SRYCD (IDX)(1:1)
                                           =   "7" )
               AND   ( RECE85-TENSIKIBETU (IDX)
                                       NOT =   "5" )
                   IF      RECE85-TANICD (IDX) =   0
                       MOVE    12          TO  RECE82-ERR-ERRKBN
                   END-IF
               END-IF
           END-IF
      *
           IF      RECE82-ERR-ERRKBN   =   ZERO
               CONTINUE
           ELSE
               REWRITE RECE82-REC
           END-IF

           .
       20021-DATA-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療区分（１１）編集処理
      *****************************************************************
       2003-SRYKBN11-HENSYU-SEC    SECTION.
      *
      *    剤　明細テーブルチェック
           INITIALIZE                    TBL-ZAI-AREA
           MOVE    ZERO              TO  WRK-TBLMAX
           MOVE    ZERO              TO  WRK-TENSUGOKEI
           MOVE    ZERO              TO  FLG-GENTENCD-ARI
      *
           PERFORM VARYING IDX1    FROM    1   BY  1
                   UNTIL   IDX1    >       1  
               PERFORM VARYING IDX2    FROM    1   BY  1
                         UNTIL IDX2    >   WRK-ZAIWMAX (IDX1)  
                   MOVE    IDX1          TO  WRK-WIDX1
                   MOVE    IDX2          TO  WRK-WIDX2
                   PERFORM 20031-SRYKBN11-CHECK-SEC
                   IF  FLG-NOPRT         =   ZERO
      *            レセプト０５剤編集処理
                       PERFORM 430-TBLZAI-HEN-SEC
                   END-IF
               END-PERFORM
           END-PERFORM    
      *
           IF  WRK-TBLMAX           >   ZERO
      ***      IF  WRK-TBLMAX       NOT =   RECE84-MEISAISU
               IF  WRK-TENSUGOKEI   NOT =   RECE84-ZAITEN
                   PERFORM 400-TENSU-SAIKEISAN-SEC
               END-IF
      *        点数・回数セット処理
               MOVE    WRK-TBLMAX       TO  IDY
               PERFORM 440-TNSKAI-SET-SEC
               MOVE    IDY              TO  WRK-TBLMAX
      *        剤・診療行為テーブル出力処理
               PERFORM 450-RECE86-OUT-SEC
           END-IF
      *
           .
       2003-SRYKBN11-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    明細印字チェック 診療区分（１１） 処理
      *****************************************************************
       20031-SRYKBN11-CHECK-SEC        SECTION.
      *
           MOVE    ZERO          TO  FLG-NOPRT
      *
      *    服用方法でないこと
           IF  TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2)(1:3)   =   "001"
               MOVE    1             TO  FLG-NOPRT
           END-IF
      *
           IF   RECE84-JIHOKBN  =  SPACE
             IF   RECE82-SRYYM   >=   "202204"
               PERFORM  VARYING  IDY  FROM  1  BY  1
                          UNTIL (IDY        >  TBL-NOSRYCD-MAX)
                           OR   (FLG-NOPRT  =  1              )
                 IF  TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2)
                                            =  TBL-NOSRYCD(IDY)
                   MOVE    1    TO    FLG-NOPRT
                 END-IF
               END-PERFORM
      *
               IF  FLG-NOPRT = ZERO
                 IF  (FLG-COMMENT4    =   2)
                 OR  (FLG-TANZAING    =   1)
                   IF  (FLG-SYOSAI-CHK        = ZERO                  )
                   AND (TBL-ZAIW-SRYCD(WRK-WIDX1 WRK-WIDX2)(1:1) = "1")
                   AND (WRK-ZAIW-SRYCD1(1:1)  = "1"                   )
                     IF TBL-ZAIW-SRYCD(WRK-WIDX1 WRK-WIDX2)
                                          NOT = WRK-ZAIW-SRYCD1
                       MOVE  TBL-ZAIW-CDKBNINF(WRK-WIDX1 WRK-WIDX2)
                                    TO    WRK-ZAIW-CDKBN-AREA
                     END-IF
                     MOVE      1    TO    FLG-SYOSAI-CHK
                   END-IF
                 ELSE
                   PERFORM  VARYING  IDY  FROM  1  BY  1
                             UNTIL  (IDY        >  TBL-SYOSRYCD-MAX)
                              OR    (FLG-NOPRT  =  1               )
                     IF  TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2)
                                                =  TBL-SYOSRYCD(IDY)
                       MOVE    1    TO    FLG-NOPRT
                     END-IF
                   END-PERFORM
                 END-IF
               END-IF
             ELSE
               IF ((TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2)  =
                     "111003770" OR "111700470" OR "111010370" OR
                     "111010870" OR "111010970" OR "111011070" OR
                     "111011170" OR "111011270" OR "111011370" OR
                     "111009970" OR "111010070" OR "111010170" OR
                     "111010270" OR "111702770" OR "111702870" OR
                     "111702370" OR "111702470" OR "111702570" OR
                     "111702670" OR "111700870" OR "111012970" OR
                     "111013070" OR "111013170" OR "111013270" OR
                     "111013370" OR "111013470" OR "111013570" OR
                     "111013670" OR "111014070" OR "111012370"     ) AND
                   (RECE82-SRYYM   <=   "202109"                   ))
      *        同一日複数科受診時の２科目
               OR ( TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2)  =
                     "111011810" OR "111012610" OR "111012810" OR
                     "111014510" OR "111014610" OR "111014710"      )
      *        情報通信機器
               OR ( TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2)  =
                     "111014210" OR "111014310" OR "111014410"      )
               OR ((TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2)(1:1) = "8") AND
                   (TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2)   
                                                  NOT = "830000020"))
               OR ( TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2)(1:3)
                                                      = "008"       )
      *        公務災害用
               OR ( TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2)  =
                     "101989010" OR "101989020" OR "101989050"      )
                     CONTINUE
               ELSE
                     MOVE    1                 TO  FLG-NOPRT
               END-IF
             END-IF
           END-IF
      *
      *    識別が点数の場合
           IF  FLG-NOPRT                     =   ZERO
               IF  TBL-ZAIW-TENSIKIBETU (WRK-WIDX1 WRK-WIDX2)
                                     =   3
                   ADD     TBL-ZAIW-TEN (WRK-WIDX1 WRK-WIDX2)
                                         TO  WRK-TENSUGOKEI
               END-IF
               IF  TBL-ZAIW-TENSIKIBETU (WRK-WIDX1 WRK-WIDX2)
                                     =   8
                   COMPUTE WRK-TENSUGOKEI =
                           WRK-TENSUGOKEI -
                           TBL-ZAIW-TEN (WRK-WIDX1 WRK-WIDX2)
                   MOVE    1             TO  FLG-GENTENCD-ARI
               END-IF
           END-IF
      *
           IF   RECE84-JIHOKBN  =  SPACE
            IF  ((RECE82-SRYYM   >=  "202204") AND
                 (FLG-COMMENT4    =   2      ))
            OR  ( FLG-TANZAING    =   1       )
              CONTINUE
            ELSE
              MOVE     ZERO     TO   FLG-ARI
              PERFORM  VARYING  IDY  FROM  1  BY  1
                        UNTIL  (IDY      >  TBL-SYOSRYCD-MAX)
                         OR    (FLG-ARI  =    1             )
                IF  TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2)
                                         =  TBL-SYOSRYCD(IDY)
                  MOVE  1               TO TBL-SORT11-FLG(IDY)
                  MOVE  TBL-ZAIW-TEN (WRK-WIDX1 WRK-WIDX2)
                                        TO TBL-SORT11-TEN(IDY)
                  MOVE  TBL-ZAIW-TENSIKIBETU(WRK-WIDX1 WRK-WIDX2)
                                        TO TBL-SORT11-TENSIKIBETU(IDY)
                  MOVE  TBL-ZAIW-YUKOKETA(WRK-WIDX1 WRK-WIDX2)
                                        TO TBL-SORT11-YUKOKETA(IDY)
                  MOVE  TBL-ZAIW-NAME(WRK-WIDX1 WRK-WIDX2)
                                        TO TBL-SORT11-NAME(IDY)
                  MOVE  TBL-ZAIW-CDKBNINF(WRK-WIDX1 WRK-WIDX2)
                                        TO TBL-SORT11-CDKBNINF(IDY)
                  IF  TBL-ZAIW-TENSIKIBETU(WRK-WIDX1 WRK-WIDX2) =  8
                    MOVE  "9"   TO  TBL-SORT11-CDKBN-KBN(IDY)
                    MOVE   9    TO  TBL-SORT11-CDKBN-SYO(IDY)
                    MOVE  99    TO  TBL-SORT11-CDKBN-BU(IDY)
                    MOVE  999   TO  TBL-SORT11-CDKBN-KBNNUM(IDY)
                    MOVE  99    TO  TBL-SORT11-CDKBN-KBNNUM-EDA(IDY)
                    MOVE  999   TO  TBL-SORT11-CDKBN-KOUBAN(IDY)
                  END-IF
                  ADD   RECE84-ZAIKAISU TO TBL-SORT11-ZAIKAISU(IDY)
                  MOVE  RECE84-HKNCOMBI TO TBL-SORT11-HKNCOMBI(IDY)
                  MOVE  1               TO FLG-ARI
                END-IF
              END-PERFORM
            END-IF
           END-IF
      *
           .
       20031-SRYKBN11-CHECK-EXT.
           EXIT.
      *
      *****************************************************************
      *    レセプト０４ファイル  診療区分（その他）編集処理
      *****************************************************************
       2005-SRYKBNELSE-HENSYU-SEC    SECTION.
      *
      *    検査のとき剤が一つ以上なら”、”で編集
           IF  RECE84-SRYKBN         =   "60" OR "64"
      *****AND  WRK-ZAIWMAX (6)      >   1
               PERFORM 20051-SRYKBNELSE-KENSA-SEC
           ELSE 
               PERFORM 20052-SRYKBNELSE-CHECK-SEC
           END-IF
      *
           IF  WRK-TBLMAX           >   ZERO
               IF      RECE84-SRYKBN      >=  "33"                      
      **>      IF      RECE84-SRYKBN      >   "33"                      
               AND     RECE84-ZAIKBN   NOT =   1
               AND     RECE84-JIHOKBN      =   SPACE
                   CONTINUE
               ELSE        
                   MOVE    WRK-TBLMAX       TO  IDY
                   PERFORM 440-TNSKAI-SET-SEC
                   MOVE    IDY              TO  WRK-TBLMAX
               END-IF    
      *        剤・診療行為テーブル出力処理
               PERFORM 450-RECE86-OUT-SEC
           END-IF
      *
           .
       2005-SRYKBNELSE-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    剤　明細テーブル編集　 診療区分（その他）検査のとき処理
      *****************************************************************
       20051-SRYKBNELSE-KENSA-SEC      SECTION.
      *
           MOVE    WRK-ZAIWMAX (3)     TO  WRK-KENSA-MAX
           MOVE    ZERO                TO  WRK-TNS-HOUKNSKBN
                                           WRK-KOUMOKUSU
      *     
           PERFORM VARYING IDX FROM    1   BY  1
                   UNTIL   IDX >       WRK-ZAIWMAX (3)
               IF    ( TBL-ZAIW-HOUKNSKBN(3 IDX) =   11   )
               AND   ( TBL-ZAIW-KZMCOMPSIKIBETU  (3 IDX)
                                                 >   ZERO ) 
      *            数量編集
                   MOVE    3               TO  WRK-WIDX1
                   MOVE    IDX             TO  WRK-WIDX2
                   PERFORM 4301-SURYO-HEN1-SEC
                   MOVE    SPACE           TO  TBL-NAME (IDX)
                   STRING  TBL-ZAIW-NAME     (3 IDX)
                                              DELIMITED  BY  SPACE
                           "（"               DELIMITED  BY  SIZE
                           WRK-SRYSURYOW      DELIMITED  BY  SPACE
                           TBL-ZAIW-TANINAME(3 IDX)
                                              DELIMITED  BY  SPACE
                           "）"               DELIMITED  BY  SIZE
                           INTO               TBL-NAME (IDX)
                   END-STRING
                   MOVE    ZERO               TO  TBL-YUKOKETA (IDX)
                   MOVE    SPACE   TO   TBL-ZAIW-TANINAME(3 IDX)
               ELSE
                   MOVE    TBL-ZAIW-NAMEINF (3 IDX)
                                              TO  TBL-NAMEINF (IDX)
               END-IF
               MOVE    TBL-ZAIW-SRYSURYO(3 IDX)  TO  TBL-SRYSURYO(IDX)
               MOVE    TBL-ZAIW-TANICD  (3 IDX)  TO  TBL-TANICD  (IDX)
               MOVE    TBL-ZAIW-TANIMOJISU(3 IDX)
                                             TO  TBL-TANIMOJISU(IDX)
               MOVE    TBL-ZAIW-TANINAME(3 IDX)  TO  TBL-TANINAME(IDX)
               MOVE    TBL-ZAIW-SRYCD   (3 IDX)  TO  TBL-SRYCD   (IDX)
               MOVE    TBL-ZAIW-DATAKBN (3 IDX)  TO  TBL-DATAKBN (IDX)
               MOVE    TBL-ZAIW-HOUKNSKBN(3 IDX) TO  TBL-HOUKNSKBN(IDX)
               IF      TBL-YUKOKETA (IDX)      =   ZERO
                   PERFORM VARYING IDZ FROM   300 BY  -1
                           UNTIL   IDZ <      1
                       IF      TBL-NAME (IDX) (IDZ:1)    NOT =   SPACE
                           COMPUTE TBL-YUKOKETA (IDX)    =
                                   IDZ     /   2
                           MOVE    1          TO  IDZ
                       END-IF
                   END-PERFORM
               END-IF             
           END-PERFORM
      ***  MOVE    TBL-ZAIW-SRYCD   (1)  TO  TBL-SRYCD
      *
           COMPUTE WRK-ZAIWMAX (6)   =   WRK-ZAIWMAX  (6)     -   
                                         WRK-ZAIWMAX  (3) 
           INITIALIZE                    TBL-ZAIW-OCC (3)
           MOVE    ZERO              TO  WRK-ZAIWMAX  (3)
      *
      *    剤が一つ以上のとき’、’で名称を編集する
      *
           MOVE    1                 TO  IDX
               
           PERFORM UNTIL   IDX     >       WRK-KENSA-MAX
               IF      TBL-HOUKNSKBN(IDX)   =   ZERO
               OR      FLG-SRYCD1           =    1
                   IF     (TBL-HOUKNSKBN(IDX)  NOT =  ZERO)
                   AND    (FLG-SRYCD1              =   1  )
                     IF    IDX  =   1
                       MOVE  TBL-HOUKNSKBN(IDX)  TO  WRK-TNS-HOUKNSKBN
                     END-IF
                     IF  TBL-DATAKBN (IDX)   NOT =    2
                       ADD     1                 TO  WRK-KOUMOKUSU
                     END-IF
                   END-IF
                   ADD     1                        TO  WRK-ZAIWMAX (3)
                   MOVE    WRK-ZAIWMAX (3)          TO  IDZ
                   MOVE    TBL-NAMEINF (IDX)   TO
                                    TBL-ZAIW-NAMEINF (3 IDZ)
                   MOVE    TBL-SRYSURYO(IDX)   TO
                                    TBL-ZAIW-SRYSURYO(3 IDZ)
                   MOVE    TBL-TANICD  (IDX)   TO
                                    TBL-ZAIW-TANICD  (3 IDZ)
                   MOVE    TBL-TANIMOJISU(IDX) TO
                                    TBL-ZAIW-TANIMOJISU(3 IDZ)
                   MOVE    TBL-TANINAME(IDX)   TO
                                    TBL-ZAIW-TANINAME(3 IDZ)
                   MOVE    TBL-SRYCD   (IDX)   TO
                                    TBL-ZAIW-SRYCD   (3 IDZ)
                   MOVE    TBL-DATAKBN (IDX)   TO
                                    TBL-ZAIW-DATAKBN (3 IDZ)
                   MOVE    TBL-HOUKNSKBN(IDX)  TO
                                    TBL-ZAIW-HOUKNSKBN(3 IDZ)
                   IF      (FLG-GAIJINKEN  =   1) 
      ****           AND   (IDX            =   WRK-KENSA-MAX) 
                     AND   (TBL-SRYCD (IDX)(1:1)
                                       NOT =   "8")
                     AND   (TBL-SRYCD (IDX)(1:3)
                                       NOT =   "008")
                           PERFORM 200511-GJK-DAY-ADD-SEC
                   END-IF
                   ADD     1                   TO  IDX                     
               ELSE
                   IF      IDX             =   1
                       MOVE    TBL-HOUKNSKBN(IDX)
                                           TO  WRK-TNS-HOUKNSKBN         
                   END-IF                                
                   MOVE    ZERO            TO  WRK-YUKOKETA
                   MOVE    SPACE           TO  WRK-NAME
                   MOVE    TBL-SRYCD (IDX) TO  WRK-SRYCD
                   PERFORM     UNTIL   IDX >       WRK-KENSA-MAX
                               OR      TBL-SRYCD (IDX) (1:1)
                                           NOT =   WRK-SRYCD
      *                次の名称があるとき”、”＋名称をつける
                       IF      WRK-YUKOKETA    >   ZERO
                           STRING  WRK-NAME    DELIMITED  BY  SPACE
                                   "，"        DELIMITED  BY  SIZE
                                   INTO        WRK-NAME
                           END-STRING        
                           ADD     2           TO  WRK-YUKOKETA
      *              
                           COMPUTE IDZ     =   WRK-YUKOKETA        +
                                               TBL-YUKOKETA (IDX)  *  2
                           IF      IDZ             >   38
                               ADD     1               TO
                                                   WRK-ZAIWMAX (3)
                               MOVE    WRK-ZAIWMAX (3) TO  IDZ
                               MOVE    WRK-NAME        TO
                                           TBL-ZAIW-NAME(3 IDZ)
                               COMPUTE TBL-ZAIW-YUKOKETA (3 IDZ) 
                                           =   WRK-YUKOKETA   /   2  
                               MOVE    SPACE           TO  WRK-NAME
                               MOVE    ZERO            TO  WRK-YUKOKETA
                           END-IF
                       END-IF
      *
                       IF      TBL-YUKOKETA (IDX)  >   19
                           ADD     1               TO  WRK-ZAIWMAX (3)
                           MOVE    WRK-ZAIWMAX (3) TO  IDZ
                           MOVE    TBL-NAME (IDX) (1:38)   TO
                                       TBL-ZAIW-NAME    (3 IDZ)
                           MOVE    19                      TO  
                                       TBL-ZAIW-YUKOKETA(3 IDZ)
                           MOVE    TBL-NAME (IDX) (39:)    TO  WRK-NAME
                           COMPUTE WRK-YUKOKETA    =  
                                   ( TBL-YUKOKETA (IDX) -   19 )  *   2
                       ELSE
                           STRING  WRK-NAME        DELIMITED  BY  SPACE
                                   TBL-NAME    (IDX)
                                                   DELIMITED  BY  SPACE
                                   INTO            WRK-NAME
                           END-STRING
                           COMPUTE WRK-YUKOKETA =  WRK-YUKOKETA   +
                                       TBL-YUKOKETA (IDX)  *   2
                       END-IF
      *                 
                       IF      TBL-DATAKBN (IDX)   NOT =    2
                           ADD     1           TO  WRK-KOUMOKUSU 
                       END-IF
                       ADD     1           TO  IDX
                   END-PERFORM                            
                   ADD     1               TO  WRK-ZAIWMAX (3)
                   MOVE    WRK-ZAIWMAX (3) TO  IDZ
                   MOVE    WRK-NAME    TO  TBL-ZAIW-NAME (3 IDZ)
                   COMPUTE TBL-ZAIW-YUKOKETA (3 IDZ) 
                                       =   WRK-YUKOKETA   /   2
                   IF      (FLG-GAIJINKEN  =   1) 
                           PERFORM 200512-GJK-DAY-ADD-SEC
                   END-IF
               END-IF
           END-PERFORM
      *
      *    検査の項目数をコメントにセット
           IF      WRK-TNS-HOUKNSKBN  >   ZERO
               INITIALIZE                 WRK-CHAR-HENKAN-AREA
               MOVE    1                  TO  WRK-CHAR-FLG
               MOVE    WRK-KOUMOKUSU      TO  WRK-Z9
               MOVE    WRK-Z9 (3:2)       TO  WRK-CHAR-X
               MOVE    2                  TO  WRK-CHAR-CNT
               PERFORM 800-NUM-ZENKAKU-HENKAN-SEC
               ADD     1                  TO  WRK-ZAIWMAX (2)
                                              WRK-ZAIWMAX (6)
               MOVE    WRK-ZAIWMAX (2)    TO  IDZ   
               IF      WRK-TNS-HOUKNSKBN  =   11
                   MOVE    WRK-CONS-KENSACOMMENT (WRK-TNS-HOUKNSKBN)
                                          TO    TBL-ZAIW-NAME (2 IDZ)
               ELSE
                   STRING  WRK-CONS-KENSACOMMENT (WRK-TNS-HOUKNSKBN)
                                              DELIMITED  BY  SPACE
                           "（"               DELIMITED  BY  SIZE
                           WRK-CHAR-N         DELIMITED  BY  SPACE
                           "項目）"           DELIMITED  BY  SIZE
                                          INTO    TBL-ZAIW-NAME (2 IDZ)
                   END-STRING
               END-IF
               PERFORM VARYING IDX FROM   300 BY  -1
                       UNTIL   IDX <      1
                   IF      TBL-ZAIW-NAME (2 IDZ) (IDX:1)
                                              NOT =   SPACE
                       COMPUTE TBL-ZAIW-YUKOKETA (2 IDZ)    =
                                                  IDX     /   2
                       MOVE    1          TO  IDX
                   END-IF
               END-PERFORM
               MOVE    "88888888"         TO  TBL-ZAIW-SRYCD(2 IDZ)
           END-IF                                    
      *
           COMPUTE WRK-ZAIWMAX (6)   =   WRK-ZAIWMAX  (6)     +   
                                         WRK-ZAIWMAX  (3) 
           MOVE    WRK-ZAIW-SRYCD1   TO  TBL-ZAIW-SRYCD (3 1) 
      *
           PERFORM 20052-SRYKBNELSE-CHECK-SEC
           .
       20051-SRYKBNELSE-KENSA-EXT.
           EXIT.
      *
      *****************************************************************
      *    外来迅速検体検査加算算定日編集処理
      *****************************************************************
       200511-GJK-DAY-ADD-SEC          SECTION.
      *
           INITIALIZE                  WRK-CHAR-HENKAN-AREA
      *
           PERFORM VARYING IDV FROM 1  BY  1
                   UNTIL   IDV >           31
                   IF  RECE84-KAISU (1 IDV)    >   0
                       MOVE    IDV     TO  WRK-Z9
                       MOVE    31      TO  IDV
                   END-IF
           END-PERFORM
      *
           MOVE    1                   TO  WRK-CHAR-FLG
           MOVE    WRK-Z9 (3:2)        TO  WRK-CHAR-X
           MOVE    2                   TO  WRK-CHAR-CNT
           MOVE    IDZ                 TO  IDV
           PERFORM 800-NUM-ZENKAKU-HENKAN-SEC
           MOVE    IDV                 TO  IDZ
      * 
           IF      WRK-Z9              <   10
                   MOVE    4           TO  IDV
           ELSE
                   MOVE    5           TO  IDV
           END-IF
           MOVE    SPACE               TO  WRK-NAME
           COMPUTE WRK-YUKOKETA        =   TBL-ZAIW-YUKOKETA(3 IDZ)
                                       +   IDV
           IF      WRK-YUKOKETA        >   19
                   ADD     1           TO  WRK-ZAIWMAX (3)
                   MOVE    WRK-ZAIWMAX (3) TO  IDZ
                   STRING  "（"               DELIMITED  BY  SIZE
                           WRK-CHAR-N         DELIMITED  BY  SPACE
                           "日）"             DELIMITED  BY  SIZE
                                       INTO   TBL-ZAIW-NAME (3 IDZ)
                   END-STRING
                   MOVE    IDV         TO     TBL-ZAIW-YUKOKETA(3 IDZ)
           ELSE
                   STRING  TBL-ZAIW-NAME (3 IDZ)
                                              DELIMITED  BY  SPACE
                           "（"               DELIMITED  BY  SIZE
                           WRK-CHAR-N         DELIMITED  BY  SPACE
                           "日）"             DELIMITED  BY  SIZE
                                       INTO   WRK-NAME
                   END-STRING
                   MOVE    WRK-NAME    TO  TBL-ZAIW-NAME (3 IDZ)
                   MOVE    WRK-YUKOKETA    TO  TBL-ZAIW-YUKOKETA(3 IDZ)
           END-IF
      *                 
           .
       200511-GJK-DAY-ADD-EXT.
           EXIT.
      *
      *****************************************************************
      *    外来迅速検体検査加算算定日編集処理
      *****************************************************************
       200512-GJK-DAY-ADD-SEC          SECTION.
      *
           INITIALIZE                  WRK-CHAR-HENKAN-AREA
      *
           PERFORM VARYING IDV FROM 1  BY  1
                   UNTIL   IDV >           31
                   IF  RECE84-KAISU (1 IDV)    >   0
                       MOVE    IDV     TO  WRK-Z9
                       MOVE    31      TO  IDV
                   END-IF
           END-PERFORM
      *
           MOVE    1                   TO  WRK-CHAR-FLG
           MOVE    WRK-Z9 (3:2)        TO  WRK-CHAR-X
           MOVE    2                   TO  WRK-CHAR-CNT
           MOVE    IDZ                 TO  IDV
           PERFORM 800-NUM-ZENKAKU-HENKAN-SEC
           MOVE    IDV                 TO  IDZ
      * 
           IF      WRK-Z9              <   10
                   MOVE    4           TO  IDV
           ELSE
                   MOVE    5           TO  IDV
           END-IF
           ADD     1                   TO  WRK-ZAIWMAX (3)
           MOVE    WRK-ZAIWMAX (3)     TO  IDZ
           STRING  "（"                DELIMITED  BY  SIZE
                   WRK-CHAR-N          DELIMITED  BY  SPACE
                   "日）"              DELIMITED  BY  SIZE
                                       INTO   TBL-ZAIW-NAME (3 IDZ)
           END-STRING
           MOVE    IDV                 TO  TBL-ZAIW-YUKOKETA(3 IDZ)
      *                 
           .
       200512-GJK-DAY-ADD-EXT.
           EXIT.
      *
      *****************************************************************
      *    剤　明細テーブルチェック 診療区分（その他） 処理
      *****************************************************************
       20052-SRYKBNELSE-CHECK-SEC      SECTION.
      *
           INITIALIZE                    TBL-ZAI-AREA
           MOVE    ZERO              TO  WRK-TBLMAX
           MOVE    SPACE             TO  WRK-ZAI
           MOVE    "＊"              TO  WRK-ZAI-MK
      *     
           PERFORM VARYING IDX3    FROM    1   BY  1
                   UNTIL   IDX3    >       5
      *
      *        薬剤と器材の表示を逆にする
               EVALUATE    IDX3
                   WHEN    4
                       MOVE    5               TO  IDX1
                   WHEN    5
                       MOVE    4               TO  IDX1
                   WHEN   OTHER
                       MOVE    IDX3            TO  IDX1
               END-EVALUATE
      *  
               IF      RECE84-SRYKBN   <=   "33"
                   MOVE    "＊"            TO  WRK-ZAI-MK
               END-IF    
               IF    ((RECE84-YKZTEN           =   ZERO) AND
                      (RECE84-JIHOKBN          =  SPACE) AND
                      (RECE84-RYOTENKISAISU    =   ZERO) AND
                      (RECE84-YKZKISAIFLG      =  SPACE) AND
                      (IDX1                    =   5   ))
               OR    ((RECE84-KIZAITEN         =   ZERO) AND
                      (RECE84-JIHOKBN          =  SPACE) AND
                      (RECE84-RYOTENKISAISU    =   ZERO) AND
                      (IDX1                    =   4   ))
               OR    ((RECE84-HOKATU-YKZTEN    =   ZERO) AND
                      (RECE84-JIHOKBN      NOT =  SPACE) AND
                      (RECE84-RYOTENKISAISU    =   ZERO) AND
                      (RECE84-YKZKISAIFLG      =  SPACE) AND
                      (IDX1                    =   5   ))
               OR    ((RECE84-HOKATU-KIZAITEN  =   ZERO) AND
                      (RECE84-JIHOKBN      NOT =  SPACE) AND
                      (RECE84-RYOTENKISAISU    =   ZERO) AND
                      (IDX1                    =   4   ))
                   CONTINUE
               ELSE    
                       IF  ( RECE84-SRYKBN        =   "31" OR "32" )
                       OR  ((RECE84-SRYKBN        =   "33"        ) AND
                            (RECE84-JIHOKBN   NOT =   SPACE       ))
                         IF   WRK-TBLMAX      >    ZERO
                             MOVE     SPACE      TO  WRK-ZAI-MK
                         END-IF
                       END-IF
                       PERFORM VARYING IDX2    FROM    1   BY  1
                               UNTIL   IDX2    >   WRK-ZAIWMAX (IDX1)  
                           MOVE    ZERO          TO  FLG-NOPRT
                           MOVE    IDX1          TO  WRK-WIDX1
                           MOVE    IDX2          TO  WRK-WIDX2
                           PERFORM 200521-SEYKBNELSE-MEISAI-SEC
                           IF  FLG-NOPRT         =   ZERO
      *                        レセプト０５剤編集処理
                               PERFORM 430-TBLZAI-HEN-SEC
                           END-IF    
                       END-PERFORM
               END-IF        
               IF      WRK-ZAIWMAX (IDX1)    >   ZERO        
                   MOVE    SPACE             TO  WRK-ZAI
               END-IF    
      *             
      *        点数・回数セット処理（診療行為）
               IF      IDX1              =   3
               AND     WRK-ZAIWMAX(IDX1) >   ZERO
               AND     RECE84-JIHOKBN    =   SPACE
                   IF      RECE84-SRYKBN     >=  "33"                   
      **>          IF      RECE84-SRYKBN     >   "33"                   
                       MOVE    RECE84-SYUTEN1    TO  WRK-ZAITENX
                       MOVE    RECE84-ZAIKAISU   TO  WRK-ZAIKAISUX
                       PERFORM 810-TENSUKAI-HEN-SEC
                       MOVE    WRK-TBLMAX        TO  IDY
                       PERFORM 440-TNSKAI-SET-SEC
                       MOVE    IDY               TO  WRK-TBLMAX 
                   END-IF
               END-IF    
      *        点数・回数セット処理（特定器材）
               IF     ( IDX1                =   4    )
               AND    ( WRK-ZAIWMAX(IDX1)   >   ZERO )
               AND    ( RECE84-JIHOKBN      =   SPACE)
                 IF     RECE84-KIZAITEN     >   ZERO
                   IF      RECE84-SRYKBN     >=  "33"                   
      **>          IF      RECE84-SRYKBN     >   "33"                   
                       MOVE    RECE84-KIZAITEN   TO  WRK-ZAITENX
                       MOVE    RECE84-ZAIKAISU   TO  WRK-ZAIKAISUX
                       PERFORM 810-TENSUKAI-HEN-SEC
                       MOVE    WRK-TBLMAX        TO  IDY
                       PERFORM 440-TNSKAI-SET-SEC
                       MOVE    IDY               TO  WRK-TBLMAX 
                   END-IF
                 END-IF
               END-IF     
      *        点数・回数セット処理（薬剤）
               IF     ( IDX1                =   5    )
               AND    ( WRK-ZAIWMAX(IDX1)   >   ZERO )
               AND    ( RECE84-JIHOKBN      =   SPACE)
                 IF     RECE84-YKZTEN       >   ZERO
                 OR     RECE84-YKZKISAIFLG  =   "1"
      ***          IF      RECE84-SRYKBN     >=  "31"   
      *****************IF      RECE84-SRYKBN     =   "31"    OR  
      **                                             "32"    OR  "33"  
      **               AND     RECE84-YKZTEN     <=  20
      **                   ADD     1                 TO  WRK-TBLMAX
      **                   MOVE    "＊"              TO
      **                               TBL-ZAI (WRK-TBLMAX)(1:2)
      *****************END-IF     
      *                点数・回数セット
                       IF      RECE84-SRYKBN     >=  "33"               
      **>              IF      RECE84-SRYKBN     >   "33"               
                           MOVE    RECE84-YKZTEN     TO  WRK-ZAITENX
                           MOVE    RECE84-ZAIKAISU   TO  WRK-ZAIKAISUX
                           PERFORM 810-TENSUKAI-HEN-SEC
                           MOVE    WRK-TBLMAX        TO  IDY
                           PERFORM 440-TNSKAI-SET-SEC
                           MOVE    IDY               TO  WRK-TBLMAX 
                       END-IF          
      ***          END-IF
                 END-IF
               END-IF        
           END-PERFORM
      *             
      *    検査のとき回数を集計
           IF     (WRK-ZAIWMAX(3)     >   ZERO        )
           AND    (RECE84-SRYKBN      =   "60" OR "64")
           AND    (RECE84-JIHOKBN     =   SPACE       )
               IF      TBL-ZAIW-SRYCD (3 1)   =   WRK-CONS-SAIKETSU1
                                              OR  WRK-CONS-SAIKETSU2
                                              OR  WRK-CONS-SAIKETSU3 
                   CONTINUE
               ELSE
                   IF      WRK-ZAIW-KNSJISKBN  =  "2"
                       CONTINUE
                   ELSE 
                       ADD     RECE84-ZAIKAISU   TO  WRK-KENSA-KAISU
                   END-IF
               END-IF
           END-IF     
      *
           .
       20052-SRYKBNELSE-CHECK-EXT.
           EXIT.
      *
      *****************************************************************
      *    明細印字チェック 診療区分（その他） 処理
      *****************************************************************
       200521-SEYKBNELSE-MEISAI-SEC        SECTION.
      *
           MOVE    ZERO          TO  FLG-NOPRT
      *
      *    服用方法でないこと
           IF  (TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2)(1:3) =   "001")
           OR  (TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2)(1:1) =   "S"  )
               MOVE    1             TO  FLG-NOPRT
           END-IF
      *    診療行為テーブルに存在した場合、印字しないこと
           IF   RECE84-JIHOKBN  =  SPACE
             PERFORM   VARYING   IDY   FROM    1   BY  1
                       UNTIL   ( IDY           >   TBL-NOSRYCD-MAX )
                       OR      ( FLG-NOPRT     =    1 )
      *
               IF  TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2)
                                =   TBL-NOSRYCD (IDY)
                   MOVE    1                 TO  FLG-NOPRT
               END-IF
             END-PERFORM
           END-IF
      *
           .
       200521-SEYKBNELSE-MEISAI-EXT.
           EXIT.
      *
      *****************************************************************
      *    検査チェック処理
      *****************************************************************
       2006-SRYKBN60-CHECK-SEC         SECTION.
      *
           PERFORM 2006-GAIJINKEN-BUNRI-SEC
           MOVE    RECE84-REC          TO   RECE84SAV-REC
      *
      *    薬剤やコメントがあればまとめない
           IF      FLG-SRYCD1          =   1
               PERFORM 2006-GAIJINKEN-HENSYU-SEC
               MOVE    RECE84SAV-REC       TO  RECE84-REC
               IF      RECE84-ZAIKAISU     >   0
                       MOVE    ZERO        TO  FLG-GAIJINKEN
                       COMPUTE RECE84-ZAINUM   =   RECE84-ZAINUM
                                               +   99000000
                       PERFORM 2005-SRYKBNELSE-HENSYU-SEC
               END-IF
           ELSE
      *        剤の明細数が３以上の場合と明細数が２で
      *        先頭が検体名コードでない場合はまとめない
               IF    ( RECE84-MEISAISU     >   2 )
                 OR (( RECE84-MEISAISU     =   2 )
                 AND ( TBL-ZAIW-KENTAICOMMENT-SRYCD (3 1) = 0 ))
                   PERFORM 2006-GAIJINKEN-HENSYU-SEC
                   MOVE    RECE84SAV-REC       TO  RECE84-REC
                   IF      RECE84-ZAIKAISU     >   0
                           MOVE    ZERO        TO  FLG-GAIJINKEN
                           COMPUTE RECE84-ZAINUM   =   RECE84-ZAINUM
                                                   +   99000000
                           PERFORM 2005-SRYKBNELSE-HENSYU-SEC
                   END-IF
               ELSE    
      *        包括対象でないときまとめない         
                   IF      TBL-ZAIW-HOUKNSKBN(3 1)  =   ZERO
                       PERFORM 2006-GAIJINKEN-HENSYU-SEC
                       MOVE    RECE84SAV-REC       TO  RECE84-REC
                       IF      RECE84-ZAIKAISU     >   0
                               MOVE    ZERO        TO  FLG-GAIJINKEN
                               COMPUTE RECE84-ZAINUM   =   RECE84-ZAINUM
                                                       +   99000000
                               PERFORM 2005-SRYKBNELSE-HENSYU-SEC
                       END-IF
                   ELSE
                       IF      WRK-RECE84GJK-MAX   >   0
                               PERFORM VARYING IDXX FROM  1  BY  1 
                                       UNTIL   IDXX >  WRK-RECE84GJK-MAX 
                                 MOVE  1           TO  FLG-GAIJINKEN
                                 MOVE  WRK-RECE84GJK-REC (IDXX) 
                                                   TO  RECE84-REC 
                                 PERFORM 2006-SRYKBN60-TAIHI-SEC
                               END-PERFORM
                       END-IF
                       MOVE    RECE84SAV-REC       TO  RECE84-REC
                       IF      RECE84-ZAIKAISU     >   0
                               MOVE    ZERO        TO  FLG-GAIJINKEN
                               PERFORM 2006-SRYKBN60-TAIHI-SEC
                       END-IF
                   END-IF
               END-IF
           END-IF
           .
       2006-SRYKBN60-CHECK-EXT.
           EXIT.
      *
      *****************************************************************
      *    外来迅速検体検査加算剤分離処理
      *****************************************************************
       2006-GAIJINKEN-BUNRI-SEC        SECTION.
      *
           INITIALIZE                  WRK-RECE84GJK-TABLE
           MOVE    ZERO                TO  WRK-RECE84GJK-MAX
      *
           IF      ( RECE82-KENTAIKASANFLG   =   '1' )
             AND   ( WRK-ZAIW-KNSJISKBN  NOT =   2 )
             AND  (((RECE84-KNSJISGRPKBN     >   0)
             AND    (RECE84-KNSJISGRPKBN     <   7))
             OR     (RECE84-SRYCDTOTAL       =   WRK-CONS-NYOIPPAN-N))
             MOVE    RECE84-REC        TO  RECE84GJK-REC
             PERFORM VARYING IDX FROM  1   BY  1
                     UNTIL   IDX >     31
                 IF  RECE84-KAISU (1 IDX)    >   0
                   IF  WRK-KENTAIKASAN (IDX) =   1
                       MOVE    ZERO    TO  RECE84GJK-ZAIKAISU
                                           RECE84GJK-DAY-AREA
                       MOVE    RECE84-KAISU (1 IDX)
                                       TO  RECE84GJK-ZAIKAISU
                       MOVE    RECE84-KAISU (1 IDX)
                                       TO  RECE84GJK-KAISU (1 IDX)
                       MOVE    RECE84-KAISU (2 IDX)
                                       TO  RECE84GJK-KAISU (2 IDX) 
                       MOVE    RECE84-KAISU (3 IDX)
                                       TO  RECE84GJK-KAISU (3 IDX) 
                       MOVE    RECE84-KAISU (4 IDX)
                                       TO  RECE84GJK-KAISU (4 IDX) 
                       COMPUTE WRK-RECE84GJK-MAX =  WRK-RECE84GJK-MAX
                                                 +  1
                       MOVE    RECE84GJK-REC
                                       TO  WRK-RECE84GJK-REC 
                                               (WRK-RECE84GJK-MAX)
                       COMPUTE RECE84-ZAIKAISU   =  RECE84-ZAIKAISU
                                       -   RECE84-KAISU (1 IDX)
                       MOVE    ZERO    TO  RECE84-KAISU (1 IDX)
                                           RECE84-KAISU (2 IDX) 
                                           RECE84-KAISU (3 IDX) 
                                           RECE84-KAISU (4 IDX) 
                   END-IF
                 END-IF
             END-PERFORM
           END-IF        
           .
       2006-GAIJINKEN-BUNRI-EXT.
           EXIT.
      *
      *****************************************************************
      *    外来迅速検体検査加算対象剤処理
      *****************************************************************
       2006-GAIJINKEN-HENSYU-SEC       SECTION.
      *
           IF     WRK-RECE84GJK-MAX    >   0
                  PERFORM VARYING IDXX FROM  1  BY  1 
                          UNTIL   IDXX >     WRK-RECE84GJK-MAX 
                      MOVE  WRK-RECE84GJK-REC (IDXX)
                                       TO  RECE84-REC
                      COMPUTE WRK-GAIJINKEN-ZAINUM  =
                              WRK-GAIJINKEN-ZAINUM  +  1
                      COMPUTE RECE84-ZAINUM = RECE84-ZAINUM
                                            + WRK-GAIJINKEN-ZAINUM
                                            * 100000
                      MOVE    1        TO  FLG-GAIJINKEN
                      MOVE    TBL-ZAIW-OCC (3)
                                       TO  TBL-ZAIW-OCC (6)
                      MOVE    WRK-ZAIWMAX-G
                                       TO  WRK-ZAIWMAX-SAV-G
                      PERFORM 2005-SRYKBNELSE-HENSYU-SEC
                      MOVE    TBL-ZAIW-OCC (6)
                                       TO  TBL-ZAIW-OCC (3)
                      MOVE    WRK-ZAIWMAX-SAV-G
                                       TO  WRK-ZAIWMAX-G
                  END-PERFORM
           END-IF
           .
       2006-GAIJINKEN-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    検査まとめ対象待避処理
      *****************************************************************
       2006-SRYKBN60-TAIHI-SEC         SECTION.
      *
           ADD     1               TO  WRK-RECE84-MAX
           MOVE    RECE84-REC      TO  WRK-RECE84-REC (WRK-RECE84-MAX)
           MOVE    RECE84-DAY-AREA TO  WRK-RECE84-DAY-AREA
                                                      (WRK-RECE84-MAX)
           MOVE    TBL-ZAIW-HOUKNSKBN(3 1) TO  WRK-RECE84-HOUKNSKBN
                                                      (WRK-RECE84-MAX)
           IF      FLG-GAIJINKEN   =   0                    
                   MOVE ZERO       TO  WRK-RECE84-GAIJINKEN
                                                      (WRK-RECE84-MAX)
           ELSE
                   PERFORM VARYING IDX FROM    1   BY  1
                           UNTIL   IDX >       31
                       IF  RECE84-KAISU (1 IDX)    >   0
                           MOVE IDX TO  WRK-RECE84-GAIJINKEN
                                                      (WRK-RECE84-MAX)
                           MOVE 31  TO  IDX
                       END-IF
                   END-PERFORM
           END-IF
           MOVE    TBL-ZAIW-SRYSYUKBN-INPUT(3 1)
                                   TO  WRK-RECE84-SRYSYUKBN
                                                      (WRK-RECE84-MAX)
           MOVE    TBL-ZAIW-SRYCD(3 1)
                                   TO  WRK-RECE84-SRYCD
                                                      (WRK-RECE84-MAX)
           MOVE    TBL-ZAIW-SRYSURYO(3 1)
                                   TO  WRK-RECE84-SRYSURYO
                                                      (WRK-RECE84-MAX)
           MOVE    TBL-ZAIW-KENTAICOMMENT-SRYCD (3 1)
                                   TO  WRK-RECE84-KENTAICOMMENT-SRYCD
                                                      (WRK-RECE84-MAX)
           PERFORM VARYING IDX FROM    1   BY  1
                   UNTIL   IDX >       31
               ADD      RECE84-KAISU (1 IDX)   TO  WRK-SUM-KAISU (IDX)
               IF      (RECE84-KAISU (1 IDX)   >   0)
                 IF    (WRK-MIN-KAISU (IDX)    =   0)
                 OR    (RECE84-KAISU (1 IDX)   <   WRK-MIN-KAISU (IDX))
                   MOVE RECE84-KAISU (1 IDX)   TO  WRK-MIN-KAISU (IDX)
                   MOVE TBL-ZAIW-HOUKNSKBN(3 1)
                                            TO  WRK-MIN-HOUKNSKBN (IDX)
                   MOVE WRK-RECE84-GAIJINKEN (WRK-RECE84-MAX)
                                            TO  WRK-MIN-GAIJINKEN (IDX)
                   MOVE TBL-ZAIW-SRYSYUKBN-INPUT(3 1)
                                            TO  WRK-MIN-SRYSYUKBN (IDX)
                 END-IF
               END-IF
           END-PERFORM
           .
       2006-SRYKBN60-TAIHI-EXT.
           EXIT.
      *
      *****************************************************************
      *    包括対象検査まとめ処理
      *****************************************************************
       2007-SRYKBN60-HENSYU-SEC         SECTION.
      *
           INITIALIZE                   WRK-RECE84-MATOME-TABLE
      *
           PERFORM     VARYING  IDXX     FROM  1  BY  1
                       UNTIL    IDXX     >    31
      *
             PERFORM            UNTIL   WRK-SUM-KAISU (IDXX)  =  ZERO
               INITIALIZE       WRK-KENSA-MATOME (101)
                                WRK-RECE84-FLG-TABLE
               PERFORM  VARYING IDY FROM    1   BY  1
                        UNTIL   IDY >       WRK-RECE84-MAX
                 IF    ( WRK-RECE84-KAISU (IDY 1 IDXX)
                                       >=   WRK-MIN-KAISU (IDXX) )
                 AND   ( WRK-RECE84-HOUKNSKBN (IDY)
                                        =   WRK-MIN-HOUKNSKBN (IDXX) )
                 AND   ( WRK-RECE84-GAIJINKEN (IDY)
                                        =   WRK-MIN-GAIJINKEN (IDXX) )
                 AND   ( WRK-RECE84-SRYSYUKBN (IDY)
                                        =   WRK-MIN-SRYSYUKBN (IDXX) )
                 AND   ( WRK-RECE84-FLG (IDY)      =   ZERO )
                    MOVE  WRK-RECE84-HOUKNSKBN (IDY)
                                              TO  WRK-HOUKNSKBN (101)
                    MOVE  WRK-RECE84-GAIJINKEN (IDY)
                                              TO  WRK-GAIJINKEN (101)
                    MOVE  WRK-RECE84-SRYSYUKBN (IDY)
                                              TO  WRK-SRYSYUKBN-K (101)
                    MOVE  WRK-MIN-KAISU (IDXX)
                                              TO  WRK-KAISU (101 1)
                    MOVE  1                   TO  WRK-RECE84-FLG (IDY)
                    ADD   1                   TO  WRK-KENSA-SU (101)
                    MOVE  WRK-KENSA-SU (101)  TO  IDX1
                    MOVE  IDY             TO  WRK-RECE84-IDX (101 IDX1)
                    PERFORM  VARYING  IDX2  FROM 1 BY 1
                             UNTIL    IDX2  >    IDX1
                      IF   WRK-KENSA-SRYCD(101 IDX2)  =  0
                           MOVE  WRK-RECE84-SRYCD-G (IDY)
                                       TO  WRK-KENSA-SRYCD-OCC(101 IDX2)
                      ELSE
                        IF   WRK-RECE84-SRYCD-G (IDY)
                                       <   WRK-KENSA-SRYCD-OCC(101 IDX2)
                             PERFORM  VARYING  IDX3  FROM  IDX1  BY -1
                                      UNTIL    IDX3  =     IDX2
                               COMPUTE  IDX4   =     IDX3  -  1
                               MOVE   WRK-KENSA-SRYCD-OCC(101 IDX4)
                                       TO  WRK-KENSA-SRYCD-OCC(101 IDX3)
                             END-PERFORM
                             MOVE  WRK-RECE84-SRYCD-G (IDY)
                                       TO  WRK-KENSA-SRYCD-OCC(101 IDX2)
                             MOVE  IDX1   TO  IDX2
                        END-IF
                      END-IF
                    END-PERFORM
                 END-IF
               END-PERFORM
      *
      *        同一検査内容のグループがテーブルに存在するか
               MOVE    ZERO                   TO  IDX1
               PERFORM  VARYING IDY FROM    1   BY  1
                        UNTIL   IDY >       WRK-KENSA-MATOME-MAX
                 IF    ( WRK-HOUKNSKBN (IDY)  =  WRK-HOUKNSKBN (101) )
                 AND   ( WRK-GAIJINKEN (IDY)  =  WRK-GAIJINKEN (101) )
                 AND   ( WRK-KENSA-SU (IDY)   =  WRK-KENSA-SU (101) )
                   IF  ( WRK-RECE84-IDX-G (IDY)
                                           =  WRK-RECE84-IDX-G (101) )
                   OR  ( WRK-KENSA-SRYCD-G (IDY)
                                           =  WRK-KENSA-SRYCD-G (101) )
                    MOVE  IDY                 TO  IDX1
                    MOVE  WRK-KENSA-MATOME-MAX
                                              TO  IDY
                   END-IF
                 END-IF
               END-PERFORM
      *
               IF      IDX1                   =   0
                       ADD  1                 TO  WRK-KENSA-MATOME-MAX
                       MOVE WRK-KENSA-MATOME (101)
                            TO  WRK-KENSA-MATOME (WRK-KENSA-MATOME-MAX)
               ELSE
                       ADD  WRK-MIN-KAISU (IDXX)
                                              TO  WRK-KAISU (IDX1 1)
               END-IF
      *
      *        回数テーブルより差し引く
               PERFORM  VARYING IDY FROM    1   BY  1
                        UNTIL   IDY >       WRK-RECE84-MAX
                 IF    ( WRK-RECE84-FLG (IDY)      =   1 )
                    COMPUTE WRK-SUM-KAISU (IDXX)   =
                            WRK-SUM-KAISU (IDXX)   -
                            WRK-MIN-KAISU (IDXX)
                    COMPUTE WRK-RECE84-KAISU (IDY 1 IDXX)   =
                            WRK-RECE84-KAISU (IDY 1 IDXX)   -
                            WRK-MIN-KAISU (IDXX)
                 END-IF
               END-PERFORM
      *
      *        新たに最小回数情報をセットする
               MOVE     ZERO             TO  WRK-MIN-KAISU (IDXX)
               MOVE     ZERO             TO  WRK-MIN-HOUKNSKBN (IDXX)
               MOVE     ZERO             TO  WRK-MIN-GAIJINKEN (IDXX)
               MOVE     SPACE            TO  WRK-MIN-SRYSYUKBN (IDXX)
               PERFORM  VARYING IDY FROM    1   BY  1
                        UNTIL   IDY >       WRK-RECE84-MAX
                 IF    (WRK-RECE84-KAISU (IDY 1 IDXX)  >   0)
                   IF  (WRK-MIN-KAISU (IDXX)   =   0)
                   OR  (WRK-RECE84-KAISU (IDY 1 IDXX)
                                             <   WRK-MIN-KAISU (IDXX))
                     MOVE WRK-RECE84-KAISU (IDY 1 IDXX)
                                           TO  WRK-MIN-KAISU (IDXX)
                     MOVE WRK-RECE84-HOUKNSKBN (IDY)
                                           TO  WRK-MIN-HOUKNSKBN (IDXX)
                     MOVE WRK-RECE84-GAIJINKEN (IDY)
                                           TO  WRK-MIN-GAIJINKEN (IDXX)
                     MOVE WRK-RECE84-SRYSYUKBN (IDY)
                                           TO  WRK-MIN-SRYSYUKBN (IDXX)
                   END-IF
                 END-IF
               END-PERFORM
             END-PERFORM
           END-PERFORM
      *
      *    まとめた剤を出力する
           PERFORM      VARYING IDXX FROM   1   BY  1
                        UNTIL   IDXX >      WRK-KENSA-MATOME-MAX
             MOVE    ZERO                TO  FLG-SRYCD1
      *
             INITIALIZE                      TBL-ZAIW-AREA
             MOVE    ZERO                TO  WRK-ZAIWMAX-G
             MOVE    ZERO                TO  WRK-TBLMAX
             MOVE    ZERO                TO  FLG-TEIGEN
                                             FLG-COMMENT
                                             FLG-COMMENT2
                                             FLG-COMMENT3
                                             FLG-COMMENT4
                                             FLG-HIKAKIN
                                             FLG-GAIJINKEN
                                             FLG-TANZAING
                                             FLG-SYOSAI-CHK
             MOVE    ZERO                TO  WRK-RECE85
                                             IDY
             MOVE    SPACE               TO  WRK-SRYSYUKBN
      *
             MOVE    SPACE               TO  RECE84-REC
             INITIALIZE                      RECE84-REC
             MOVE    ZERO                TO  WRK-RECE84-ZAITEN
             PERFORM VARYING IDW FROM    1   BY  1
                     UNTIL   IDW >       WRK-KENSA-SU (IDXX)
               MOVE  WRK-RECE84-IDX (IDXX IDW) TO  IDX1
               MOVE  WRK-RECE84-REC (IDX1)     TO  RECE84-REC
               MOVE  WRK-RECE84-DAY-AREA(IDX1) TO  RECE84-DAY-AREA
               ADD   RECE84-ZAITEN             TO  WRK-RECE84-ZAITEN
               PERFORM 2002-TBLZAIW-TAIHI-SEC
             END-PERFORM
             MOVE    WRK-KAISU (IDXX 1)  TO  RECE84-ZAIKAISU
             MOVE    WRK-RECE84-ZAITEN   TO  RECE84-SYUTEN1
                                             RECE84-ZAITEN
             IF      WRK-GAIJINKEN (IDXX)  >  0
                     COMPUTE WRK-GAIJINKEN-ZAINUM  =
                             WRK-GAIJINKEN-ZAINUM  +  1
                     COMPUTE RECE84-ZAINUM = RECE84-ZAINUM
                                           + WRK-GAIJINKEN-ZAINUM
                                           * 100000
                     MOVE    1           TO  FLG-GAIJINKEN
             ELSE
                     COMPUTE RECE84-ZAINUM = RECE84-ZAINUM
                                           + 99000000
                     MOVE    0           TO  FLG-GAIJINKEN
             END-IF
      *
      *     レセプト０４剤編集前処理
            PERFORM 2001-RECE84-ZAIHEN-MAE-SEC
      *
            IF      SPA-STS          =   ZERO
                    PERFORM 20051-SRYKBNELSE-KENSA-SEC
      *             剤・診療行為テーブル出力処理
                    PERFORM 450-RECE86-OUT-SEC
            END-IF
           END-PERFORM
           .
       2007-SRYKBN60-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    外来迅速検体検査加算の算定日確認処理
      *****************************************************************
       2009-KENTAIKASAN-KAKUNIN-SEC      SECTION.
      *
           INITIALIZE                      RECE84-REC
           MOVE    RECE82-HOSPNUM      TO  RECE84-HOSPNUM
           MOVE    RECE82-SRYYM        TO  RECE84-SRYYM
           MOVE    RECE82-NYUGAIKBN    TO  RECE84-NYUGAIKBN
           MOVE    RECE82-PTID         TO  RECE84-PTID
           MOVE    RECE82-RECEKA       TO  RECE84-RECEKA
           MOVE    RECE82-HKNID        TO  RECE84-HKNID
           MOVE    RECE82-TEKSTYMD     TO  RECE84-TEKSTYMD
           MOVE    ZERO                TO  RECE84-ZAINUM
      *
           START   RECE84-FILE
               KEY IS    >=   RECE84-KEY
               INVALID
                   MOVE    1           TO  FLG-RECE84
               NOT INVALID
                   PERFORM 990-RECE84-NEXT-SEC
           END-START
           PERFORM
               UNTIL  ( SPA-STS              =   2                   )
               OR     ( FLG-RECE84           =   1                   )
               OR     ( RECE82-HOSPNUM   NOT =   RECE84-HOSPNUM      )
               OR     ( RECE82-SRYYM     NOT =   RECE84-SRYYM        )
               OR     ( RECE82-NYUGAIKBN NOT =   RECE84-NYUGAIKBN    )
               OR     ( RECE82-PTID      NOT =   RECE84-PTID         )
               OR     ( RECE82-RECEKA    NOT =   RECE84-RECEKA       )
               OR     ( RECE82-HKNID     NOT =   RECE84-HKNID        )
               OR     ( RECE82-TEKSTYMD  NOT =   RECE84-TEKSTYMD     )
      *
               IF     RECE84-KNSJISGRPKBN    =   99
                   PERFORM VARYING IDX FROM      1  BY  1
                           UNTIL   IDX >         31
                       IF  RECE84-KAISU (1 IDX)  >    0
                           MOVE    1         TO  WRK-KENTAIKASAN (IDX)
                       END-IF
                   END-PERFORM
               END-IF
      *
               PERFORM 990-RECE84-NEXT-SEC
      *
           END-PERFORM
      *
           MOVE    ZERO                TO  FLG-RECE84
           .
      *
       2009-KENTAIKASAN-KAKUNIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    初診の剤まとめ編集
      *****************************************************************
       2010-SYOSAIMATOME-HENSYU1-SEC      SECTION.
      *
           MOVE    ZERO      TO    FLG-TEIGEN
           IF    TBL-SORT11-TENSIKIBETU(IDX6)  =  8
             MOVE   1        TO    FLG-TEIGEN
           END-IF
      *
           IF  TBL-SORT11-ZAIKAISU(IDX6)  NOT =  ZERO
      *
               EVALUATE    TRUE
                 WHEN    TBL-SORT11-YUKOKETA(IDX6) <=  16
                   MOVE    SPACE             TO  WRK-ZAI
                   MOVE    TBL-SORT11-TEN(IDX6)  TO  WRK-ZAITENX
                   MOVE    TBL-SORT11-ZAIKAISU(IDX6)
                                                 TO  WRK-ZAIKAISUX
                   PERFORM 810-TENSUKAI-HEN-SEC
                   MOVE    WRK-TNSKAI        TO  WRK-ZAI-TNSKAI
      *
                   MOVE    "＊"              TO  WRK-ZAI-MK
                   MOVE    TBL-SORT11-YUKOKETA(IDX6)
                                             TO  IDX8
                   COMPUTE IDX8 = IDX8 * 2
                   MOVE    TBL-SORT11-NAME(IDX6)(1:IDX8)
                                             TO  WRK-ZAI-NAME(1:IDX8)
      *
                   MOVE    1                 TO  WRK-SET-LINENUM
                   PERFORM 20101-SYOSAIMATOME-WRITE1-SEC
                 WHEN    TBL-SORT11-YUKOKETA(IDX6) <=  41
                   MOVE    SPACE             TO  WRK-ZAI
                   MOVE    "＊"              TO  WRK-ZAI-MK
                   MOVE    TBL-SORT11-YUKOKETA(IDX6)
                                             TO  IDX8
                   COMPUTE IDX8 = IDX8 * 2
                   IF   IDX8  <=  50
                     MOVE  TBL-SORT11-NAME(IDX6)(1:IDX8)
                                             TO  WRK-ZAI-NAME(1:IDX8)
                   ELSE
                     MOVE  TBL-SORT11-NAME(IDX6)(1:50)
                                             TO  WRK-ZAI-NAME(1:50)
                   END-IF
      *
                   MOVE    1                 TO  WRK-SET-LINENUM
                   PERFORM 20101-SYOSAIMATOME-WRITE1-SEC
      *@
                   MOVE    SPACE             TO  WRK-ZAI
                   MOVE    TBL-SORT11-TEN(IDX6)  TO  WRK-ZAITENX
                   MOVE    TBL-SORT11-ZAIKAISU(IDX6)
                                                 TO  WRK-ZAIKAISUX
                   PERFORM 810-TENSUKAI-HEN-SEC
                   MOVE    WRK-TNSKAI        TO  WRK-ZAI-TNSKAI
                   IF   IDX8  >  50
                     COMPUTE IDX9 = IDX8 - 50
                     MOVE    TBL-SORT11-NAME(IDX6)(51:IDX9)
                                             TO  WRK-ZAI-NAME(1:IDX9)
                   END-IF
      *
                   MOVE    2                 TO  WRK-SET-LINENUM
                   PERFORM 20101-SYOSAIMATOME-WRITE1-SEC
               END-EVALUATE
      *
           END-IF
      *
           .
      *
       2010-SYOSAIMATOME-HENSYU1-EXT.
           EXIT.
      *
      *****************************************************************
      *    初診の剤まとめ出力
      *****************************************************************
       20101-SYOSAIMATOME-WRITE1-SEC      SECTION.
      *
           MOVE    SPACE             TO  RECE86-REC
           INITIALIZE                    RECE86-REC
           MOVE    RECE82-KEY        TO  RECE86-KEY
      *
           MOVE    WRK-SET-LINENUM   TO  RECE86-LINENUM
      *
           COMPUTE IDX9  =  (IDX6 - 1) * 10
           COMPUTE RECE86-ZAINUM       =   99992000 + IDX9 + 1
      *
           MOVE    1                 TO  RECE86-ZAIMEISAISU
           MOVE    RECE82-PTNUM      TO  RECE86-PTNUM
           MOVE    SPACE             TO  RECE86-SRYKA
           MOVE    WRK-ZAI           TO  RECE86-TEKIYO-INF
      *
           MOVE    "11"              TO  RECE86-SRYKBN
      *
      ***  MOVE    99999999          TO  RECE86-ZAITEN
           MOVE    TBL-SORT11-TEN(IDX6)
                                     TO  RECE86-ZAITEN
           MOVE    ZERO              TO  RECE86-KNSJISKBN
      *
           MOVE    TBL-SORT11-CDKBN-SYO(IDX6)
                                     TO  RECE86-CDKBN-SYO
           MOVE    TBL-SORT11-CDKBN-BU(IDX6)
                                     TO  RECE86-CDKBN-BU
           MOVE    TBL-SORT11-CDKBN-KBN(IDX6)
                                     TO  RECE86-CDKBN-KBN
           MOVE    TBL-SORT11-CDKBN-KBNNUM(IDX6)
                                     TO  RECE86-CDKBN-KBNNUM
           MOVE    TBL-SORT11-CDKBN-KBNNUM-EDA(IDX6)
                                     TO  RECE86-CDKBN-KBNNUM-EDA
           MOVE    TBL-SORT11-CDKBN-KOUBAN(IDX6)
                                     TO  RECE86-CDKBN-KOUBAN
           MOVE    TBL-SORT11-KOUHYOJYUNNUM(IDX6)
                                     TO  RECE86-KOUHYOJUNNUM
      *
           MOVE    SPACE             TO  RECE86-SAFLG
           MOVE    ZERO              TO  RECE86-ZAIKBN
           MOVE    SPACE             TO  RECE86-SRYSYUKBN
           MOVE    ZERO              TO  RECE86-DAY
      *
           MOVE    SPACE             TO  RECE86-JIHOKBN
      *
           MOVE    SPACE             TO  RECE86-ZAISKBKBN
           MOVE    ZERO              TO  RECE86-ZAISKBBAN
           MOVE    ZERO              TO  RECE86-TENTEKIKBN
           MOVE    ZERO              TO  RECE86-TENTEKIBAN
      *
           MOVE    ZERO              TO  RECE86-TEKIYO2-KISAIFLG
           MOVE    ZERO              TO  RECE86-TEKIYO2-YENFLG
           MOVE    ZERO              TO  RECE86-TEKIYO2-TEN
           MOVE    ZERO              TO  RECE86-TEKIYO2-KAISU
      *
           MOVE    SPACE             TO  RECE86-OKIKAEFLG
           MOVE    ZERO              TO  RECE86-DAY2
           MOVE    ZERO              TO  RECE86-PRIORITYKBN
      *
           WRITE   RECE86-REC
      *
           IF  STS-RECE86        NOT =   ZERO
               DISPLAY "*** ORCR0910 RECE86 WRITE ERR  ***"
               DISPLAY "*** RECE86-SRYKBN ***" RECE86-SRYKBN
               DISPLAY "*** RECE86-ZAINUM ***" RECE86-ZAINUM
               DISPLAY "<SYOSAIMATOME-WRITE1>"
               MOVE    2             TO  SPA-STS
           ELSE
               ADD     1             TO  CNT-RECE86
           END-IF
      *
           .
      *
       20101-SYOSAIMATOME-WRITE1-EXT.
           EXIT.
      *
      *****************************************************************
      *    点数再計算処理
      *****************************************************************
       400-TENSU-SAIKEISAN-SEC    SECTION.
      *
           MOVE    SPACE               TO  WRK-ZAI
           MOVE    "＊"                TO  WRK-ZAI-MK
      *    点数・回数セット
           MOVE    WRK-TENSUGOKEI      TO  WRK-ZAITENX
           MOVE    RECE84-ZAIKAISU     TO  WRK-ZAIKAISUX
      *    必要有効桁数・開始位置・回数編集
           PERFORM 810-TENSUKAI-HEN-SEC
      *
           .
       400-TENSU-SAIKEISAN-EXT.
           EXIT.
      *
      *****************************************************************
      *    剤テーブル編集処理
      *****************************************************************
       430-TBLZAI-HEN-SEC     SECTION.
      *
      *    数量編集
           PERFORM 4301-SURYO-HEN1-SEC
      *
      *    分画数編集
           PERFORM 4301-BUNKAKU-HEN1-SEC
      *
      *    明細編集・明細桁数計算
           PERFORM 4302-MEISAI-KETA-HEN1-SEC
      *
      *    剤・診療行為テーブル編集処理
           PERFORM 4303-ZAITBL-TENSO-SEC
      *    
      *    明細が１行に収まりきらない場合
           IF  WRK-WCLM            >   50
               MOVE    SPACE             TO  WRK-ZAI-NAME
               MOVE    WRK-KOUI (51:)    TO  WRK-ZAI-NAME
               PERFORM 4303-ZAITBL-TENSO-SEC
           END-IF
           IF  WRK-WCLM            >   100
               MOVE    SPACE             TO  WRK-ZAI-NAME
               MOVE    WRK-KOUI (101:)   TO  WRK-ZAI-NAME
               PERFORM 4303-ZAITBL-TENSO-SEC
           END-IF
           IF  WRK-WCLM            >   150
               MOVE    SPACE             TO  WRK-ZAI-NAME
               MOVE    WRK-KOUI (151:)   TO  WRK-ZAI-NAME
               PERFORM 4303-ZAITBL-TENSO-SEC
           END-IF
           IF  WRK-WCLM            >   200
               MOVE    SPACE             TO  WRK-ZAI-NAME
               MOVE    WRK-KOUI (201:)   TO  WRK-ZAI-NAME
               PERFORM 4303-ZAITBL-TENSO-SEC
           END-IF
           IF  WRK-WCLM            >   250
               MOVE    SPACE             TO  WRK-ZAI-NAME
               MOVE    WRK-KOUI (251:)   TO  WRK-ZAI-NAME
               PERFORM 4303-ZAITBL-TENSO-SEC
           END-IF
      *
           .
       430-TBLZAI-HEN-EXT.
           EXIT.
      *     
      *****************************************************************
      *    数量編集処理
      *****************************************************************
       4301-SURYO-HEN1-SEC        SECTION.
      *
           MOVE    SPACE           TO  WRK-SURYO-X
           MOVE    ZERO            TO  WRK-SURYOST
           MOVE    ZERO            TO  WRK-SURYOKETA
           MOVE    ZERO            TO  WRK-SURYOKETA1
           MOVE    ZERO            TO  WRK-SURYOKETA2
           MOVE    SPACE           TO  WRK-SURYO-N
           MOVE    SPACE           TO  WRK-SRYSURYOW
      *
           IF      TBL-ZAIW-SRYSURYO (WRK-WIDX1 WRK-WIDX2)
                                   =   ZERO
             IF  (RECE84-JIHOKBN  NOT =  SPACE)
             AND (RECE84-ZAIKBN       =   4   )
               CONTINUE
             ELSE
               GO  TO  4301-SURYO-HEN1-EXT  
             END-IF
           END-IF
      * 
      *    数字表示用編集（半角）
           MOVE    TBL-ZAIW-SRYSURYO (WRK-WIDX1 WRK-WIDX2)
                                   TO  WRK-SURYO
           MOVE    WRK-SURYO-S1    TO  WRK-SURYO-Z1
      *
      *    「０５９」で始まる場合は小数点以下３桁までとする
           IF      TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2) (1:3)
                                   =   "059"
               MOVE    "00"            TO  WRK-SURYO-S2 (4:2)
           END-IF
      *
           MOVE    ZERO            TO  FLG-SP
           PERFORM   VARYING   IDY   FROM    5   BY  -1
                     UNTIL     IDY           =   ZERO
             IF  WRK-SURYO-S2(IDY:1) NOT =   ZERO
                 MOVE    1               TO  FLG-SP
             END-IF
             IF  FLG-SP                  =   1
                 MOVE    WRK-SURYO-S2(IDY:1)
                                         TO  WRK-SURYO-Z3(IDY:1)
             END-IF
           END-PERFORM
           IF  WRK-SURYO-Z3          NOT =   SPACE
               MOVE    "."               TO  WRK-SURYO-Z2
           END-IF
      *    表示文字数カウント
           PERFORM   VARYING   IDY   FROM    1   BY  1
                     UNTIL     IDY           >   11
             IF  WRK-SURYO-X (IDY:1)   NOT =   SPACE
                 IF  WRK-SURYOST           =   ZERO
                     MOVE    IDY           TO  WRK-SURYOST
                 END-IF
      *          数量桁数カウント(６桁目：ピリオド)
                 ADD     1                 TO  WRK-SURYOKETA
                 IF  IDY                   <   6
                     ADD     1             TO  WRK-SURYOKETA1
                 END-IF
                 IF  IDY                   >   6
                     ADD     1             TO  WRK-SURYOKETA2
                 END-IF
             END-IF
           END-PERFORM
           IF  WRK-SURYOST             =   6
               MOVE    "0"             TO  WRK-SURYO-Z1(5:1)
               MOVE    5               TO  WRK-SURYOST
               MOVE    1               TO  WRK-SURYOKETA1
               ADD     1               TO  WRK-SURYOKETA
           END-IF
      *    全角変換
           IF  WRK-SURYOKETA1          >   ZERO
               INITIALIZE              WRK-CHAR-HENKAN-AREA
               MOVE    WRK-SURYO-Z1 (WRK-SURYOST:WRK-SURYOKETA1)
                                       TO  WRK-CHAR-X
               MOVE    WRK-SURYOKETA1  TO  WRK-CHAR-CNT
               PERFORM 800-NUM-ZENKAKU-HENKAN-SEC
               MOVE    WRK-CHAR-N      TO  WRK-SURYO-N1
           END-IF
           IF  WRK-SURYOKETA2          >   ZERO
               MOVE    "．"            TO  WRK-SURYO-N2
               INITIALIZE              WRK-CHAR-HENKAN-AREA
               MOVE    WRK-SURYO-Z3 (1:WRK-SURYOKETA2)
                                       TO  WRK-CHAR-X
               MOVE    WRK-SURYOKETA2  TO  WRK-CHAR-CNT
               PERFORM 800-NUM-ZENKAKU-HENKAN-SEC
               MOVE    WRK-CHAR-N      TO  WRK-SURYO-N3
           END-IF
           STRING      WRK-SURYO-N1    DELIMITED   BY  " "
                       WRK-SURYO-N2    DELIMITED   BY  " "
                       WRK-SURYO-N3    DELIMITED   BY  " "
                       INTO            WRK-SRYSURYOW
           END-STRING
      *
           .
       4301-SURYO-HEN1-EXT.
           EXIT.
      *     
      *****************************************************************
      *    分画数編集処理
      *****************************************************************
       4301-BUNKAKU-HEN1-SEC        SECTION.
      *
           MOVE    ZERO            TO  WRK-BUNKAKUKETA
           MOVE    SPACE           TO  WRK-BUNKAKU-N
      *
           IF      TBL-ZAIW-SRYCD (WRK-WIDX1 WRK-WIDX2) (1:1)
                                   =   "7"
           AND     TBL-ZAIW-TANICD(WRK-WIDX1 WRK-WIDX2)
                                   =   WRK-CONS-TANICD 
           AND     TBL-ZAIW-SRYKAISU(WRK-WIDX1 WRK-WIDX2)
                                   >   1
             IF   TBL-ZAIW-SRYCD(WRK-WIDX1 WRK-WIDX2) =
                  "700020000" OR "700040000" OR "700060000" OR
                  "700070000" OR "700090000" OR "700100000" OR
                  "700120000" OR "700130000" OR "700150000" OR
                  "700300000" OR "700320000" OR "700340000" OR
                  "700350000" OR "700720000" OR "700740000" OR
                  "700760000" OR "700770000" OR "700790000" OR
                  "700800000" OR "700820000" OR "700830000" OR
                  "700850000" OR "700860000" OR "700880000" OR
                  "700890000"
               GO  TO  4301-BUNKAKU-HEN1-EXT
             ELSE
               CONTINUE
             END-IF
           ELSE                            
               GO  TO  4301-BUNKAKU-HEN1-EXT  
           END-IF
      * 
      *    表示用編集（半角）
           MOVE    TBL-ZAIW-SRYKAISU (WRK-WIDX1 WRK-WIDX2)
                                   TO  WRK-SRYKAISU-X
      *    全角変換
           INITIALIZE              WRK-CHAR-HENKAN-AREA
           MOVE    WRK-SRYKAISU-X  TO  WRK-CHAR-X
           MOVE    3               TO  WRK-CHAR-CNT
           MOVE    1               TO  WRK-CHAR-FLG
           PERFORM 800-NUM-ZENKAKU-HENKAN-SEC
           STRING  WRK-CHAR-N      DELIMITED BY  SPACE
                   "分画"          DELIMITED  BY  SIZE
                                   INTO  WRK-BUNKAKU-N
           END-STRING
           COMPUTE WRK-BUNKAKUKETA =   WRK-CHAR-N-CNT  +  4
      *
           .
       4301-BUNKAKU-HEN1-EXT.
           EXIT.
      *
      *****************************************************************
      *    明細・明細桁　編集処理
      *****************************************************************
       4302-MEISAI-KETA-HEN1-SEC    SECTION.
      *
           MOVE    SPACE           TO  WRK-KOUI
                                       WRK-RECEHENNAME
                                       WRK-RECEHENTANINAME
           MOVE    ZERO            TO  WRK-RECEHENTANI
      *
           IF      TBL-ZAIW-RECEHENFLG (WRK-WIDX1 WRK-WIDX2) 
                                   =   "1"
               STRING    "（"          DELIMITED   BY  SIZE
                         TBL-ZAIW-NAME (WRK-WIDX1 WRK-WIDX2)
                                       DELIMITED   BY  SIZE
                                       INTO    WRK-RECEHENNAME
               END-STRING
               MOVE    WRK-RECEHENNAME TO
                                   TBL-ZAIW-NAME (WRK-WIDX1 WRK-WIDX2)
               COMPUTE TBL-ZAIW-YUKOKETA (WRK-WIDX1 WRK-WIDX2)  =
                       TBL-ZAIW-YUKOKETA (WRK-WIDX1 WRK-WIDX2)  +   1
           END-IF
      *
           IF  TBL-ZAIW-TANINAME (WRK-WIDX1 WRK-WIDX2)
                                   =   SPACE
      *        < 名称 >
               MOVE    TBL-ZAIW-NAME (WRK-WIDX1 WRK-WIDX2)
                                   TO  WRK-KOUI
      *        < 名称*2 >
               COMPUTE WRK-WCLM  =
                       TBL-ZAIW-YUKOKETA (WRK-WIDX1 WRK-WIDX2) *  2
           ELSE
      *
               IF   TBL-ZAIW-TANINAME (WRK-WIDX1 WRK-WIDX2)(1:2)
                                            =  "＊"
                   IF      TBL-ZAIW-RECEHENFLG (WRK-WIDX1 WRK-WIDX2) 
                                                =   "1"
                       MOVE    "）"    TO  
                               TBL-ZAIW-TANINAME   (WRK-WIDX1 WRK-WIDX2)
                       MOVE    2       TO  
                               TBL-ZAIW-TANIMOJISU (WRK-WIDX1 WRK-WIDX2)
                   ELSE
                       MOVE    SPACE   TO  
                              TBL-ZAIW-TANINAME (WRK-WIDX1 WRK-WIDX2)
                   END-IF
               ELSE
                   IF      TBL-ZAIW-RECEHENFLG (WRK-WIDX1 WRK-WIDX2) 
                                                =   "1"
                       COMPUTE WRK-RECEHENTANI =
                           TBL-ZAIW-TANIMOJISU (WRK-WIDX1 WRK-WIDX2)
                                           *   2
                       STRING    TBL-ZAIW-TANINAME (WRK-WIDX1 WRK-WIDX2)
                                                    (1:WRK-RECEHENTANI)
                                           DELIMITED   BY  SIZE
                             "）"          DELIMITED   BY  SIZE
                                           INTO    WRK-RECEHENTANINAME
                       END-STRING
                       MOVE    WRK-RECEHENTANINAME TO
                               TBL-ZAIW-TANINAME   (WRK-WIDX1 WRK-WIDX2)
                       COMPUTE
                           TBL-ZAIW-TANIMOJISU (WRK-WIDX1 WRK-WIDX2)  =
                           TBL-ZAIW-TANIMOJISU (WRK-WIDX1 WRK-WIDX2)
                                                                   +  1
                   END-IF
               END-IF
      *        名称の長さ
               COMPUTE   WRK-NAMEKETA  =
                         TBL-ZAIW-YUKOKETA (WRK-WIDX1 WRK-WIDX2)
                         *   2
      *        -----------------------------------
      *        １明細に数量単位まで収まるかどうか
      *        -----------------------------------
               EVALUATE    TRUE
               WHEN    WRK-NAMEKETA    >   50
      *            明細行
                   MOVE    TBL-ZAIW-NAME (WRK-WIDX1 WRK-WIDX2)
                                       TO  WRK-KOUI
                   MOVE    WRK-KOUI    TO  WRK-ZAI-NAME
                   PERFORM 4303-ZAITBL-TENSO-SEC
      *
                   MOVE    SPACE       TO  WRK-KOUI
                   MOVE    SPACE       TO  WRK-ZAI-NAME
                   COMPUTE WRK-NAMEKETA    =   WRK-NAMEKETA  -  50
      *   　       < 名称後半 + 空白 + 数量 + 単位 + 分画数>
                   STRING  TBL-ZAIW-NAME     (WRK-WIDX1 WRK-WIDX2)
                                             (51:WRK-NAMEKETA)
                                              DELIMITED  BY  SIZE
                           "  "               DELIMITED  BY  SIZE
                           WRK-SRYSURYOW      DELIMITED  BY  " "
                           TBL-ZAIW-TANINAME (WRK-WIDX1 WRK-WIDX2)
                                              DELIMITED  BY  " "
                           "  "               DELIMITED  BY  SIZE
                           WRK-BUNKAKU-N      DELIMITED  BY  " "
                           INTO               WRK-KOUI
                   END-STRING
                   COMPUTE   WRK-WCLM
                         =   ( WRK-SURYOKETA                  *   2 )
                         +   ( TBL-ZAIW-TANIMOJISU (WRK-WIDX1 WRK-WIDX2)
                                                              *   2 )
                         +   WRK-NAMEKETA  +  2
                   IF      WRK-BUNKAKUKETA        >   ZERO
                       COMPUTE   WRK-WCLM    =   WRK-WCLM         +
                                                 WRK-BUNKAKUKETA  +
                                                 2
                   END-IF                                             
      *
               WHEN    WRK-NAMEKETA    =   50
      *            数量・単位　次行印字処理
                   PERFORM 43021-SURYO-NEXTPRT-SEC
      *
               WHEN    WRK-NAMEKETA    <   50
      *            間の空白分を足し込む
                   COMPUTE   WRK-NAMEKETA  =   WRK-NAMEKETA   +   2
                   IF  WRK-NAMEKETA    <   50
      *                余白
                       COMPUTE   WRK-YOHAKU  =   50  -   WRK-NAMEKETA
      *                数量・分画数桁数
                       COMPUTE   WRK-WKETA   =
                         ( WRK-SURYOKETA                  * 2 )
                             +
                         ( TBL-ZAIW-TANIMOJISU (WRK-WIDX1 WRK-WIDX2)
                                                          * 2 )
                       IF      WRK-BUNKAKUKETA   >   ZERO
                           COMPUTE   WRK-WKETA   =   WRK-WKETA        +
                                                     WRK-BUNKAKUKETA  +
                                                     2
                       END-IF                                          
      *
                       IF  WRK-YOHAKU        >=  WRK-WKETA
      *                    < 名称 + 空白 + 数量 + 単位 + 空白 + 分画数>
                          STRING  TBL-ZAIW-NAME (WRK-WIDX1 WRK-WIDX2)
                                                (1:WRK-NAMEKETA)
                                                  DELIMITED  BY  SIZE
                                   WRK-SRYSURYOW  DELIMITED  BY  " "
                                   TBL-ZAIW-TANINAME
                                                 (WRK-WIDX1 WRK-WIDX2)
                                                  DELIMITED  BY  " "
                                   "  "           DELIMITED  BY  SIZE
                                   WRK-BUNKAKU-N  DELIMITED  BY  " "
                                   INTO           WRK-KOUI
                           END-STRING
      *                    < 名称*2 + 空白*2 + 数量 + 単位*2 + 空白 + 分画数>
                           COMPUTE   WRK-WCLM     =
                               ( TBL-ZAIW-YUKOKETA 
                                         (WRK-WIDX1 WRK-WIDX2)  * 2 )
                             + ( WRK-SURYOKETA                  * 2 )
                             + ( TBL-ZAIW-TANIMOJISU 
                                         (WRK-WIDX1 WRK-WIDX2)  * 2 )
                             +  2
                           IF      WRK-BUNKAKUKETA   >   ZERO
                               COMPUTE   WRK-WCLM   =   WRK-WCLM      +
                                                     WRK-BUNKAKUKETA  +
                                                     2
                           END-IF
                       ELSE
      *                    数量・単位　次行印字処理
                           PERFORM 43021-SURYO-NEXTPRT-SEC
                       END-IF
                   ELSE
      *                数量・単位　次行印字処理
                       PERFORM 43021-SURYO-NEXTPRT-SEC
                   END-IF
               END-EVALUATE
           END-IF
           MOVE    WRK-KOUI        TO  WRK-ZAI-NAME
      *
           .
       4302-MEISAI-KETA-HEN1-EXT.
           EXIT.
      *
      *****************************************************************
      *    数量・単位　次行編集処理
      *****************************************************************
       43021-SURYO-NEXTPRT-SEC    SECTION.
      *
      *    < 名称 >
           MOVE    TBL-ZAIW-NAME (WRK-WIDX1 WRK-WIDX2)
                                      TO  WRK-KOUI
           MOVE    WRK-KOUI           TO  WRK-ZAI-NAME
           PERFORM 4303-ZAITBL-TENSO-SEC
      *
           MOVE    SPACE              TO  WRK-KOUI
           MOVE    SPACE              TO  WRK-ZAI-NAME
      *    < 空白 + 数量 + 単位 + 空白 + 分画数>
           STRING  "    "             DELIMITED  BY  SIZE
                   WRK-SRYSURYOW      DELIMITED  BY  " "
                   TBL-ZAIW-TANINAME (WRK-WIDX1 WRK-WIDX2)
                                      DELIMITED  BY  " "
                   "  "               DELIMITED  BY  SIZE
                   WRK-BUNKAKU-N      DELIMITED  BY  " "
                   INTO               WRK-KOUI
           END-STRING
           COMPUTE   WRK-WCLM
                 =   ( WRK-SURYOKETA                  *   2 )
                 +   ( TBL-ZAIW-TANIMOJISU (WRK-WIDX1 WRK-WIDX2)
                                                      *   2 )
                 +   4
           IF      WRK-BUNKAKUKETA   >   ZERO
               COMPUTE   WRK-WCLM   =   WRK-WCLM         +
                                        WRK-BUNKAKUKETA  +
                                        2
           END-IF
      *
           .
       43021-SURYO-NEXTPRT-EXT.
           EXIT.
      *
      *****************************************************************
      *    剤・診療行為テーブル編集処理
      *****************************************************************
       4303-ZAITBL-TENSO-SEC    SECTION.
      *
           ADD     1                 TO  WRK-TBLMAX
      ***  IF  WRK-TBLMAX           >   100
           IF  WRK-TBLMAX           >   300
               DISPLAY "*** ORCR0910 TBL-ZAI INDEX[300] OVER  ***"
           ELSE
               MOVE    WRK-ZAI       TO  TBL-ZAI (WRK-TBLMAX)
               IF      RECE84-SRYKBN      =  "33"
                  MOVE    IDX1       TO  TBL-ZAI-IDX(WRK-TBLMAX)
                  MOVE    TBL-ZAIW-SRYCD(IDX1 IDX2)  
                                     TO  TBL-ZAI-SRYCD(WRK-TBLMAX)
               END-IF
               MOVE    SPACE         TO  WRK-ZAI
           END-IF
      *
           .
       4303-ZAITBL-TENSO-EXT.
           EXIT.
      *
      *****************************************************************
      *    点数回数セット処理
      *****************************************************************
       440-TNSKAI-SET-SEC          SECTION.
      *
      *---- (01.00.05) LINE ADD START --------------------------------
      ***  IF   RECE84-ZAIKBN    =    1 OR 2
           IF   RECE84-ZAIKBN    =    2
              CONTINUE
           ELSE
              IF ((FLG-COMMENT        =    1                  ) AND
                  (RECE84-SRYKBN      =   "99"                ))
              OR ((FLG-COMMENT2       =    1                  ) AND
                  (RECE84-SRYKBN      =   "99"                ))
              OR ((FLG-COMMENT2       =    1                  ) AND
                  (RECE84-SRYKBN      =   "11"                ) AND
                  (FLG-GENTENCD-ARI   =   ZERO                ))
              OR ((FLG-COMMENT3       =    1                  ) AND
                  (RECE84-SRYKBN      =   "11"                ) AND
                  (WRK-TENSUGOKEI     =   ZERO                ) AND
                  (FLG-GENTENCD-ARI   =   ZERO                ))
              OR ((FLG-COMMENT3       =    1                  ) AND
                  (RECE84-SRYKBN      NOT =  "11"             ) AND
                  (RECE84-ZAITEN      =   ZERO                ) AND
                  (RECE84-TOYAKUFLG   =   SPACE               ) AND
                  (RECE84-YKZKISAIFLG =   SPACE               ))
              OR ( RECE84-SAFLG   NOT =   SPACE                )
              OR ( RECE84-TOYAKUFLG   =   "2" OR "4"           )
              OR ( RECE84-ZAIKBN      =    1                   )
                  GO  TO  440-TNSKAI-SET-EXT
              END-IF
           END-IF
      *---- (01.00.05) LINE ADD END   --------------------------------
      *         
           MOVE    WRK-TNSKAI-ST     TO  IDA
           COMPUTE   IDA             =   IDA     -   2
           MOVE    WRK-TNSKAI-KETA   TO  IDB
      *****MOVE    WRK-TBLMAX       TO  IDY
      *
      *    最終明細に点数・回数を印字するスペース有り
           MOVE    TBL-ZAI (IDY)(3:)     TO  WRK-ZAI-NAME
           IF  WRK-ZAI-NAME (IDA:)       =   SPACE
               COMPUTE   WRK-TNSKAI-ST   =   22
                                         -   WRK-TNSKAI-KETA
               ADD     1                 TO  WRK-TNSKAI-ST
               MOVE    WRK-TNSKAI (WRK-TNSKAI-ST:IDB)
                                         TO  WRK-ZAI-TNSKAI
                                                    (WRK-TNSKAI-ST:)
               MOVE    WRK-ZAI-NAME      TO  TBL-ZAI (IDY)(3:)
           ELSE
      *    最終明細に点数・回数を印字するスペース無し
               ADD     1                 TO  IDY
      ***      IF  IDY                   >   100
               IF  IDY                   >   300
                   DISPLAY "*#* ORCR0910 TBL-ZAI INDEX[300] OVER  ***"
               ELSE
                   MOVE    SPACE         TO  WRK-ZAI
                   MOVE    WRK-TNSKAI    TO  WRK-ZAI-TNSKAI
                   MOVE    WRK-ZAI       TO  TBL-ZAI (IDY)
                   IF      RECE84-SRYKBN      =  "33"
                      MOVE    TBL-ZAI-IDX(WRK-TBLMAX)
                                         TO  TBL-ZAI-IDX(IDY)
                      MOVE    TBL-ZAI-SRYCD(WRK-TBLMAX)
                                         TO  TBL-ZAI-SRYCD(IDY)
                   END-IF
                   MOVE    SPACE         TO  WRK-ZAI
               END-IF
           END-IF
      *
           .
       440-TNSKAI-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    レセプト０６出力処理
      *****************************************************************
       450-RECE86-OUT-SEC    SECTION.
      *
           PERFORM   VARYING   IDY   FROM    1   BY  1
                     UNTIL     IDY   >   WRK-TBLMAX
      *
               ADD     1                 TO  WRK-LINENUM
               MOVE    SPACE             TO  RECE86-REC
               INITIALIZE                    RECE86-REC
               MOVE    RECE82-KEY        TO  RECE86-KEY
               MOVE    RECE84-ZAINUM     TO  RECE86-ZAINUM
               MOVE    WRK-LINENUM       TO  RECE86-LINENUM
               MOVE    WRK-TBLMAX        TO  RECE86-ZAIMEISAISU
               MOVE    RECE84-PTNUM      TO  RECE86-PTNUM
               MOVE    RECE84-SRYKA      TO  RECE86-SRYKA
               MOVE    RECE84-SRYKBN     TO  RECE86-SRYKBN
               MOVE    RECE84-ZAISKBKBN  TO  RECE86-ZAISKBKBN
               IF    RECE84-ZAISKBKBN  =   "1" OR "2" OR "4"
                   MOVE   RECE84-ZAINUM  TO  RECE86-ZAISKBBAN
               END-IF
               IF    RECE84-ZAISKBKBN  =   "6"
               AND   RECE84-NYUSAIFLG  =   "1"
                   MOVE   99999999       TO  RECE86-ZAISKBBAN
               END-IF
               MOVE    TBL-ZAI (IDY)     TO  RECE86-TEKIYO-INF
      *
               IF      RECE84-SRYKBN      =  "33"
      *
                IF     RECE82-SRYYM      >=  "200804"
                  IF  (WRK-SRYSYUKBN      =  "330" OR "332"            )
                  AND (TBL-ZAI-SRYCD(1)   =  "130003710" OR "130003810")
                          MOVE   1              TO  RECE86-TENTEKIKBN
                  END-IF
                  IF  (WRK-SRYSYUKBN      =  "330" OR "332"            )
                  AND (TBL-ZAI-SRYCD(1)   =  "130000110" OR "130000210"
                                          OR "130000310"               )
                  AND (RECE86-TENTEKIKBN  =  ZERO                      )
                          MOVE   2              TO  RECE86-TENTEKIKBN
                  END-IF
                  IF  (WRK-SRYSYUKBN      =  "330" OR "332")
                  AND (RECE86-TENTEKIKBN  =  ZERO          )
                    IF  (RECE84-ZAITEN  NOT =  ZERO          )
                    AND (RECE84-YKZTEN      =  ZERO          )
                    AND (RECE84-KIZAITEN    =  ZERO          )
                    AND (RECE84-JIHOKBN     =  SPACE         )
                          MOVE   3              TO  RECE86-TENTEKIKBN
                    END-IF
                    IF  (RECE84-HOKATU-ZAITEN  NOT =  ZERO   )
                    AND (RECE84-HOKATU-YKZTEN      =  ZERO   )
                    AND (RECE84-HOKATU-KIZAITEN    =  ZERO   )
                    AND (RECE84-JIHOKBN        NOT =  SPACE  )
                          MOVE   3              TO  RECE86-TENTEKIKBN
                    END-IF
                  END-IF
      *
                  IF  (WRK-SRYSYUKBN      =  "340")
                  AND (TBL-ZAI-SRYCD(1)   =  "130004410" OR "130008510"
                                          OR "130004670"               )
                    IF  (RECE84-YKZTEN      =  ZERO )
                    AND (RECE84-KIZAITEN    =  ZERO )
                    AND (RECE84-JIHOKBN     =  SPACE)
                          MOVE   4              TO  RECE86-TENTEKIKBN
                    END-IF
                    IF  (RECE84-HOKATU-YKZTEN      =  ZERO )
                    AND (RECE84-HOKATU-KIZAITEN    =  ZERO )
                    AND (RECE84-JIHOKBN        NOT =  SPACE)
                          MOVE   4              TO  RECE86-TENTEKIKBN
                    END-IF
                  END-IF
                  IF  (WRK-SRYSYUKBN      =  "340")
                  AND (TBL-ZAI-SRYCD(1)   =  "130000110" OR "130000210"
                                          OR "130000310"               )
                  AND (RECE86-TENTEKIKBN  =  ZERO )
                    IF  (RECE84-YKZTEN      =  ZERO )
                    AND (RECE84-KIZAITEN    =  ZERO )
                    AND (RECE84-JIHOKBN     =  SPACE)
                          MOVE   5              TO  RECE86-TENTEKIKBN
                    END-IF
                    IF  (RECE84-HOKATU-YKZTEN    =  ZERO )
                    AND (RECE84-HOKATU-KIZAITEN  =  ZERO )
                    AND (RECE84-JIHOKBN      NOT =  SPACE)
                          MOVE   5              TO  RECE86-TENTEKIKBN
                    END-IF
                  END-IF
                ELSE
                  IF  (WRK-SRYSYUKBN      =  "330" OR "332"            )
                  AND (TBL-ZAI-SRYCD(1)   =  "130003710" OR "130003810"
                                          OR "130700310" OR "130700410"
                                          OR "130702910" OR "130701010"
                                          OR "130702810"               )
                          MOVE   1              TO  RECE86-TENTEKIKBN
                  END-IF
                  IF  (WRK-SRYSYUKBN      =  "330" OR "332"            )
                  AND (TBL-ZAI-SRYCD(1)   =  "130000110" OR "130000210"
                                          OR "130000310" OR "130008270"
                                          OR "130701270" OR "130004570"
                                          OR "130009670" OR "130701370"
                                          OR "130010570" OR "130702670"
                                          OR "130010670" OR "130702770"
                                          OR "130011070"               )
                  AND (RECE86-TENTEKIKBN  =  ZERO                      )
                          MOVE   2              TO  RECE86-TENTEKIKBN
                  END-IF
                  IF  (WRK-SRYSYUKBN      =  "330" OR "332")
                  AND (RECE84-ZAITEN  NOT =  ZERO          )
                  AND (RECE84-YKZTEN      =  ZERO          )
                  AND (RECE84-KIZAITEN    =  ZERO          )
                  AND (RECE86-TENTEKIKBN  =  ZERO          )
                          MOVE   3              TO  RECE86-TENTEKIKBN
                  END-IF
      *
                  IF  (WRK-SRYSYUKBN      =  "340")
                  AND (TBL-ZAI-SRYCD(1)   =  "130004410" OR "130008510"
                                          OR "130004670"               )
                  AND (RECE84-YKZTEN      =  ZERO )
                  AND (RECE84-KIZAITEN    =  ZERO )
                          MOVE   4              TO  RECE86-TENTEKIKBN
                  END-IF
                  IF  (WRK-SRYSYUKBN      =  "340")
                  AND (TBL-ZAI-SRYCD(1)   =  "130000110" OR "130000210"
                                          OR "130000310" OR "130008270"
                                          OR "130701270" OR "130004570"
                                          OR "130009670" OR "130701370"
                                          OR "130010570" OR "130702670"
                                          OR "130010670" OR "130702770"
                                          OR "130011070"               )
                  AND (RECE84-YKZTEN      =  ZERO )
                  AND (RECE84-KIZAITEN    =  ZERO )
                  AND (RECE86-TENTEKIKBN  =  ZERO )
                          MOVE   5              TO  RECE86-TENTEKIKBN
                  END-IF
                END-IF
      *
                IF   WRK-SRYSYUKBN   =  "330" OR "331" OR "332" OR
                                        "350" OR "352"
                  IF   (RECE84-YKZTEN             NOT =  ZERO )
                  AND  (RECE84-JIHOKBN                =  SPACE)
                  AND  (RECE86-TENTEKIKBN             =  ZERO )
                          MOVE   6              TO  RECE86-TENTEKIKBN
                  END-IF
                  IF   (RECE84-HOKATU-YKZTEN      NOT =  ZERO )
                  AND  (RECE84-JIHOKBN            NOT =  SPACE)
                  AND  (RECE86-TENTEKIKBN             =  ZERO )
                          MOVE   6              TO  RECE86-TENTEKIKBN
                  END-IF
                  IF   (RECE84-KIZAITEN           NOT =  ZERO )
                  AND  (RECE84-JIHOKBN                =  SPACE)
                  AND  (RECE86-TENTEKIKBN             =  ZERO )
                          MOVE   7              TO  RECE86-TENTEKIKBN
                  END-IF
                  IF   (RECE84-HOKATU-KIZAITEN    NOT =  ZERO )
                  AND  (RECE84-JIHOKBN            NOT =  SPACE)
                  AND  (RECE86-TENTEKIKBN             =  ZERO )
                          MOVE   7              TO  RECE86-TENTEKIKBN
                  END-IF
                END-IF
      *
                IF   WRK-SRYSYUKBN   =  "340"
                  IF   (RECE84-ZAITEN             NOT =  ZERO )
                  AND  (RECE84-JIHOKBN                =  SPACE)
                  AND  (RECE86-TENTEKIKBN             =  ZERO )
                          MOVE   8              TO  RECE86-TENTEKIKBN
                  END-IF
                  IF   (RECE84-HOKATU-ZAITEN      NOT =  ZERO )
                  AND  (RECE84-JIHOKBN            NOT =  SPACE)
                  AND  (RECE86-TENTEKIKBN             =  ZERO )
                          MOVE   8              TO  RECE86-TENTEKIKBN
                  END-IF
                END-IF
      *
      **********以下コメントのみ分
                IF   (RECE86-TENTEKIKBN       =  ZERO )
                         MOVE   9              TO  RECE86-TENTEKIKBN
                END-IF
      *
               END-IF
      *
               IF    ( FLG-COMMENT2     =   1   )               
               AND   ( RECE84-SRYKBN    =  "11" )               
                   MOVE  SPACE           TO  RECE86-SRYKBN              
               END-IF        
               IF      IDY              =   1
                 IF       (WRK-ZAIW-SRYCD1(1:1)  =   "6"  OR  "7")
                 OR       (WRK-ZAIW-SRYCD1(1:3)  =   "059"       )
                 OR       (RECE84-SAFLG      NOT =   SPACE       )
                 OR       (RECE84-ZAIKBN         =    1          )
                     MOVE    ALL "9"            TO
                                            WRK-ZAIW-CDKBN-AREA
                     IF   (RECE84-SAFLG      NOT =   SPACE)
                     OR   (RECE84-ZAIKBN         =    1   )
                         MOVE    999999999   TO  WRK-ZAIW-KOUHYOJYUNNUM
                     ELSE
                       IF    WRK-ZAIW-SRYCD1(1:1)  =   "6"
                         MOVE    999999997   TO  WRK-ZAIW-KOUHYOJYUNNUM
                       ELSE
                         MOVE    999999998   TO  WRK-ZAIW-KOUHYOJYUNNUM
                       END-IF
                     END-IF
                     IF  ( RECE84-SRYKBN     =  "13" OR "14" )
                     OR  ((RECE84-SRYKBN     =  "80"        ) AND
                          (RECE84-OKIKAEFLG  =  "2"         ))
                       MOVE    9    TO    WRK-ZAIW-PRIORITYKBN
                     END-IF
                 ELSE
                   IF  ((RECE84-SRYKBN         =  "13" OR "14") AND
                        (WRK-ZAIW-SRYCD1   NOT =   SPACE      ))
                   OR  ((RECE84-SRYKBN         =  "80"        ) AND
                        (RECE84-OKIKAEFLG      =  "2"         ) AND
                        (WRK-ZAIW-SRYCD1   NOT =   SPACE      ))
                     MOVE      1    TO    WRK-ZAIW-PRIORITYKBN
                     IF    WRK-ZAIW-KOKUJISKBKBN2  =   7
                     AND   WRK-ZAIW-CDKBN-KBN      =  "A"
                       MOVE    2    TO    WRK-ZAIW-PRIORITYKBN
                     END-IF
                   END-IF
                 END-IF
                 IF   RECE82-SRYYM   <=   "201306"
                   EVALUATE    WRK-ZAIW-SRYCD1
                     WHEN     "101800175"
                       MOVE      ALL "9"     TO  WRK-ZAIW-CDKBN-AREA
                       MOVE      999999982   TO  WRK-ZAIW-KOUHYOJYUNNUM
                     WHEN     "101800180"
                       MOVE      ALL "9"     TO  WRK-ZAIW-CDKBN-AREA
                       MOVE      999999983   TO  WRK-ZAIW-KOUHYOJYUNNUM
                     WHEN     "101800330"
                       MOVE      ALL "9"     TO  WRK-ZAIW-CDKBN-AREA
                       MOVE      999999984   TO  WRK-ZAIW-KOUHYOJYUNNUM
                     WHEN     "101800340"
                       MOVE      ALL "9"     TO  WRK-ZAIW-CDKBN-AREA
                       MOVE      999999985   TO  WRK-ZAIW-KOUHYOJYUNNUM
                   END-EVALUATE
                 ELSE
                   IF  WRK-ZAIW-SRYCD1(1:3) = "101"
                   AND WRK-ZAIW-CDKBN-KBN   = SPACE
                       MOVE      ALL "9"     TO  WRK-ZAIW-CDKBN-AREA
                       MOVE  WRK-ZAIW-SRYCD1 TO  WRK-ZAIW-KOUHYOJYUNNUM
                   END-IF
                 END-IF
               END-IF
               MOVE    WRK-ZAIW-PRIORITYKBN
                                           TO  RECE86-PRIORITYKBN
      *
               MOVE    WRK-ZAIW-CDKBN-SYO  TO  RECE86-CDKBN-SYO
               MOVE    WRK-ZAIW-CDKBN-BU   TO  RECE86-CDKBN-BU
               MOVE    WRK-ZAIW-CDKBN-KBN  TO  RECE86-CDKBN-KBN
               MOVE    WRK-ZAIW-CDKBN-KBNNUM
                                           TO  RECE86-CDKBN-KBNNUM
               MOVE    WRK-ZAIW-CDKBN-KBNNUM-EDA
                                           TO  RECE86-CDKBN-KBNNUM-EDA
               MOVE    WRK-ZAIW-CDKBN-KOUBAN
                                           TO  RECE86-CDKBN-KOUBAN
               MOVE    WRK-ZAIW-KOUHYOJYUNNUM
                                           TO  RECE86-KOUHYOJUNNUM
      *
               IF    RECE84-GAZOGEN = "1"
                  MOVE    202              TO  RECE86-CDKBN-KBNNUM
                  MOVE    999              TO  RECE86-CDKBN-KOUBAN
                  MOVE    ZERO             TO  RECE86-KOUHYOJUNNUM
               END-IF
      *
               IF    RECE84-JIHOKBN = SPACE
                  MOVE    RECE84-ZAITEN         TO  RECE86-ZAITEN
               ELSE
                  MOVE    RECE84-HOKATU-ZAITEN  TO  RECE86-ZAITEN
               END-IF
      *
               IF      RECE84-SRYKBN     =   "60" OR "64"
                       MOVE   WRK-ZAIW-KNSJISKBN
                                             TO  RECE86-KNSJISKBN
               ELSE
                       MOVE   ZERO           TO  RECE86-KNSJISKBN
               END-IF
      *
               MOVE       SPACE          TO  RECE86-SRYSYUKBN
               MOVE       ZERO           TO  RECE86-DAY
               MOVE       ZERO           TO  RECE86-DAY2
               EVALUATE  RECE84-SRYKBN
                 WHEN      "21"
                 WHEN      "22"
                 WHEN      "23"
                 WHEN      "33"
                 WHEN      "40"
                   PERFORM   VARYING   IDW3   FROM    1   BY  1
                              UNTIL    IDW3   >      31
                       IF    RECE84-KAISU(1 IDW3)    NOT =   ZERO
                          MOVE   IDW3    TO  RECE86-DAY
                          MOVE    31     TO  IDW3
                       END-IF
                   END-PERFORM
                 WHEN      "50"
                 WHEN      "54"
                   MOVE   WRK-SRYSYUKBN  TO  RECE86-SRYSYUKBN
                   PERFORM   VARYING   IDW3   FROM    1   BY  1
                              UNTIL    IDW3   >      31
                       IF    RECE84-KAISU(1 IDW3)    NOT =   ZERO
                          MOVE   IDW3    TO  RECE86-DAY
                          MOVE    31     TO  IDW3
                       END-IF
                   END-PERFORM
                 WHEN      "70"
                   IF    RECE84-GAZOGEN = "1"
                     PERFORM   VARYING   IDW3   FROM    1   BY  1
                                UNTIL    IDW3   >      31
                       IF    RECE84-KAISU(1 IDW3)    NOT =   ZERO
                          MOVE   IDW3    TO  RECE86-DAY2
                          MOVE    31     TO  IDW3
                       END-IF
                     END-PERFORM
                   END-IF
                 WHEN      "99"
                   MOVE   WRK-SRYSYUKBN  TO  RECE86-SRYSYUKBN
               END-EVALUATE
      *
               MOVE    RECE84-SAFLG      TO  RECE86-SAFLG
               MOVE    RECE84-ZAIKBN     TO  RECE86-ZAIKBN
      *
               MOVE    RECE84-OKIKAEFLG  TO  RECE86-OKIKAEFLG
      *
               MOVE    RECE84-JIHOKBN    TO  RECE86-JIHOKBN
      *
               WRITE   RECE86-REC
      *
               IF  STS-RECE86        NOT =   ZERO
                   DISPLAY "*** ORCR0910 RECE86 WRITE ERR  ***"
                   DISPLAY "*** RECE86-SRYKBN ***" RECE86-SRYKBN
                   DISPLAY "*** RECE86-ZAINUM ***" RECE86-ZAINUM
                   MOVE   WRK-TBLMAX     TO  IDY
                   MOVE    2             TO  SPA-STS
               ELSE
                   ADD     1             TO  CNT-RECE86
               END-IF
           END-PERFORM
      *     
           INITIALIZE                    TBL-ZAI-AREA
      *
           .
       450-RECE86-OUT-EXT.
           EXIT.
      *
      *****************************************************************
      *    エラー出力処理
      *****************************************************************
       500-ERR-HENSYU-SEC                SECTION.
      *
           OPEN    INPUT   RECEERR-FILE
           IF      STS-RECEERR         =   ZERO
               CLOSE   RECEERR-FILE
           ELSE
               OPEN    OUTPUT              RECEERR-FILE
      *
               MOVE    WRK-RECEERR         TO  RECEERR-REC
               WRITE   RECEERR-REC
               CLOSE   RECEERR-FILE
      *         
               IF      WRK-PARA-SHELLID    NOT =   "RECEPTX"
      *            ジョブ管理終了処理
                   MOVE    "JBE"           TO  SJOBKANRI-MODE
                   INITIALIZE                  JOBKANRI-REC
                   MOVE    WRK-RECEERR     TO  JOB-YOBI
                   MOVE    "9999"          TO  JOB-ERRCD
grpsys             PERFORM 900-CALL-ORCSJOB-SEC
               END-IF
           END-IF
      *                             
           MOVE    1                   TO  FLG-END
           MOVE    2                   TO  SPA-STS
      *
           .
       500-ERR-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    エラー時終了処理
      *****************************************************************
       500-COBABORT-SEC                SECTION.
      *
           CALL    "cobabort"          USING   WRK-ERRMSG
      *
           .
       500-COBABORT-EXT.
           EXIT.
      *
      *****************************************************************
      *    処理中止処理
      *****************************************************************
       500-CANCEL-HENSYU-SEC                SECTION.
      *
           IF      WRK-PARA-SHELLID    =   "RECEPTX"
               CONTINUE
           ELSE
               MOVE    "CHK"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
grpsys         PERFORM 900-CALL-ORCSJOB-SEC
               IF    ( SJOBKANRI-RETURN
                                       =   ZERO )
               AND   ( JOB-STOPFLG     =   1    )
                   MOVE    "処理が中止されました"
                                           TO  WRK-RECEERR
      *
                   OPEN    INPUT   RECEERR-FILE
                   IF      STS-RECEERR         =   ZERO
                       CLOSE   RECEERR-FILE
                   ELSE
                       OPEN    OUTPUT              RECEERR-FILE
      *    
                       MOVE    WRK-RECEERR         TO  RECEERR-REC
                       WRITE   RECEERR-REC
                       CLOSE   RECEERR-FILE
      *         
      *                ジョブ管理終了処理
                       MOVE    "CAN"           TO  SJOBKANRI-MODE
                       INITIALIZE                  JOBKANRI-REC
                       MOVE    WRK-RECEERR     TO  JOB-YOBI
                       MOVE    "8888"          TO  JOB-ERRCD
                       MOVE    2               TO  JOB-STOPFLG
grpsys                 PERFORM 900-CALL-ORCSJOB-SEC
                   END-IF
      *                             
                   MOVE    1                   TO  FLG-END
                   MOVE    2                   TO  SPA-STS
               END-IF
           END-IF
           .
       500-CANCEL-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    終了　　処理
      *****************************************************************
       300-END-SEC                 SECTION.
      *
      *    ファイルＣＬＯＳＥ処理
           CLOSE                       RECE82-FILE
           CLOSE                       RECE84-FILE
           CLOSE                       RECE85-FILE
           CLOSE                       RECE86-FILE     
      *
           IF      WRK-PARA-SHELLID    NOT =   "RECEPTX"
      *        ステップ管理終了処理
               MOVE    "STE"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
grpsys         PERFORM 900-CALL-ORCSJOB-SEC
      *
               PERFORM 900-DBDISCONNECT-SEC
           END-IF
      *
           DISPLAY "RECE82  IN  " CNT-RECE82   
           DISPLAY "RECE86  CNT " CNT-RECE86 
           DISPLAY "*** ORCR0910 END ***"
      *
           .
       300-END-EXT.
           EXIT.
      *
      *****************************************************************
      *    数値半角→全角変換処理
      *****************************************************************
       800-NUM-ZENKAKU-HENKAN-SEC    SECTION.
      *
      *    FLG=1のとき空白は編集しない
      *
           MOVE    1                   TO  WRK-CHAR-IDX
           MOVE    ZERO                TO  WRK-CHAR-N-CNT
      *     
           PERFORM VARYING     IDZ     FROM    1   BY  1
                   UNTIL       IDZ     >   WRK-CHAR-CNT
               IF      WRK-CHAR-FLG        =   1
               AND     WRK-CHAR-X (IDZ:1)  =   SPACE
                   CONTINUE
               ELSE        
                   IF    WRK-CHAR-X (IDZ:1)  IS  NUMERIC
                       IF      WRK-CHAR-X (IDZ:1)  =   "0"
                           MOVE    TBL-SUJI (10)
                                       TO  WRK-CHAR-N (WRK-CHAR-IDX:)
                       ELSE
                           MOVE    WRK-CHAR-X (IDZ:1)  TO  WRK-NUM
                           MOVE    TBL-SUJI (WRK-NUM)
                                       TO  WRK-CHAR-N (WRK-CHAR-IDX:)
                       END-IF                    
                   ELSE
                       MOVE    "  "    TO  WRK-CHAR-N (WRK-CHAR-IDX:)
                   END-IF
                   ADD     2           TO  WRK-CHAR-IDX
                                           WRK-CHAR-N-CNT
               END-IF    
           END-PERFORM
      *
           .
       800-NUM-ZENKAKU-HENKAN-EXT.
           EXIT.
      *
      *****************************************************************
      *    点数・回数編集処理
      *****************************************************************
       810-TENSUKAI-HEN-SEC    SECTION.
      *    点数編集
           INITIALIZE                      WRK-CHAR-HENKAN-AREA
           MOVE    WRK-ZAITENX         TO  WRK-CHAR-X
           MOVE    7                   TO  WRK-CHAR-CNT
           PERFORM 800-NUM-ZENKAKU-HENKAN-SEC
           MOVE    WRK-CHAR-N          TO  WRK-ZAITEN
      *    回数編集
           INITIALIZE                      WRK-CHAR-HENKAN-AREA
           MOVE    WRK-ZAIKAISUX       TO  WRK-CHAR-X
           MOVE    3                   TO  WRK-CHAR-CNT
           PERFORM 800-NUM-ZENKAKU-HENKAN-SEC
           MOVE    WRK-CHAR-N          TO  WRK-ZAIKAISU
           MOVE    "×"                TO  WRK-X
      *
      *    薬剤料逓減・未実施減算のとき点数のまえに▲を表示
           IF      FLG-TEIGEN          =   1
               PERFORM VARYING IDZ FROM    13  BY  -2
                       UNTIL   IDZ <       3
                   IF      WRK-ZAITEN (IDZ:2)  =   SPACE
                       MOVE    "▲"        TO  WRK-ZAITEN (IDZ:2)
                       MOVE    1           TO  IDZ
                   END-IF
               END-PERFORM
           END-IF
      *
      *
      *    必要有効桁数（点数ZZZZZZZ X 数量ZZZ の全角の桁数）
           MOVE    ZERO                TO  WRK-TNSKAI-KETA
           PERFORM VARYING   IDZ     FROM    1   BY  1
                   UNTIL     IDZ     >       22
               IF      WRK-TNSKAI (IDZ:1)      NOT =   SPACE
                   COMPUTE WRK-TNSKAI-KETA   =   22  -  IDZ     +   1
                   MOVE    22                  TO  IDZ
               END-IF 
           END-PERFORM
      *    開始位置を取得
           COMPUTE   WRK-TNSKAI-ST     =   ( 50  -  WRK-TNSKAI-KETA )
                                       +   1
      *
           .
       810-TENSUKAI-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    レセプト０２データ順検索処理
      *****************************************************************
       990-RECE82-READ-SEC           SECTION.
      *
           READ    RECE82-FILE         NEXT
               AT  END
                   MOVE    1           TO  FLG-END
                   MOVE    2           TO  SPA-STS
               NOT AT  END
                   ADD     1           TO  CNT-RECE82
      *
                   DISPLAY "RECE82  PTNUM=" RECE82-PTNUM
      *
                   IF    (WRK-PARA-RECEKBN       =  "4")
                   AND   (WRK-PARA-JIBAIPRTKBN   =  "2")
                       GO  TO  990-RECE82-READ-SEC
                   END-IF
      *
           END-READ
      *
           .
       990-RECE82-READ-EXT.
           EXIT.
      *
      ****************************************************************
      *    レセプト０４データ読み
      ****************************************************************
       990-RECE84-NEXT-SEC           SECTION.
      *
           READ    RECE84-FILE       NEXT
               AT  END
                   MOVE    1         TO  FLG-RECE84
               NOT AT  END
                   MOVE    ZERO      TO  FLG-RECE84
                   IF  ( RECE84-MEISAISU       =   ZERO  )
                   OR  ((WRK-PARA-PRTKBN       =   "1"  ) AND
                        (RECE84-TOYAKUFLG  NOT =   SPACE))
                       GO  TO  990-RECE84-NEXT-SEC
                   END-IF       
                   ADD     1         TO  CNT-IN-CANCEL 
           END-READ
      *
           .
       990-RECE84-NEXT-EXT.
           EXIT.
      *
      ****************************************************************
      *    レセプト０５データ読み
      ****************************************************************
       990-RECE85-NEXT-SEC           SECTION.
      *
           READ    RECE85-FILE       NEXT
               AT  END
                   MOVE    1         TO  FLG-RECE85
               NOT AT  END
                   MOVE    ZERO      TO  FLG-RECE85
                   ADD     1         TO  CNT-IN-CANCEL 
           END-READ
      *
           .
       990-RECE85-NEXT-EXT.
           EXIT.
      *
      *****************************************************************
      *    ジョブ管理ＤＢ制御処理
      *****************************************************************
       900-CALL-ORCSJOB-SEC            SECTION.
      *
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
grpsys     MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA 
                                   JOBKANRI-REC
                                   SPA-AREA
           .
       900-CALL-ORCSJOB-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢＳＥＬＥＣＴ処理
      *****************************************************************
       900-DBSELECT-SEC                SECTION.
      *
           MOVE    "DBSELECT"          TO  MCP-FUNC
           PERFORM 900-ORCDBMAIN-SEC
           IF      MCP-RC              =   ZERO
               CONTINUE
           ELSE
               MOVE    SPACE               TO  WRK-ERR-AREA
               INITIALIZE                      WRK-ERR-AREA
               MOVE    MCP-RC              TO  WRK-MCP-RC
      *
               STRING  "ＤＢ読み込みエラー　TABLE="
                                           DELIMITED   BY  SIZE
                       MCP-TABLE           DELIMITED   BY  SPACE
                       " RC="              DELIMITED   BY  SIZE
                       WRK-MCP-RC          DELIMITED   BY  SIZE
                                           INTO    WRK-RECEERR
               END-STRING
               PERFORM 500-ERR-HENSYU-SEC
      *
               STRING  "ORCR0460 select err TABLE="
                                           DELIMITED   BY  SIZE
                       MCP-TABLE           DELIMITED   BY  SPACE
                       " PATHNAME="        DELIMITED   BY  SIZE
                       MCP-PATHNAME        DELIMITED   BY  SPACE
                       " RC="              DELIMITED   BY  SIZE
                       WRK-MCP-RC          DELIMITED   BY  SIZE
                       LOW-VALUE           DELIMITED   BY  SIZE
                                           INTO    WRK-ERRMSG
               END-STRING
               PERFORM 500-COBABORT-SEC
           END-IF
           .
       900-DBSELECT-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢＦＥＴＣＨ処理
      *****************************************************************
       900-DBFETCH-SEC                SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
grpsys     PERFORM 900-ORCDBMAIN-SEC
      *
           .
       900-DBFETCH-EXT.
           EXIT.
      *
      *****************************************************************
      *    テーブルクローズ処理
      *****************************************************************
       900-CLOSE-SEC               SECTION.
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
grpsys     PERFORM 900-ORCDBMAIN-SEC
      *
           .
       900-CLOSE-EXT.
           EXIT.
      *
      *****************************************************************
      *    テーブルアクセス処理
      *****************************************************************
       900-ORCDBMAIN-SEC               SECTION.
      *
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           .
      *
       900-ORCDBMAIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢ　オープン処理
      *****************************************************************
       120-DBOPEN-SEC                SECTION.
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBOPEN"            TO  MCP-FUNC
grpsys     PERFORM 900-ORCDBMAIN-SEC
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBSTART"           TO  MCP-FUNC
grpsys     PERFORM 900-ORCDBMAIN-SEC
           .
       120-DBOPEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢ　クローズ処理
      *****************************************************************
       900-DBDISCONNECT-SEC            SECTION.
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBCOMMIT"          TO  MCP-FUNC
grpsys     PERFORM 900-ORCDBMAIN-SEC
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBDISCONNECT"      TO  MCP-FUNC
grpsys     PERFORM 900-ORCDBMAIN-SEC
           .
       900-DBDISCONNECT-EXT.
           EXIT.
